; RED MOON. ASPECIAL.TXT Converted to ST M.J.Austin 17/6/87
;
; ***** Check for missing code near l54930 and L44390 ******
;
 BEGIN

;----
.essentialinit
 list9(0)=thispart
 list9(1)=solvedtimelords
 CLEAR ; clears all variables
 thispart=list9(0)
 solvedtimelords=list9(1)
 thisgame=timeandmagik
; initialise some variables which retain their values throughout
; the game
 C1=1
 C2=2
 C3=3
; C4=4
 c6=6
 c8=8
 NOMOREINPUT=TRUE ; have come to end of input line
 return
;---
.STARTGAME 
;
;>>Mike 15/7/87 - chaining code added...
 message space
 message cr
 if thisgame=timeandmagik then start2
 gosub @essentialinit ; set up vars etc.
 if c0=diskversion then start1
 message 2412 ; wrong game.
 parttochain=1
 thisgame=0 ; cause p1 to restart
 goto @chainparttochain

.start1
; cassette start with junk data in memory
 message 2419 ; you'll have to restore
 goto @dorestore

.start2
; this was definately chained from some part of KO
; the part it was chained from is "THISPART"
 if parttochain<>constantpartnum then notchained
 if thispart<>constantpartnum then notchained
; data is a position saved from this part.
; must have loaded a saved position into another part
; which caused this part to be chained in.
 goto @afterrestore ;cancelinput

.notchained
 if thispart=constantpartnum then initall ; restarting this part
; we are chaining in from another part...
 if thispart<>2 then notsolvedtimelords
 x1=list2(0)
 if x1<>174 then notsolvedtimelords ; haven't properly solvedtimelord
 solvedtimelords=true
.notsolvedtimelords

 x2=thispart ; set up THISPART.
 thispart=parttochain
; gosub @setuppartdependentobjects
; if x2<>1 then @cancelinput ; (chaining in from alternate
;			      of P2/P3, so just go straight into game)
; we are chaining in from part 1

.INITALL
 gosub essentialinit ;>>mike 20/7/87
 screen g c0
 x1=1 ; part1 title page starts drawing
 gosub @dp3 ;rawpicturex1 ; start picture drawing (at position 0,0)
;;>>mike 15/7/87 - end of added code


;>>mike 15/7/87 - was CLEAR
 C1=1 ;ANDY 27/6/86
 C2=2 ;ANDY 27/6/86
 C3=3 ;ANDY 27/6/86
 C4=4 ;ANDY 27/6/86
 GOSUB @INITPARSER
.RETURNTOSTART ;>> ANDY 27/6/86
 VERSION=GAMECODE
 GAMESOLVED=JUSTPLAYING ;>> NICK 30/6/86
;>>.L10 ; RED MOON. LEVEL 9 COMPUTING
 ; COPYRIGHT (C) 30 JUNE 85   
 ; BY SIMON ASPINALL
.L1000 ;
; START
 ;>>C1=1;SET CONSTANTS
 ;>>C2=2
 ;>>C3=3
 ;>>C4=4
 C5=254
 C6=255
 C7=253
 C8=252
 O4=6;CARRYING CAP
 U5=20;SHIELD HP
 U6=20;ARMOUR HP
 U1=50;MANS HP
 R1=11;STARTING ROOM
 R5=11;ESCAPE POS.
; init objects .....
 E1=80;SET OBJ START POS
.L1110
 E2=LIST1(E1)
 LIST2(E1)=E2
 ADD E1,C1
 IF E1<230 THEN L1110
 E1=0;SET SCORE VALUES
.L1160 E2=LIST3(E1)
 ADD E1,C1
 LIST4(E1)=E2
 IF E1<19 THEN L1160
 E1=0;SET COMBAT VALUES
.L1210 E2=LIST5(E1)
 ADD E1,C1
 LIST6(E1)=E2
 IF E1<66 THEN L1210
.L2000 ;
 ; RESTART
 MESSAGE 230;WELCOME



 GOSUB @INITOOPS ;>> ANDY 30/6/86 
 PreventExits=true ;>>mike 15/2/88
 I5=37;LOOK
 X0=1 
 GOSUB @L42500;SCORE INITIAL
.L3000 ;
; PRINT ROOM DESC
.DESCRIBE ;>> ANDY 27/6/86
 H8=0;0 PAYMENT TO NEZZON  
 IF R2<>107 THEN L3019;METAL ROOM?
 X0=LIST2(114);BOOTS POS.
 IF X0=C6 THEN L3019;BOOTS WORN?
.L3016 MESSAGE 374;SHOCK + JUMP OUT
 R2=R4;BACK OUT
 GOTO @L50000;GOTO LR2
.L3019 IF R2<>70 THEN L3030;IN LAKE?
 IF F4>0 THEN L3030
 MESSAGE 274;DRAGGED UNDER
 R2=71;BACK ONTO DRY LAND
 GOTO @L50000;MOVE

.gameafterrestore
 nomoreinput=true ;>>mike 15/2/88
 goto describe ;>>mike 15/2/88

 ;
.L3030 R2=R1
 GOSUB @L54000;STATUS
 GOSUB @L55000;LIGHT?
 ;
 ;
 IF L3>0 THEN L3200    
 MESSAGE 48;IT'S DARK
;>> NICK 30/6/86 IF P0=65529 THEN L3120
;>> NICK 30/6/86 P0=0
 LASTPICTURE=0 ;>> NICK 30/6/86
 CLS G
.L3120 GOTO L3300
.L3200
 GOSUB @L48000;MESSAGE R2 DETAILS 
 IF S3<>143 THEN L3206;KELLF?
 MESSAGE 376;KELLF DYING OF THIRST    
.L3206 IF S3<>144 THEN L3210;NEZZON? 
 MESSAGE 379;SALES PITCH
.L3210 M8=0;COMBAT OFF  
 IF F8=0 THEN L3250;SHIELD ON?
 F8=0    
 MESSAGE 351;SHIELD DOWN
.L3250 IF F9=0 THEN L3280;STRONG ON?
 MESSAGE 352;STRONG GONE
 F9=0    
.L3280 ;
.L3300 ;
 IF R2<>30 THEN L3400;GUARDIAN'S ROOM?
 X0=LIST2(87);EMERALD POS.
 IF X0<C5 THEN L3400;EMERALD ABOUT?
 MESSAGE 279;PUSHED SOUTH
 R2=143;INTO MAZE
 GOTO @L50000;GOTO LR2  
.L3400 IF R2>86 THEN L3500;UNDERWATER?
 IF R2<73 THEN L3500
 GOSUB @L45900;SWIMMING COSTUME?
 IF H1>0 THEN L3470 
 MESSAGE 280;FORCED BACK
 R2=R4;OLD ROOM 
 GOTO @L50000;GOTO LR2  
.L3470 MESSAGE 281;UNDERWATER
 IF L4=0 THEN L3500;LAMP ON?
 X4=LIST2(127);LAMP POS.
 IF X4=R2 THEN L3484
 IF X4<C5 THEN L3500
.L3484 ;
 MESSAGE 355;LAMP STILL ON !!
.L3500 IF R1<>52 THEN L3550
 X0=17
 GOSUB @L42500;SCORE
.L3550 IF R1<150 THEN L3600
 X0=16
 GOSUB @L42500;SCORE
.L3600 IF R2>76 THEN L3650;STILL UNDERGROUND?
 X0=LIST2(89);CRYSTAL POS. 
 IF X0<C7 THEN L3650;CRYSTAL CARRIED?
 X0=15
 GOSUB @L42500;SCORE
.L3650 IF R2<>26 THEN L3700;ZIIX'S ROOM?
 IF H2>0 THEN L3700
 MESSAGE 323;ZIIX PLEADS
 H2=1
.L3700 IF R2<>33 THEN L3800;XIIZ'S ROOM?
 X0=LIST2(148);XIIZ POS.
 IF X0<>R2 THEN L3800
 IF H4=0 THEN L3740;PAID ALREADY?
 MESSAGE 362;XIIZ SAYS
 M1=359
 GOTO @L64500;WITTY COMMENT     
.L3740 MESSAGE 335;GIVE ME TREASURE
 RETURN

.L3800
 IF R2<>37 THEN L3900;MIRROR ROOM
;>>mike 17/2/88 X0=LIST2(161)
;>>mike 17/2/88 IF X0<>R2 THEN L3900
 if ReflectionSetUp=True then l3900 ; >>mike 17/2/88
 ReflectionSetUp=true ;>>mike 17/2/88

 R4=R2;NO ESCAPE
 MESSAGE 363;REFLECTION APPEARS   
 list2(161)=r2 ;>>mike 21/2/88 - move reflection in!
;
 H5=1;CALC  
 GOSUB @L45000;CALC HIT VALUE

.L3846
 LIST6(66)=U3;SAME HIT VALUE
 GOSUB @L44070;CALC DODGE
.L3850
 LIST6(44)=X1;SET DODGE VALUE
 H5=0;CALC OFF 
 X1=20
 X4=U1;HP
 X0=LIST2(110);SHIELD
;>>mike 20/2/88 IF X0<C5 THEN L3868
 if x0<>c6 then l3868 ;>>mike 20/2/88
 ADD X4,X1 ; increase reflection's hit points

.L3868
 X0=LIST2(112);ARMOUR
;>>mike 20/2/88 IF X0<C5 THEN L3872
 if x0<>c6 then l3872 ;>>mike 20/2/88
 ADD X4,X1 ; increase reflection's hit points
.L3872
 LIST6(22)=X4;SAME HIT POINTS

.L3900 IF R2<>110 THEN L3970;DEMONS ROOM?
 X0=LIST2(172);LINE POS.
 IF X0<>R2 THEN L3970    
 MESSAGE 392;WARNING
.L3970 IF R2<>66 THEN L4000;BOSTOG'S ROOM?
 IF S3=0 THEN L4000
 MESSAGE 266;ENTERTAINER?
.L4000 ;
 ; INROOM PRE-COMMAND
 GOSUB @L54000;STATUS    
 IF R2<>107 THEN L4011;METAL ROOM?  
 X0=LIST2(114);BOOTS POS. 
 IF X0<>C6 THEN @L3016;SHOCK+OUT 
.L4011 IF R2<>97 THEN L4020;FOUNTAIN ABYSS
 X0=LIST2(122)
 IF X0<>R2 THEN L4020;CHALK HERE?
 MESSAGE 368;NEUTRALISE
 LIST2(122)=C8;KILL CHALK
 LIST2(169)=C8;KILL POOL
.L4020 IF F8=0 THEN L4026;SHIELD ON?
 MESSAGE 346;YOU HAVE A SHIELD
.L4026 IF F9=0 THEN L4030;STRONG ON?
 MESSAGE 347;STRONGER
.L4030 R2=R1
 IF R2<>15 THEN L4040
 I2=89
 GOSUB @L60000;O2=HERE/CARRIED?
 IF O2=0 THEN L4040 

.winredmoon ;>>mike 3/8/87
 MESSAGE 223;YOU HAVE WON    
 gosub @l20000 ;>>mike 20/2/88

 m1=2217 ; want to go on to POM? ;>>mike 20/2/88
 gosub @YesOrNo ;>>mike 20/2/88
 if y1=0 then @RestartOrRestore ;>>mike 20/2/88

 parttochain=3 ; chain price of magik ;>>mike 3/8/87 - added
 x1=236 ; code for having solved timelord and Red Moon
 list2(0)=x1
 goto @chainparttochain
; GOTO @L49510
;----------
.L4040
 GOSUB @L54000;STATUS
 GOSUB @L45800;MONSTER HOSTILE?
 GOSUB @L53900;RANDOM EVENTS
 IF B1=0 THEN L4100;SHRUNKEN?
 SUB B1,C1
 IF B1>0 THEN L4100
 ADD U1,S9
 X0=5
 SUB U1,X0;HP=OLD+CHANGE IN SHRINK HP
 MESSAGE 332;BACK TO FULL SIZE
 GOSUB @L45300;0 HP?
.L4100 ADD T1,C1;TIME PASSES
 ;
 ADD T2,C1
 IF T2<4 THEN L4205   
 MESSAGE 247;ATTACKED FROM THE DARK
 GOTO @L49000;DIE
.L4205 IF L3=0 THEN L4210 
 T2=0    
.L4210 IF S3<>157 THEN L4300;VAMPIRE
 I2=107;CRUCIFIX
 GOSUB @L61000;O2=WORN/CARRIED?
 IF O2=0 THEN L4250;HERE ETC?
 MESSAGE 390;CRUC
.L4250 IF F0=0 THEN L4300;SUN LIGHT?
 MESSAGE 391;VAMP DEAD
 S3=0
 I2=157
 GOSUB @L57500;KILL 
 ;
.L4300 IF R2>22 THEN L4310;OUTSIDE?  
 IF F0=0 THEN L4310;WITH SUN? 
 MESSAGE 271;FLOATS AWAY! 
 F0=0  
 LIST2(178)=C7;KILL SUN
 ;
.L4310 ;
.L6000 ;
 ; COMMAND
.RESETSTACK
 STACK;CLEAR STACK
 GOSUB @L6033;SET UP RETURN ADDR
 GOTO @L8000

.L6033 O2=1;OK 
 R2=R1
 ;
 GOSUB @OOPSCHECK ;>> NICK 30/6/86
;>> NICK 30/6/86 M1=28;WHAT NOW?  
 GOSUB @GETCOMMAND;INPUT
 if i1=101 then @Cheat
 if i1=73 then @Brief
 if i1=74 then @verbose
 if i2=significant then @l63500 ;>>mike 14/7/87 added: "You can't"
 ; GOSUB @L55500;MESSAGE I1-I4     
 ;
 IF I4=0 THEN L6033 ;>> ANDY 30/6/86
 GOSUB @SAVEOOPS ;>> NICK 30/6/86
 IF I1<>58 THEN L6070;RUB  
 IF I2>20  THEN L6070
 I2=I3
 ;
.L6070 IF I1<>20 THEN L6080;AGAIN *
 I1=I5 
 I2=I6
 I3=I7  
 I4=I8 
 ;
.L6080 IF I1=0 THEN @L64000;EH?  
 ;    
 IF I1<>250 THEN L6084;IT   
 I1=I6  
.L6084 IF I2<>250 THEN L6086   
 I2=I6 
.L6086 IF I3<>250 THEN L6100 
 I3=I6
 ;
.L6100
 IF I1>72 THEN NOTADDED ;>> NICK 30/6/86
 IF I1>69 THEN L6200    ;>> NICK 30/6/86 INTRANS
.NOTADDED               ;>> NICK 30/6/86
 if i1=irub then l6200 ; kludge to intrans
 IF I1<40 THEN L6200;INTRANS
 IF I1=59 THEN L6200
 IF I1=65 THEN L7000;CAST ANYTHING 
 IF I1<80 THEN L6110 
 I2=I1  
 IF I2>180 THEN @L63200; WIRTS MIRT BUD  
 MESSAGE 67;DO WHAT WITH 
 I6=I2
 GOTO @L64650;I2 .
.L6110 IF I2>79 THEN L6150;OK OBJECT   
 IF I4<2 THEN @L64100;NO OBJECT 
 IF I4>8 THEN @L64300;TOO COMPLEX
 GOTO @L64200;BAD OBJECT
 ;
.L6150 ;
 ;
 IF I2=251 THEN L6200;EVERYTHING
 IF I1=66 THEN L6200;SAY 
 GOSUB @L61500;OBJECT HERE/CARRIED?    
.L6200 ;
.L7000 ;
 ; ACT ON COMMAND
 GOSUB @L47000;SAVE CMD     
 ;
 IF I2=251 THEN @L11000;EVERYTHING
 ;
 IF I1>75 THEN @L64000;WOT?
 JUMP @L7120 I1

.L7120 DATA @L64000,@L14000,@L14000,@L14000,@L14000,@L14000 ;0-5 
 DATA @L14000,@L14000,@L14000,@L14000,@L14000 ;6-10
 DATA @L14000,@L14000,@L14000,@L14000,@L14000 ;11-15 
 DATA @L14000,@L14000,@L14000,@L14000,@L14000 ;16-20
 DATA @L15000,@L16000,@L16500,@L31000,@L17500 ;21-25  
 DATA @L18000,@restore,@save,@L20000,@L19500 ;26-30 
 DATA @L19500,@L21000,@L10000,@L32000,@L39000 ;31-35    
 DATA @L64400,@L17000,@L63300,@rude,@L22000 ;36-40
 DATA @L23000,@L24000,@L25000,@L41000,@L26000 ;41-45 
 DATA @L26500,@L27000,@L28000,@L29000,@L29500 ;46-50
 DATA @L30000,@L30500,@L64400,@L64400,@L33000 ;51-55 
 DATA @L33500,@L57500,@L34500,@L39500,@L43000 ;56-60    
 DATA @L36000,@L37000,@L63500,@L64400,@L12000 ;61-65   
 DATA @L42000,@L37000,@L43000 ;66-68
 DATA @L64000,@RAMCOMMAND,@OOPS,@AUTHORS ;69-72 ???,RAM,OOPS,CREDITS
 data @brief,@verbose ; 73-74
 data @cheat



.L8000 ;
 MESSAGE 5
 ; IN-ROOM POST-COMMAND
 X0=LIST2(104);SWORD POS.
 IF X0<C5 THEN L8030;SWORD CARRIED?
 X0=LIST2(108);GLOVES POS.
 IF X0>C5 THEN L8030
 MESSAGE 317
 GOSUB @L44500;-1 HP
 I2=104
 R2=R1
 i1=0 ; don't report drop >>mike 12/7/87
 GOSUB @L53000;DROP SWORD
.L8030 X0=LIST2(112);CHAIN MAIL POS.
 IF X0<>C6 THEN L8043;ARMOUR WORN?
 X0=LIST2(113);SHIRT POS.
 IF X0=C6 THEN L8043;SHIRT WORN?
 MESSAGE 378;HURTS
 ADD B2,C1;INC COUNT
 IF B2<11 THEN L8043;LESS THAN 10?
 B2=0    
 GOSUB @L44500;-1 HP
.L8043 ;
 GOSUB @L54000;UPDATE STATUS
 IF R2>86 THEN L8100;UNDERWATER?
 IF R2<73 THEN L8100
 GOSUB @L45900;SWIMMING GEAR?
 IF H1>0 THEN L8100 
 MESSAGE 282;DROWNED
 GOTO @L49000
.L8100
 GOSUB @L45800;MONSTER HOSTILE? 
 GOSUB @L44000;MONSTER ATTACKS
 ;
 IF R1<213 THEN L9990;FATAL LOCATION?
 MESSAGE 277;YOU DIE
 GOTO @L49000
.L9990 GOTO @L4000;LOOP BACK


.L10000 ;
; PSI. 
 goto @l64000 ;>>Mike 16/7/87. RETURN

.L11000 ;
 ; EVERYTHING
 IF I1=40 THEN L11100;TAKE
 IF I1=41 THEN L11200;DROP
 GOTO @L63500;CAN'T
 ;
.L11100 E4=80
.L11110 E2=LIST2(E4)
 IF E2<>R1 THEN L11150 
 I2=E4
 GOSUB @L51000;GAIN (HELD)
.L11150 ADD E4,C1
 IF E4<141 THEN L11110
 RETURN
 ;
.L11200 E4=80
.L11210 E2=LIST2(E4)
 IF E2<>C5 THEN L11250
 I2=E4
 GOSUB @L53000;DROP
.L11250 ADD E4,C1
 IF E4<141 THEN L11210
 RETURN
 ;
.L12000 ;
 ; CAST SPELL  
 ;
 IF I2=230 THEN @L12850;FIND
 IF I2=27 THEN @restore ;>>mike 16/2/88 L18500;RESTORE  
 IF I2=28 THEN @restore ; >>mike 16/2/88 L19000;SAVE      
 IF I2=231 THEN L12100;EXTINGUISH  ; >>mike 1/7/87 - was 52
 IF I2=232 THEN L12200;SNOOP
 IF I2=233 THEN @L12300;TREASURE
 IF I2=234 THEN @L12400;MAGIC ;>>mike 1/7/87 - was 149
 IF I2=235 THEN @L12500;ZAP 
 IF I2=236 THEN @L12600;BOUNCE
 IF I2=237 THEN @L12700;ESCAPE
 IF I2=110 THEN @L12800;SHIELD
 IF I2=239 THEN @L12900;STRONG
 MESSAGE 312;FAILS
.ENDCOMMAND ;>> ANDY 27/6/86  
 RETURN
.L12100 ; EXTINGUISH
 ; AFFECTS LAMP,SUN,FIRE & LIGHTS (53)
 X0=96;FAN FOCUS
 GOSUB @L13900;FOCUS,IRON  
 D1=I3
 GOSUB @L47500;FIND TARGET ROOM (R3)
 IF R3<>0 THEN L12132;ROOM LOCATED?
.L12131 R3=R2;DEFAULT TO R2
.L12132 ;
 R2=R3;FOR CHECK PROC
 I2=127;LAMP
 GOSUB @L60000;O2=HERE/CARRIED? 
 IF O2=0 THEN L12141 
 L4=0;LAMP OUT  
.L12141 IF F0=0 THEN L12148;RAISIN SUN?
 F0=0;RASIN SUN OUT 
 LIST2(101)=R2;RAISIN THERE
 LIST2(178)=C7;KILL SUN
.L12148 I2=175;FIRE 
 GOSUB @L60000;O2=HERE/CARRIED? 
 IF O2=0 THEN L12156  
 GOSUB @L57500;KILL FIRE
.L12156 IF R3<>53 THEN L12160;DISABLE FLASHING LIGHTS
 F6=1;ROOM 53 SAFE  
.L12160 ;
 MESSAGE 315;ROOM BLACKED OUT.
 RETURN
.L12200 ; SNOOP
 X0=86;BLACK PEARL FOCUS
 GOSUB @L13900;FOCUS,IRON  
 D1=I3
 GOSUB @L47500;FIND TARGET ROOM (R3)
 IF R3<>0 THEN L12260;ROOM LOCATED?
.L12255 R3=R2; *
.L12260 R2=R3;SET WHERE TO LOOK
 MESSAGE 330;MINDS EYE ZOOMS INTO ROOM 
 GOSUB @L48018;MESSAGE ROOM DESCRP 
 R2=R1;RESET R2
 RETURN
.L12300 ; TREASURE
 X0=108;GLOVES FOCUS
 GOSUB @L13900;FOCUS,IRON 
 I2=I3
 IF I2<80 THEN @L63500;CAN'T
 IF I2>180 THEN @L63500;CAN'T
 GOSUB @L60000;O2=HERE/CARRIED?  
 IF O2=0 THEN @L63800;CANT SEE?
 MESSAGE 338;THE OBJECT IS
 IF I2<91 THEN L12390
 MESSAGE 340;WORTH LESS
 RETURN
.L12390 MESSAGE 339;VALUABLE
 RETURN
.L12400 ; MAGIC 
 X0=84;MEDALLION FOCUS
 GOSUB @L13900;FOCUS,IRON 
 I2=I3
 IF I2<80 THEN @L63500;CAN'T
 IF I2>180 THEN @L63500;CAN'T
 GOSUB @L60000;O2=HERE/CARRIED? 
 IF O2=0 THEN @L63800;CANT SEE?
 MESSAGE 338;THE OBJECT IS
 IF I2<88 THEN L12490
 IF I2>111 THEN L12490
 MESSAGE 342;MAGIC
 RETURN
.L12490 MESSAGE 341;NORMAL
 RETURN
.L12500 ; ZAP
 X0=124;DAGGER FOCUS
 GOSUB @L13900;FOCUS,IRON 
 I2=I3;SET OBJECT
 IF I2<>0 THEN L12525;NO TARGET  
 I2=S3;DEFALUT
.L12525 IF I2<80 THEN @L63500;CAN'T   
 IF I2>180 THEN @L63500;CAN'T  
 IF I2=S3 THEN L12560
 MESSAGE 348;TARGET MUST BE SENTIENT
 RETURN
.L12560 IF I2=147 THEN @L64400;DOG IS ZAP-PROOF
 M8=1;COMBAT IS GO 
 X0=I2;SET TARGET
 M7=1;ZAP ATTACK 
 U3=15;ATTACK RATING
 GOSUB @L45026;MAN COMBAT (NO DODGE)
 M7=0;ZAP OFF 
 RETURN
.L12600 ; BOUNCE
 X0=92;BALL FOCUS
 GOSUB @L13900;FOCUS,IRON 
 IF R2>212 THEN L12655;FATAL LOCATION?
 ;
 MESSAGE 349;STEP SPRINGY
 RETURN
.L12655 MESSAGE 344;BOUNCE BACK
 R2=R4;OLD ROOM 
 GOTO @L50000;GOTO LR2   
.L12700 ; ESCAPE
 X0=94;DULCIMER FOCUS
 GOSUB @L13900;FOCUS,IRON 
 IF S3=0 THEN L12740;CREATURE HERE 
 MESSAGE 350;DONT WORK
 RETURN
.L12740 MESSAGE 345;SPELL SUCCESS
 IF R2=10 THEN L12770  
 R5=R2;RESET ESCAPE POS.
 R2=10
 GOTO @L50000;GOTO LR2    
.L12770 R2=R5
 R5=10
 GOTO @L50000;GOTO LR2   
.L12800 ; SHIELD
 X0=106;CLOAK FOCUS
 GOSUB @L13900;FOCUS,IRON 
 F8=1;SHIELD ON 
 RETURN
.L12850 ; FIND
 IF I3<80 THEN L12890
 IF I3>189 THEN L12890
 R2=LIST2(I3)
 IF R2<210 THEN @L12255 
.L12890
 MESSAGE 229 ; you just see DARKNESS
 r2=r1 ; reset current room ;>>mike 12/7/87 - fix existing bug
 RETURN
.L12900 ; STRONG
 X0=103;SPICES FOCUS
 GOSUB @L13900;FOCUS,IRON 
 F9=1;STRONG SPELL ON 
 RETURN
.L13900 ;
 ; CHECK SPELL FOCUS,IRON,HP
 GOSUB @L44500;-1 HP 
 ;
.L13930 IF X0=0 THEN L13940; *  
 I2=X0  
 GOSUB @L60000;O2=FOCUS HERE/CARRIED? 
 IF O2>0 THEN L13940 
 MESSAGE 313;FOCUS MISSING 
 GOTO @L8000    
 ;
.L13940 I2=82
 GOSUB @L13980;COINS HERE?
 I2=97  
 GOSUB @L13980;HORSESHOE HERE?
 I2=112  
 GOSUB @L13980;ARMOUR HERE?
 I2=120  
 GOSUB @L13980;AXE HERE?
 I2=123   
 GOSUB @L13980;CROWBAR HERE?
 I2=126
 GOSUB @L13980;KEY HERE?
 RETURN
 ;
.L13980 GOSUB @L60000;O2=IRON HERE/CARRIED? 
 IF O2>0 THEN L13990 
 RETURN
 ;
.L13990 MESSAGE 314;SPELL FAILED BECAUSE IRON
 GOTO @L8000 
.L14000 ;
 ; MOVE C
 IF R2<>53 THEN L14040;FLASHING LIGHTS?
 IF F6>0 THEN L14040;LIGHTS OUT?
 MESSAGE 365;CANT MOVE
 RETURN
.L14040 IF I1<>14 THEN L14050;CLIMB
 I1=9;UP
 IF I2<>10 THEN L14050
 I1=I2
 ;
.L14050 IF I1<>15 THEN L14060;JUMP
 I1=10;DOWN
 IF I2<>9 THEN L14060
 I1=I2
 ;
.L14060 IF I1<15 THEN L14100
 IF I1=17 THEN L14070;FORWARD
 GOSUB @L58500;RIGHT TURN
 IF I1=19 THEN L14070;RIGHT
 GOSUB @L58500;RIGHT TURN
 IF I1=16 THEN L14070;BACKWARDS
 GOSUB @L58500;RIGHT TURN
 ;
.L14070 I1=D4
 ;
.L14100 D1=I1
 GOSUB @L47500;EXIT 
 IF R2<>110 THEN L14110;DEMON ROOM?
 IF R3=R4 THEN L14110;EXIT SAFE? 
 X0=LIST2(172);LINE POS.
 IF X0<>R2 THEN L14110
 MESSAGE 393;CROSS LINE 
 GOTO @L49000;DIE
 ;
.L14110 IF R3>0 THEN L14115
 MESSAGE 61;NO WAY
 RETURN 
 ;
.L14115 IF S4=0 THEN L14139

 GOSUB @L14982;MONSTER IN WAY? >>mike 12/7/87 - moved from l14125

 IF D2>0 THEN L14125;DOOR?
 IF R2=104 THEN L14120;DOG ROOM
 IF R2=24  THEN L14120;SMALL ROOM
 IF R2<>14 THEN L14122;TOWER 
.L14120 MESSAGE 62;DOOR IN WAY 
 RETURN 
.L14122

 IF R3<>24 THEN NotLittleDoor ;>>mike 17/2/88 L14138;THROUGH DOOR?
 IF R2<>124 THEN NotLittleDoor ;>>mike 17/2/88
 IF B1=0 THEN TooSmall ;SHRUNK? >>mike 17/2/88

.NotLittleDoor
 MESSAGE 74;DOOR OPENS BEFORE YOU..   
 D2=1
 ;
.L14125
;>>mike 12/7/87 moved to l14115 - GOSUB @L14982;MONSTER IN WAY?
 ;
 IF S7=0 THEN L14130;1-WAY?
 MESSAGE 275;DOOR MERGES INVISIBLY
 GOTO L14150

.L14130
 IF R3=124 THEN L14133;SECRET CUPBOARD? 
 IF R3<>24 THEN L14138;THROUGH DOOR?
 IF R2<>124 THEN L14138

.L14133
 IF B1<>0 THEN L14138;SHRUNK?
.TooSmall
 MESSAGE 357;TOO SMALL
 RETURN    

.L14138
 MESSAGE 63;DOOR CLOSES BEHIND YOU

.L14139
 GOSUB @L14982;MONSTER IN WAY?   
 IF S7=0 THEN L14150;1-WAY?
 IF R3<>76 THEN L14144;WELL FALL?
 MESSAGE 356;FALL
 GOTO L14150
.L14144 IF R3<212 THEN L14147;FATAL FALL?
 MESSAGE 358;YOU ARE FALLING
 GOTO L14150
.L14147 IF R3=1 THEN L14149
 IF R3<23 THEN L14150;OUTSIDE 
.L14149 MESSAGE 276;YOU SLIDE A CHUTE
.L14150 IF R2<>19 THEN L14200;HAS HE MOVED FROM 19
 IF R3<>18 THEN L14200
 IF F2<>0 THEN L14200;GATE OPEN? 
 MESSAGE 263;GATE CLOSES BEHIND YOU
.L14200 IF R3<>19 THEN L14250;THROUGH GATE?
 IF R2<>18 THEN L14250
 IF F2<>0 THEN L14250;GATE OPEN?  
 MESSAGE 264;GATE IN WAY
 RETURN
.L14250 IF R2<>105 THEN L14300;IN CHIMNEY?
 IF R3<>123 THEN L14300
 X0=LIST2(166);GRILL POS.
 IF X0<>R2 THEN L14300;GRILL GONE
 MESSAGE 283;GRILL BLOCKED
 RETURN
.L14300 GOSUB @L45800;MONSTER HOSTILE? 
 IF H0=0 THEN L14400;HOSTILE?
 MESSAGE 309;HIT IN BACK
 GOSUB @L44000;MONSTER ATTACKS
.L14400 IF R2<>25 THEN L14450
 IF R3<>105 THEN L14450
 X0=LIST2(175);FIRE POS.
 IF X0<>R2 THEN L14450
 MESSAGE 318;FIRE IN WAY
 RETURN
.L14450 IF R3<>131 THEN L14500;XIIZ'S ROOM?
 IF R2<>33 THEN L14500
 IF H4>0 THEN L14500;PAID XIIZ?  
 MESSAGE 335
 RETURN
.L14500 IF R2<>97 THEN L14600;FOUNTAIN ABYSS?
 IF R3<>205 THEN L14600
 X0=LIST2(169);ACID POOL POS.
 IF X0<>R2 THEN L14600;ACID HERE
 MESSAGE 367;ACID IN WAY
 RETURN
.L14600 ;
 D4=I1
 R2=R3
 GOTO @L50000;GOTO LR2  
.L14982 ; MONSTER IN WAY?
 GOSUB @L45800;HOSTILE? 
 IF H0=0 THEN L14999;FRIENDLY?
 IF R3=R4 THEN L14999
 MESSAGE 308;OPPONENT BLOCKS
 GOTO @L8000
.L14999 RETURN
.L15000 ;
 ; ATTACK C
 IF I2<>12 THEN L15015;ATTACK GHOST OF XX
 I2=I3
.L15015 IF I2=0 THEN @L64100;MISSING OBJECT
 GOSUB @L61500;HERE/CARRIED?
 IF I2<>S3 THEN L15900
 X0=S3
 M8=1     
 M7=0 
 GOTO @L45000;ATTACK
.L15900 MESSAGE 68;SHOULDN'T ATTACK
 GOTO @L64650;I2 .

.L16000 ;
 ; INVENTORY C
 MESSAGE 25;YOU OWN
 M1=0;NULL
 R2=C5;CARRIED
 GOSUB @L57000;LIST OBJ
 IF O5>0 THEN L16100  
 MESSAGE 60;NOTHING
 ;
.L16100 M1=26;AND YOU'RE WEARING
 R2=C6;WORN
 GOSUB @L57000;LIST OBJ
 ;
 ;
 RETURN
.L16500 ;
 ; LISTEN C
 MESSAGE 231;HEAR NOTHING SPECIAL
 RETURN
.L17000 ;
 ; LOOK C
 IF I2=0 THEN @L3000
 IF I2>21 THEN @L3000
 I1=35;EXAMINE 
 GOTO @L46000;ADJUST INPUT  
 RETURN
.L17500 ;
 ; INVALID MAGIC C
 X0=0  
 GOSUB @L13900;FOCUS,IRON 
 MESSAGE 232;THAT MAGIC DOESN'T WORK
 RETURN
.L18000 ;
 ; QUIT C
 M1=53;REALLY DRIVER?
 GOSUB @L46500;PLEASE CONFIRM M1
 GOTO @ABORTGAME ;>> ANDY 30/6/86
;>> GOTO @L49500;PLAY AGAIN?

;.L18500 ;
;; RESTORE C
; M1=55;REALLY RESTORE?
; GOSUB @L46500;PLEASE CONFIRM M1
; X0=0  
;;>>mike 28/6/87 - was  GOSUB @L13930;IT'S A SPELL
;;****** GOSUB @LENSLOK ;>> ANDY 30/6/86  
;;******** IF RESULT=FALSE THEN @L3000 ;>> NICK 30/6/86
;;>> IF P0=65529 THEN L18606
; RESTORE ;PICTURE MODE
; CLS G
; message cr ;>>mike 30/6/87
; LASTPICTURE=0 ;>> NICK 30/6/86
;;>> P0=0
;>> GOTO L18610
;>>.L18606 RESTORE;WORDS MODE
;>> P0=65529
;>>.L18610
; GOSUB @AFTERRESTORE ;>> ANDY 30/6/86
;>> GOTO @L3000


;.L19000 ;
; ; SAVE C
; X0=0               
;;>>mike 28/6/87 - was GOSUB @L13900;FOCUS,IRON  
; SUB S6,C1
; IF S6<600 THEN L19030
; S6=0  
;.L19030
; SAVE
; message cr ;>>mike 30/6/87
; GOTO @L3000


.L19500 ;
 ; YES C / NO C
 MESSAGE 238;DIDN'T EXPECT THAT
 RETURN
.L20000 ;
 ; SCORE C
;>>mike 16/7/87 GOSUB @SOLVEDPREVIOUS ;>> ANDY 30/6/86
 X2=0;0 TEMP ADD ON  
 X0=80
.L20030 X1=LIST2(X0);CHECK IF TREASURES ARE HELD OR IN ROOM 15
 IF X1=15 THEN L20060
 IF X1<C5 THEN L20100
.L20060 X4=X0
 X1=78
 SUB X4,X1;FIND SCORE VALUE IE 80--->2 ETC
 X3=LIST4(X4)
 ADD X2,X3;INCREASE TEMP SCORE
.L20100 ADD X0,C1
 IF X0<90 THEN L20030
 ; 
 ; MESSAGE SCORE + RATING  
 MESSAGE 57;YOU SCORED
 ADD X2,S6;SUM OF SCORE + VALUES FOR TREASURES
 PRINT X2;SCORE
 MESSAGE 58;OUT OF N, WITH A RANK OF
 M1=250;BEGINNER
 IF X2<150 THEN L20980  
 M1=251;PLAYER
 IF X2<300 THEN L20980   
 M1=252;ADVENTURER
 IF X2<600 THEN L20980   
 M1=253;EXPERIENCED ADVENTURER
 IF X2<1000 THEN L20980  
 M1=254;GRANDMASTER
.L20980 MESSAGE M1;RANK
 MESSAGE 4;DOT SPACE   
 GOSUB @L44400;MESSAGE NO. OF HP'S LEFT
;>>mike 16/7/87 GOSUB @SOLVEDPREVIOUS ;>> NICK 30/6/86
;>>mike 15/2/88 if solvedtimelords=false then scoreret
;>>mike 15/2/88 message 2315 ; and you solved the lords of time
.scoreret
 RETURN
.L21000 ;
 ; WAIT C
 MESSAGE 233;TIME PASSES 
 X1=0   
.L21110 X2=0  
.L21120 ADD X2,C1
 IF X2<200 THEN L21120     
 MESSAGE 4;DOT SPACE
 ADD X1,C1
 IF X1<5 THEN L21110
 RETURN

.L22000 ;
; TAKE C
 IF I2>139 THEN @L63600;SILLY
 E2=LIST2(I2)
 IF E2=C5 THEN @L63400;ALREADY HAVE I2.
 IF I2<>125 THEN L22100;HANDLE?
 F3=0;HANDLE NOT INSERTED 
.L22100 ;
 GOTO @L51000;TAKE

.L23000 ;
; DROP C
 IF I3=11 THEN @L36000;"PUT THING IN" = INSERT  
 GOSUB @L62500;CARRIED?
 IF I2<>92 THEN L23100;BLACK BALL?
 IF H6>0 THEN L23100;ALREADY DONE?
 H6=1;USED 
 GOSUB @L12131;EXTINGUISH ON R2
 I2=92
.L23100 ;
 GOTO @L53000;DROP

.L24000 ;
 ; WEAR C
 IF I2<83  THEN L24035
 IF I2<86  THEN L24040   
.L24035 IF I2<105 THEN @L63500;CAN'T
 IF I2>116 THEN @L63500;CAN'T    
 ;
.L24040 E2=LIST2(I2)
 IF E2=C6 THEN @L63400;ALREADY HAVE I2.
 ;
 ;
 ;
;>> IF I2<>109 THEN L24900;RING
;>> MESSAGE 220;AGILE 
;>>.L24900
;
 R2=C6;WORN
 GOSUB @L52000;TAKE (TO R2) 
 ;
 IF I2<>109 THEN L24900;RING ;<<mike 11/7/87 - 3 lines moved from above
 MESSAGE 220;AGILE 
.L24900
;
 IF I2<>105 THEN L24930;BRACERS?
 MESSAGE 388;BRACERS PART OF YOU !  
 O4=12;INC CARRYING CAP.  
 ;
.L24930 ;
 RETURN

.L25000 ;
; THROW C
 GOSUB @L62500;CARRIED?
 IF I2<>101 THEN L25100;RAISIN
 MESSAGE 221;SUN RUN    
 F0=1;SUN LIT  
 LIST2(178)=R2;SUN HERE
 GOTO @L57500;KILL RAISIN
.L25100 IF I2<>95 THEN L25200;MUMMY'S DUST?
 MESSAGE 396;THROW DUST
 GOSUB @L57500;KILL DUST
 IF S3=0 THEN L25190;ANYONE HERE? 
 IF S3=153 THEN @L64400;DRAGONS
 LIST2(S3)=C8;KILL MONSTER
 GOTO @L45250;HAIL DEATHS
.L25190 RETURN
.L25200 ;
 GOTO @L53000;DROP
;---
.L26000 ;
 ; FILL C
 IF I2<>93 THEN L26100;BOTTLE? 
 IF F7>0 THEN @L64930;FULL?
 IF R2>98 THEN L26090;WATER HERE?
 IF R2<70 THEN L26090
 MESSAGE 75
 F7=193
 RETURN
.L26090 MESSAGE 319;CANT
 RETURN
.L26100 ;
 GOTO @L63500;CAN'T
.L26500 ;
 ; EMPTY C
 IF I2<>93 THEN L26600;BOTTLE
 IF F7=0 THEN @L63900;ALREADY EMPTY
 MESSAGE 321
 F7=0   
 RETURN
.L26600 ;
 GOTO @L63500;CAN'T
.L27000 ;
; OPEN C
 IF I2<>252 THEN L27060;DOOR?
 IF R2<>104 THEN L27028;KENNEL?
 IF S3<>147 THEN L27028;DOG?
 MESSAGE 373;DOG ON DOOR
 RETURN
.L27028 IF D2>0 THEN @L63000;ALREADY OPEN  
 ;
 IF R2=14 THEN L27038;ROOM ADJACENT TO MARBLE
 IF R2<>31 THEN L27042;TOWER DOOR?
.L27038 IF F1>0 THEN L27042;ALREADY SAID HUMAKAAT?
.L27040 MESSAGE 259; LOCKED MAGICALLY *
 RETURN
.L27042 MESSAGE 236;THE DOOR OPENS
 D2=1   
 RETURN
 ;
.L27060
 IF I2<>174 THEN L27100;METAL GATE
 IF F2<>0 THEN @L63000;ALREADY OPEN?
 if i3=126 then openwithkey
 GOSUB @L54900;WHAT WITH
; message 262;WHAT WITH?.aloop1
; gosub getnextword
; gosub checknoun
; if object=0 then @L63500 ; can't without key
; i2=object
;>>mike 15/2/88 end of mods.

 IF I2<>126 THEN @L63500;KEY? CAN'T
.openwithkey
 i2=126 ; key >>Mike 16/2/88 - ensure the key is killed, not the gate
 GOSUB @L57500;KILL i2 (KEY)
 F2=1
 MESSAGE 268;GATE OPEN KEY GONE
 RETURN
 ;
.L27100 IF I2=166 THEN @L43000;MOVE GRILL
 ;
 IF I2=171 THEN @L27040;SARCOPHAGUS
 ;
 IF I2<>121 THEN L27300;BOX?
 IF H3>0 THEN @L63000;ALREADY OPEN?
 H3=1;OPEN 
 X0=LIST2(87);EMERALD POS.
 IF X0<>C7 THEN @L64960;OK !
 MESSAGE 333;GAS
 LIST2(87)=R2
 X0=LIST2(116);MASK POS.
 IF X0=C6 THEN L27280;MASK WORN?
 X1=10
 SUB U1,X1;-10 HP
 GOSUB @L45300;0 HP?
 GOTO @L44400;HP LEFT +RETURN
.L27280 MESSAGE 334;MASK SAVES YOU
 RETURN
 ;
.L27300 IF I2=173 THEN @L40000;EXAMINE FUNGUS
 ;
 ;
 GOTO @L63900;CAN'T TO I2 .
.L28000 ;
 ; CLOSE C
 IF I2<>252 THEN L28060;DOOR? 
 IF D2=0 THEN @L63100;ALREADY CLOSED 
 MESSAGE 237;THE DOOR CLOSES
 D2=0  
 RETURN 
.L28060 IF I2<>174 THEN L28100;METAL GATE
 IF F2=0 THEN @L63100;ALREADY CLOSED   
 MESSAGE 267;NEVER CLOSE AGAIN
 RETURN
.L28100 IF I2<>166 THEN L28150;GRILL
 GOTO @L43060; ---->MOVE GRILL
.L28150 IF I2<>121 THEN L28200;BOX?
 IF H3=0 THEN @L63100;ALREADY CLOSED  
 H3=0      
 GOTO @L64960;OK
.L28200 ;
 GOTO @L63900;CAN'T TO I2 .
.L29000 ;
 ; EAT C
 IF I2<>98 THEN L29100;MUSHROOM?
 MESSAGE 331;SHRINK 
 S9=U1;HP 
 U1=5;5 HP ONLY !
 B1=6;5 TURNS DURATION !
 RETURN
.L29100 IF I2<>99 THEN L29200;POISON PILLS
 GOTO @L49000;YOU ARE DEAD
.L29200 IF I2<>128 THEN L29220;MEAT
 MESSAGE 370;DOGFOOD !
 RETURN
.L29220 ;
 MESSAGE 235;YEUCH! NO THANKS!
 RETURN
.L29500 ; 
 ; DRINK C
 IF I2<>93 THEN L29600;DRINK BOTTLE?
 IF F7=0 THEN L29990
 MESSAGE 75
 F7=0
 RETURN
.L29600 IF I2<>100 THEN L29650;POTION?
 MESSAGE 347;STRONG
 U1=50;RENEW HP
 GOTO @L57500;KILL POT
.L29650 ;
.L29990 GOTO @L63600;SILLY!
.L30000 ; 
 ; IGNITE C
 IF I2<>127 THEN @L63500;CAN'T, UNLESS LAMP  
 IF L4>0 THEN @L64930;ALREADY DONE
 L4=192;LAMP LIT 
 L3=1;HE CAN SEE
 T2=0  
 MESSAGE 75; OK!
 GOTO @L3000
.L30500 ;
 ; EXTINGUISH C
 IF I2<>127 THEN @L63500;CAN'T, UNLESS LAMP
 IF L4=0 THEN @L64930;ALREADY DONE   
 L4=0;LAMP OUT 
 MESSAGE 75; OK! 
 RETURN

.L31000 ;
; PICTURE;S
 SCREEN G C0 ;>> NICK 30/6/86
 LASTPICTURE=0 ;>> NICK 30/6/86
 GOTO @L3000 ;DISPLAY PICTURE;S

.L32000 ;
 ; WORDS
 SCREEN T 
;>> NICK 30/6/86 P0=65529
 RETURN   
.L33000 ;
 ; WAVE C
 IF I2<>97 THEN L33100;HORSE SHOE
 X0=LIST2(82);COINS POS.
 IF X0<>R2 THEN L33100;HERE?
 MESSAGE 328;PULL COINS
 B0=1;COINS NORMALLY ACCESSIBLE 
 I2=82
 GOTO @L51000;GAIN COINS
.L33100 IF I2<>96 THEN L33200;FAN?
 MESSAGE 397;WIND
 IF S3=153 THEN L33140;DRAGONS?
 RETURN
.L33140 MESSAGE 398;DRAGONS DIE
 LIST2(153)=C8
 RETURN
.L33200 ;
 GOTO @L64400;NOTHING HAPPENS
.L33500 ;
 ; PLAY C
 IF I2<>94 THEN L33600;DULCIMER?
 MESSAGE 200;TUNE
 IF S3<>141 THEN L33600;BOSTOG?
 MESSAGE 399;THANKS+HINT+BYE
 LIST2(141)=C8;KILL BOSTOG
 RETURN
.L33600 ;
 GOTO @L64400;NOTHING HAPPENS
.L34500 ;
 ; RUB C
 if r2<>110 then l34600 ; red line?
 if i2=0 then ruboutline ; no object - kludge parser bug
 IF I2<>172 THEN L34600;RED LINE?
.ruboutline
 MESSAGE 394;ERASED
 LIST2(172)=C8;KILL IT
 goto @cancelinput ;>>mike 12/7/87 - also kludge for parser bug. RETURN
.L34600 ;
 GOTO @L64400;NOTHING HAPPENS
.L36000 ;
 ; INSERT C
;>>mike 12/7/87 - 3 lines added to allow "PUT CHALK IN ACID"
 if i2<>122 then insertnotchalk
 if i3=169 then @l53000 ; insert into acid

.insertnotchalk
 IF I2<>125 THEN L36100;HANDLE?
 IF R2<>71 THEN L36100
 GOSUB @L57500;KILL HANDLE
 LIST2(I2)=R2
 MESSAGE 269;HANDLE FITS HOLE
 F3=191
 RETURN
.L36100 IF I2<>99 THEN L36200;PINK PILLS?
 I2=128;MEAT
 GOSUB @L60000;O2=HERE/CARRIED? 
 IF O2=0 THEN @L36490;NO MEAT 
 MESSAGE 369
 H7=1   
 I2=99;PILLS
 GOTO @L57500;KILL PILLS
.L36200 ;
.L36490 GOTO @L63600;SILLY!
.L37000 ;
;---
; TURN C
 IF I2<>125 THEN L37100;HANDLE
 IF F3=0 THEN L37100;INSERTED?
 IF F4>0 THEN L37100;ALREADY DONE?
 MESSAGE 270;LAKE EMPTIES
 F4=1   
 RETURN
 ;
.L37100 IF I2<>163 THEN L37200;DIAL
 MESSAGE 324;YOU HEAR
 M1=325
 GOTO @L64500;CLUNK/CLICK/WHIRR
.L37200 ;
 IF I2<>170 THEN L37400
 X0=LIST2(121);BOX POS.
 IF X0<>C7 THEN @L64930;ALREADY DONE?
 MESSAGE 329;BOX HERE
 LIST2(121)=R2
 RETURN
.L37400 ;
 GOTO @L64400;NOTHING HAPPENS
.L39000 ;
; SEARCH/EXAMINE C
 i1=0 ; prevent confirmation messages for objects found/taken. >>mike12/7/87
 IF I4=1 THEN L39500;NO OBJECT->SEARCH 
 if i2=252 then examinedoor
 IF I2>190 THEN L39100;ODD OBJ->CAN'T HELP  
 IF I2>79 THEN @L40000;EXAMINE
 ;
.L39100 GOTO @L64200;FUNNY OBJECT 
.L39500 ;
; SEARCH
 IF R2<>71 THEN L39600;SEARCHING BUSHES?
.L39530 I2=125; *
 GOSUB @L58000;FIND HANDLE?
 I2=126
 IF O2>0 THEN @L58000;FIND KEY
 RETURN
 ;
.L39600 IF R2<>11 THEN L39700;ON MOUND?
 I2=83;CROWN
 GOTO @L58000;FIND CROWN
 ;
.L39700 IF R2<>108 THEN L39800;NEAR BEANS? *
 IF F5>0 THEN @L64930;ALREADY DONE   
 MESSAGE 278;FIND TRAPDOOR 
 F5=1;SET VAR 
 RETURN
 ;
.L39800 ;
 I2=0
 GOTO @L58000;FIND NOTHING

.examinedoor
 m1=1300 ; examine message for magical door in tower
 if r2=14 then examinem1
 m1=1301 ; 1301-252 ; examine message for general doors
 goto examinem1

.L40000 ;
 ; EXAMINE
 GOSUB @L61500;HERE/CARRIED?
 ;   
 M1=550 ; block of examine messages
 ADD M1,I2 

.examinem1
 IF I2<>174 THEN ExamNotGate ; >>mike 15/2/88 METAL GATE?
 IF F2=0 THEN ExamNotGate ;OPENED? ; >>mike 15/2/88
 MESSAGE 265;OPEN  ; >>mike 15/2/88
 RETURN  ; >>mike 15/2/88
 ;
.ExamNotGate
 MESSAGE M1;DETAILS OF I2   
 MESSAGE 4; . SPACE
 ;
 IF I2=82  THEN L40038
 IF I2=97  THEN L40038 
 IF I2=112 THEN L40038 
 IF I2=120 THEN L40038
 IF I2=123 THEN L40038
 IF I2<>126 THEN L40040
.L40038 MESSAGE 219;IRON . SPACE
 ;
.L40040
; >>mike 15/2/88 IF I2<>174 THEN L40050;METAL GATE?
; >>mike 15/2/88 IF F2=0 THEN L40900;OPENED?
; >>mike 15/2/88 MESSAGE 265;OPEN 
; >>mike 15/2/88 RETURN 
 ;
.L40050
 IF I2<>173 THEN L40100;OYSTER FUNGUS
 I2=86
 GOTO @L58000;FIND PEARL
 ;
.L40100 IF I2<>167 THEN L40200;BEANS
 IF F5=0 THEN @L39700;UNCOVER TRAPDOOR  
 ;
.L40200 IF I2<>93 THEN L40250;BOTTLE 
 IF F7=0 THEN L40250;EMPTY?
 MESSAGE 322
 RETURN
 ;
.L40250 IF I2=179 THEN @L39530;BUSHES   
 ;
 IF I2<>176 THEN L40400;LEAVES
 I2=91
 GOTO @L58000;FIND ACORN
 ;
.L40400 IF I2<>102 THEN L40500;SCROLL?
 IF R2<>201 THEN L40900;NORTH WALL?
 X0=LIST2(91);ACORN POS.
 IF X0<>R2 THEN L40900;ACORN HERE?
 LIST2(91)=C8;KILL ACORN
 H9=1;BRIDGE HERE 
 LIST2(177)=R2
 MESSAGE 383;BRIDGE APPEARS
 X0=13
 GOSUB @L42500;SCORING ACTION 
 RETURN
 ;
.L40500 ;
 ;
 ;
.L40900 RETURN  
.L41000 ; 
 ; GIVE C
 GOSUB @L62500;CARRIED? 
 IF S3<>0 THEN L41050  
 MESSAGE 234;NO ONE HERE WANTS
 GOTO @L64650;I2 .
.L41050 IF S3<>148 THEN L41100;XIIZ?
 IF I2<90 THEN L41080;TREASURE
 MESSAGE 336;REFUSES
 RETURN
.L41080 MESSAGE 337;ACCEPTS
 H4=1;PAID  
 GOSUB @L57500;KILL
 X0=80
 LIST2(I2)=X0;UNDERGROUND CAVE
 RETURN
.L41100 IF S3<>147 THEN L41200;WATCHDOG?
 IF I2<>128 THEN L41900;MEAT?
 MESSAGE 371;DOG EATS
 IF H7=0 THEN L41900;PILLS THERE?
 MESSAGE 372;DIES
 LIST2(147)=C8;DOG DEAD
.L41200 IF S3<>143 THEN L41300;KELLF?
 IF I2=93 THEN L41220;BOTTLE
.L41215 GOTO @L53000;DROP I2
.L41220 IF F7=0 THEN L41215;WATER IN IT?
 F7=0   
 MESSAGE 375;THANKS+HINT
 X0=14
 GOSUB @L42500;SCORING
 LIST2(143)=C8;KILL KELLF
.L41300 IF S3<>144 THEN L41400;NEZZON?
 GOSUB @L57500;KILL ITEM
 X0=144
 LIST2(I2)=X0;INTO STONE CELL
 ADD H8,C1;INC COUNTER
 IF H8>2 THEN L41350  
 MESSAGE 380;NEED MORE
 RETURN
.L41350 MESSAGE 381;THANKS +BYE
 LIST2(144)=C8;KILL NEZZON
 RETURN
.L41400 ;
.L41900 GOTO @L57500;KILL
.L42000 ; SAY/SPEAK COMMAND
 ;
 IF I2<>226 THEN L42050;SAID HUMAKAAT?
 GOSUB @L44500;-1 HP
 IF R2=14 THEN L42035;ADJACENT TO DOOR
 IF R2<>31 THEN L42490;IN MARBLE TOWER?
.L42035 MESSAGE 257; HIDDEN BOLT SLIDS BACK
 F1=1;MARBLE TOWER OPENABLE
 RETURN
 ;
.L42050 IF I2<>227 THEN L42100;SAID SATARH?
 GOSUB @L44500;-1 HP
 I2=88;CODEX
 GOSUB @L60000;O2=CODEX HERE/CARRIED?  
 IF O2=0 THEN L42080
 MESSAGE 225;BOOK VANISHES!
.L42080 IF R2<>15 THEN L42090
 MESSAGE 226;BOOK APPEARS!
.L42090 R2=15;BUILDING  
 GOTO @L53000;DROP BOOK   
 ;
.L42100 IF I2<>228 THEN L42200;OBIS?
 GOSUB @L44500;-1 HP
 IF R2<>46 THEN L42200
 X0=LIST2(81);BROOCH POS.
 IF X0<>C7 THEN L42200;BROOCH GONE
 MESSAGE 310;SARC OPENS
 LIST2(81)=R2;BROOCH HERE
 LIST2(158)=R2;MUMMY HERE
 RETURN
.L42200 IF I2<>229 THEN L42300;OLLABIN?
 GOSUB @L44500;-1 HP
 X0=LIST2(158)
 IF X0<>R2 THEN L42300;MUMMY HERE?
 MESSAGE 311;MUMMY DIES
 LIST2(95)=R2;MUMMY DUST HERE
 X0=12
 GOSUB @L42500;SCORING ACTION 
 LIST2(158)=C8
 RETURN
.L42300 ;
.L42490 GOTO @L64400;NOTHING HAPPENS
 ;
.L42500 ;   SCORING ACTION
 X1=LIST4(X0)
 LIST4(X0)=C0
 ADD S6,X1;MODIFY SCORE
 IF S6<6000 THEN L42540
 S6=0;   0 ON OVERFLOW 
.L42540 ;
 RETURN


.L43000 ;
 ; MOVE COMMAND i.e. REMOVE
 ;
 IF I2<>167 THEN L43050;BEANS?
 IF F5=0 THEN @L39700;DISCOVER TRAPDOOR 
.L43050 IF I2<>166 THEN L43100;GRILL
.L43060
 if i3=123 then openwithcrowbar ; >>Mike 12/7/87
 GOSUB @L54900;WHAT WITH *
 IF I2<>123 THEN @L64900;CROWBAR?
.openwithcrowbar
 MESSAGE 284
 LIST2(166)=C8;KILL GRILL
 RETURN
.L43100 ;
 parsex1=list2(i2) ;>>mike 16/2/88
 i1=40 ;>>mike 16/2/88 - allow "take" routine to confirm with "ok"
 if parsex1=c6 then @l22000 ; >>mike 16/2/88 - allow "remove clothes" etc.

 GOTO @L64400;NOTHING HAPPENS  

.L43500 ;
 ; CARRY I2 IF POSSIBLE, ELSE DROP IT
 IF I2>130 THEN L43600
 X1=O1
 ADD X1,C1
 IF X1<O4 THEN @L51000;CARRY
.L43600 GOTO @L53000;DROP
.L44000 ; 
 ; MONSTER ATTACKS
 IF H0=0 THEN L44013
 IF S3>0 THEN L44020 
.L44013 RETURN
.L44020 X0=S3
 X3=S3  
 X4=95     
 SUB X3,X4
 V3=LIST6(X3);MONSTER DAMAGE RATING
 MESSAGE 285;YOU'RE ATTACKED BY
 MESSAGE G1
 MESSAGE X0;MONSTER NAME 
 X3=0;MAGIC ATTACK INDICATOR 
 IF X0=144 THEN @L45500;NEZZON?
 IF X0=145 THEN @L45500;SAZA?
 IF X0=148 THEN @L45500;XIIZ?
 IF X0=149 THEN @L45500;ZIIX?
 IF X0=153 THEN @L45500;DRAGONS?
 IF X0=157 THEN @L45500;VAMPIRE?
 IF X0=158 THEN @L45500;MUMMY?
.L44070 X1=30;MAN'S DODGE
 X2=LIST2(109);RING POS
 IF X2<C6 THEN L44096
 ADD X1,X1;+30 TO DODGE 
.L44096 IF H5>0 THEN @L3850;REFLEC CALC FINISH?
 GOSUB @L45400;RANDOM 0-100 IN R6
 IF R6>X1 THEN L44140;DODGED?
 MESSAGE 286;YOU DODGE
 RETURN
.L44140 MESSAGE 287;DOING
.L44159 RANDOM R7
 IF R7>V3 THEN L44159;RANDOM 0-V3
 IF F8=0 THEN L44165;SHIELD ON?
 IF X3>0 THEN L44170;NO GOOD VERSUS MAGIC 
 R7=0;0 DAMAAGE  
.L44165 IF R7>0 THEN L44170
 MESSAGE 354;NO DAMAGE
 RETURN
.L44170 PRINT R7;NO. DAMAGE
 IF R7>1 THEN L44180
 MESSAGE 353;POINT OF DAM
 GOTO L44185
.L44180 MESSAGE 288;POINTS OF DAM
.L44185 IF X3>0 THEN L44300;MAGIC?
 X3=LIST2(110);SHIELD POS.
 IF X3<C5 THEN L44250;SHIELD CARRIED?
 SUB U5,R7
 MESSAGE 290;SHIELD
 IF U5>200 THEN L44240
 IF U5>0 THEN L44390 
.L44240 MESSAGE 291;SHIELD DESTROYED
 I2=110
 GOTO @L57500;KILL SHIELD
.L44250 X3=LIST2(112);ARM POS.
 IF X3<C6 THEN L44300;ARMOUR WORN?
 SUB U6,R7
 MESSAGE 292;ARMOUR TAKES BLOW 
 IF U6>200 THEN L44290
 IF U6>0 THEN L44390 
.L44290 MESSAGE 293;ARMOUR TOTALED
 I2=112
 GOTO @L57500;KILL ARMOUR
.L44300 SUB U1,R7 
 GOSUB @L45300;0 HP? 
 GOTO L44400;HP LEFT
.L44390 MESSAGE 4;DOT
 RETURN
 ;
.L44400 ; YOU HAVE X HIT POINTS
 ;
 IF U1<600 THEN L44440
 U1=0 
.L44440 MESSAGE 306;YOU HAVE 
 PRINT U1
 IF U1<>1 THEN L44460   
 MESSAGE 222;POINT   
 RETURN
.L44460 MESSAGE 307; HIT POINTS LEFT
 RETURN
 ;
.L44500 ; DECREASE HP BY 1 (SPELLS + MAGIC WORDS)
 SUB U1,C1
 GOSUB @L45300;0 HP?  
 GOSUB @L44400;MESSAGE HP LEFT
 RETURN
.L45000 ;
 ; MAN ATTACKS  
 ;
 U3=5;BASIC ATTACK VALUE
 X1=LIST2(124);DAGGER
 IF X1<C5 THEN L45016
 U3=10;DAGGER VALUE
.L45016 X1=LIST2(120);AXE
 IF X1<C5 THEN L45019
 U3=15;AXE VALUE
.L45019 IF X0<>158 THEN L45023
 U3=0;ONLY SWORD AFFECTS MUMMY 
 ;
.L45023 X1=LIST2(104);SWORD
 IF X1<C5 THEN L45026
 U3=20;SWORD VALUE
.L45026 IF H5>0 THEN @L3846;REFLECT CALC FINISH?
 IF F9=0 THEN L45034;STRONG ON?
 X2=6
 ADD U3,X2;+10  TO DAMAGE
.L45034 X4=S3
 X3=117
 SUB X4,X3
 V2=LIST6(X4);SET MONSTER DODGE
 X3=22
 SUB X4,X3 
 V1=LIST6(X4);SET MONSTER HP
 M1=294
 M8=1;COMBAT IS GO 
 IF F8=0 THEN L45098;SHIELD ON?
 F8=0   
 MESSAGE 351;SHIELD DOWN
.L45098 IF M7=0 THEN L45105;ZAP ATTACK 
 MESSAGE 343;SPELL HITS DOING
 GOTO L45150
.L45105 GOSUB @L64500;YOU ATTACK_
 MESSAGE G1 
 MESSAGE X0;THE MONSTER
 GOSUB @L45400;RANDOM 0-100
 IF R6>V2 THEN L45150
 MESSAGE 297;DODGES
 RETURN

.L45150
 RANDOM R7
 IF R7>U3 THEN L45150;RANDOM 0-ATTCK
 SUB V1,R7
 LIST6(X4)=V1
 MESSAGE 287;DOING
 IF R7>0 THEN L45195;0 DAMAGE?
 MESSAGE 354;NO DAMAGE
 RETURN
.L45195 PRINT R7
 IF R7>1 THEN L45200 
 MESSAGE 353;POINT OF DAMAGE
 GOTO L45210
.L45200 MESSAGE 288;POINTS OF DAMAGE 
.L45210 IF V1=0 THEN L45230
 IF V1>200 THEN L45230
 RETURN
.L45230 MESSAGE 298;SLAYED..
 GOSUB @L64650;I2 . 
 M8=0;COMBAT OFF 
.L45250 GOSUB @L54500;KILL MONSTER IN COMBAT
 IF I2<>158 THEN L45281;MUMMY?
 X0=12
 GOSUB @L42500;SCORE
.L45281 IF I2<>148 THEN L45286;XIIZ
 H4=1;PAID THE HARD WAY ! 
.L45286 IF I2<>144 THEN L45288;NEZZON
 H8=1;PAID NEZZON HARD WAY 
.L45288 ;
 RETURN
.L45300 ;
 ; DEAD AT 0 HIT POINTS?
 ;
 IF U1=0 THEN L45360
 IF U1>6000 THEN L45360;OVERFLOW?
 RETURN
.L45360 MESSAGE 289;DIE 
 GOTO @L49000
.L45400 ;
 ;  RANDOM 0-100 IN R6
 RANDOM R6
 IF R6<101 THEN L45490
 Y1=100
 SUB R6,Y1
 IF R6<101 THEN L45490
 Y1=55
 SUB R6,Y1
.L45490 RETURN
.L45500 ;
 ;  MONSTER'S MAGIC/SPECIAL ATTACKS
 X3=1;SET INDICATOR TO SKIP SHIELD/ARMOUR  
 IF X0<>144 THEN L45540;NEZZON?
 MESSAGE 299;DISEASE
.L45540 IF X0<>145 THEN L45550;SAXA?
 MESSAGE 300;DUST
.L45550 IF X0<>148 THEN L45560;XIIZ
 MESSAGE 301;FIREBALL
.L45560 IF X0<>149 THEN L45570;ZIIX
 MESSAGE 302;LIGHTNING
.L45570 IF X0<>153 THEN L45580;DRAGONS
 MESSAGE 303;FLAMES
.L45580 IF X0<>157 THEN L45590;VAMPIRE
 MESSAGE 304;BITE
.L45590 IF X0<>158 THEN L45600;MUMMY
 MESSAGE 305;BANDAGES
.L45600 ;
 GOTO @L44140;SKIP DODGE CHANCE
;---
.L45800 ;
 ; H0=S3 HOSTILE?
 H0=G1;DEFAULT PEACEFUL UNLESS GHOST  
 IF S3=0 THEN L45895  
 IF M8>0 THEN L45890;MAN STARTED COMBAT?
 IF S3<150 THEN L45895;NON HOSTILE 
 IF S3<>151 THEN L45860;STATUE GUARD? 
 X1=LIST2(106);CLOAK POS
 IF X1=C6 THEN L45895
.L45860 IF S3<>158 THEN L45880;MUMMY 
 X1=LIST2(81);BROOCH POS.
 IF X1=46 THEN L45895
.L45880 ;
.L45890 H0=1   
 IF S3<>157 THEN L45895;VAMPIRE  
 X1=LIST2(107);CRUC POS.
 IF X1<C5 THEN L45895;CARRIED?
 H0=0;UNHOSTILE BECAUSE OF CRUCIFIX  
.L45895 RETURN
;---
.L45900 ;
 ; SAFE UNDERWATER? H1
 H1=0    
 X0=LIST2(116)
 IF X0<C6 THEN L45990;GAS MASK WORN?
 X0=LIST2(117)
 IF X0<C5 THEN L45990;FLASK OF AIR
 X0=LIST2(118)
 IF X0<C5 THEN L45990;TUBING
 H1=1  
.L45990 RETURN
.L46000 ;
 ; ADJUST INPUT (WHEN 2-PART VERB ENTERED) AND TRY AGAIN
 I2=I3
 I3=0  
 GOTO @L6070;JUST AFTER INPUT (TO ACT ON THE ALTERED VERB)
.L46500 ;
 ; PLEASE CONFIRM M1
 GOSUB @L56000;YES/NO?
 IF Y1>0 THEN L46600 
 MESSAGE 246;BE MORE CAREFUL IN FUTURE!
 GOTO @L8000
.L46600 RETURN
.L47000 ;
 ; SAVE ENTERED COMMAND
 I5=I1
 IF I2=0 THEN L47050  
 I6=I2  
.L47050 I7=I3
 I8=I4
 RETURN
.L47500 ;
 ; EXIT (FROM R2 DIRN D1). R3=DEST, S4=DOOR? S5=VISIBLE? 
 EXIT R2 D1 X1 R3
 S4=0  
 S5=0  
 S7=1    
 IF R3=0 THEN L47700  
 IF X1<4 THEN L47600   
 S4=1   
 SUB X1,C4
.L47600 IF X1>1 THEN L47615 
 S5=1     
 GOTO L47640
.L47615 SUB X1,C2
.L47640 IF X1<1 THEN L47660 
 S7=0  
 ;
.L47660 IF H9>0 THEN L47670;BRIDGE EXISTS?
 IF R2=201 THEN L47665
 IF R2<>67 THEN L47670
.L47665 IF R3=201 THEN L47675  
 IF R3=67 THEN L47675   
 ;
.L47670 IF F5>0 THEN L47680;BEANS MOVED?
 IF R2<>108 THEN L47680  
 IF R3<>121 THEN L47680    
.L47675 R3=0;NO EXIT
 S5=0
 ;
.L47680 ;
.L47700 RETURN
.L48000 ;
; MESSAGE DETAILS OF R2  - describe room
 GOSUB @L55000;LIGHT?
 IF L3=0 THEN L48016;CAN HE SEE?  
 IF R2<>53 THEN L48018;FLASHING LIGHTS?
 IF F6>0 THEN L48018;OFF?
 MESSAGE 364 
.L48016 CLS G
 RETURN
.L48018
 X1=400; *
 ADD X1,R2
 GOSUB @L48950; DECIDE IN/ON/BY
 MESSAGE 24;YOU ARE
 MESSAGE M1;IN/ON/BY
 if r2=67 then DescTheHelix ;>>Mike 17/2/88
 MESSAGE 45;A SPACE   
.DescTheHelix
 MESSAGE X1;SHORT ROOM DESC     
 if briefmode=true then NoLongDesc ;>>Mike 15/8/88
 X1=730
 ADD X1,R2 
 MESSAGE X1;LONG ROOM DESCRIPTION  

.NoLongDesc
 MESSAGE 4;DOT SPACE   

;>>mike 15/2/88 if briefmode=true then l48901 ;>>Mike 20/7/87
 IF R2=37 THEN L48901;OMIT EXITS FROM MIRROR ROOM   
 GOSUB @L59000;COUNT EXITS
 if PreventExits=true then l48901 ;>>mike 15/11/87 - must count exits
; even if they're not displayed - so as to set up the "door present" flags
 IF E5=0 THEN L48910;NO EXITS 
 IF E5=1 THEN L48035 
 MESSAGE 21;EXITS LEAD
 GOTO L48050
.L48035 MESSAGE 70;ONLY EXIT LEADS
 ;
.L48050 E3=1;NUMBER OF CURRENT EXIT 
 D1=1;FIRST DIRECTION 
 ;
.L48060 GOSUB @L47500;EXIT? 
 IF S5=0 THEN L48220;NOT VISIBLE
 ;
 M1=7
 ADD M1,D1 
 MESSAGE M1;EXIT DIRN 
 IF S4=0 THEN L48160;DOOR?  
 M1=22
 ADD M1,D2
 MESSAGE M1;(OPEN) DOOR
 ;
 IF D2=0 THEN L48200;DOOR SHUT?
.L48160
 MESSAGE 2; TO_
 M1=400
 ADD M1,R3; ROOM YOU CAN SEE
 IF R3<>70 THEN L48190;LAKE?
 IF F4<>0 THEN L48190 
 MESSAGE 273;LAKE
 GOTO L48200
.L48190 MESSAGE M1
.L48200 IF E3=E5 THEN L48900
 ADD E3,C1
 IF E3=E5 THEN L48215
 MESSAGE 3;COMMA
 GOTO L48220
.L48215 MESSAGE 6;AND
.L48220 ADD D1,C1
 IF D1<14 THEN L48060
 ;
.L48900 MESSAGE 4; . SPACE
.L48901
 gosub @pictureforr2
; X1=500
; ;MESSAGE PICTURE; *
; ADD X1,R2  
; IF X1=LASTPICTURE THEN L48910 ;>> NICK 30/6/86
; LASTPICTURE=X1 ;>> NICK 30/6/86
; CLS G
;;* PICTURE LASTPICTURE ;>> NICK 30/6/86
;;>> NICK 30/6/86 PICTURE P0
.L48910 ;
 ;
 M1=27;YOU CAN SEE
 GOTO @L57000;LIST OBJ
;----
 ; 
.L48950 ; DECIDE ON IN/ON/BY FOR ROOM DESCRIPTION
 ;
 M1=73;DEFAULT IN
 IF R2=1 THEN L48981
 IF R2<15 THEN L48970
 IF R2=34 THEN L48970
 IF R2=51 THEN L48970
 IF R2=54 THEN L48970
 IF R2=70 THEN L48970
 IF R2=179 THEN L48970 
 if r2=201 then l48970 ; >>mike 17/2/88 - on the north wall of a chasm
 IF R2=204 THEN L48970
 GOTO L48971
.L48970 M1=71;  ON
.L48971 IF R2=205 THEN L48980
 IF R2=72 THEN L48980
 IF R2=97 THEN L48980
 IF R2=84 THEN L48980
 GOTO L48981
.L48980 M1=72; BY
.L48981 ;
 RETURN
;----
.L49000 ;
.DIE ;>> ANDY 30/6/86
 X0=10
 SUB S6,X0; SCORE-10 FOR DYING
 IF S6<600 THEN L49020
 S6=0   
.L49020 T2=0  
 MESSAGE 239;YOU'RE DEAD
 GOSUB @L21000;WAIT
 ADD K1,C1
 M1=240;VOICE BOOMS  
 MESSAGE M1
 ADD M1,K1
 GOSUB @L56000;RESURRECT YES/NO?
 IF Y1=0 THEN L49500;ANOTHER GAME 
 MESSAGE 244;GREEN SMOKE
 R2=11
 U1=50
 IF K1<3 THEN @L50000;GOTO LR2  
 MESSAGE 245;SMOKE EXHAUSTED 
.L49500 ;
.L49510 ; PLAY AGAIN?
 GOSUB @L20000;SCORE
 IF M1<>254 THEN NOTEND ;>> NICK 30/6/86
 GOSUB @ASKFINAL ;>> ANDY 30/6/8
.NOTEND ;>> NICK 30/6/86
 GOTO @QUITPLAYAGAIN ;>> NICK 30/6/86
;>> M1=54;PLAY AGAIN?
;>> GOSUB @L56000;YES OR NO
;>> U1=50;RENEW HP
;>> IF Y1>0 THEN @L1000;COLD START 
;>> DRIVER
.L50000 ;
 ; NEW LOCATION
 IF G1=0 THEN L50040;NO GHOST 
 E2=250   
 LIST2(S3)=E2;;OVE GHOST    
 G1=0
 ;
.L50040 R4=R1 
 R1=R2 
 D2=0;DOOR CLOSED 
 ;
 IF H9=0 THEN L50200;BRIDGE EXISTS?
 IF R2=201 THEN L50120
 IF R2<>67 THEN L50200
.L50120 LIST2(177)=R2;POSITION BRIDGE     
 ;
.L50200 IF F0=0 THEN L50300;GLOWING SPHERE EXISTS?  
 LIST2(178)=R2;POSITION SPHERE
 ;
.L50300 GOSUB @L54000;SET STATUS 
 G1=0
 IF S3>0 THEN L50400;MAYBE MEET GHOST IF UNINHABITED
.L50310 RANDOM E1
 IF E1<140 THEN L50310
 IF E1>210 THEN L50310  
 IF E1>160 THEN L50400
 E2=LIST2(E1)
 IF E2<>250 THEN L50400;NOT DEAD (KILLED)    
 if e1=155 then L50400 ;>>mike 21/2/88 - prevent ghost of blacksmith giant
 LIST2(E1)=R2
 G1=76;GHOST PRESENT
 ;
.L50400 ;
 GOTO @L3000
.L51000 ;
 ; GAIN OBJECT I2 (CARRIED)
 IF S3<>158 THEN L51020;MUMMY HERE?
 IF I2<>81 THEN L51020;BROOCH?
 R4=R2;NO ESCAPE
 MESSAGE 224;MUMMY ATTACKS
.L51020 R2=C5
.L52000 ;
 ; GAIN OBJECT I2 (IN R2) 
 IF I2<>82 THEN L52009;COINS?
 IF B0>0 THEN L52009;OUT OF GRATE?
 MESSAGE 366;OUT OF REACH
 RETURN
.L52009 IF I2<>125 THEN L52011;HANDLE?
 F3=0;UN-INSERT    
.L52011 IF I2=99 THEN L52013;PILLS?
 IF I2<>122 THEN L52020;CHALK?
.L52013 IF H8>2 THEN L52020;PAID 3?
 IF S3=0 THEN L52020;NEZZON DEAD?
 M1=385
 GOSUB @L64500;PAY UP
 RETURN
.L52020 IF I2<>89 THEN L52028;CRYSTAL OF XAX?
 X0=LIST2(153);DRAGONS POS.
 IF X0<>C7 THEN L52028;DEAD?
 i1=0 ;prevent any confirmation. >>mike 12/7/87
 MESSAGE 201;DRAG'S APPEAR
 X3=54
 LIST2(153)=X3
 R4=X3;NO ESCAPE
.L52028 E2=LIST2(I2)
 IF E2>C7 THEN L52100
 IF O1<O4 THEN L52100
 MESSAGE 47;HANDS TOO FULL TO TAKE
 GOSUB @L64650;I2 .
 GOTO @L8000
.L52100
 if i1=42 then takeconfirm ; wear
 if i1<>40 then takenoconfirmation
.takeconfirm
 message 2006 ; ok.
.TakeNoConfirmation
 GOSUB @L57500;KILL OBJECT
 LIST2(I2)=R2
 ADD O1,C1
 RETURN
;---
.L53000 ;
; DROP OBJECT
 if i1=0 then dropnoconfirm ;>>mike 12/7/87
 message 2006 ; ok.
.dropnoconfirm
 GOSUB @L57500;KILL OBJECT
 LIST2(I2)=R2
 RETURN
;---
.L53900
; RANDOM EVENTS 
 RANDOM R7
 IF R2<>16 THEN L53995
 IF R7<150 THEN L53995;NOTHING HAPPENS
 IF R7>200 THEN L53990
 MESSAGE 255; VOICE SAYS HUMAKAAT
 RETURN
.L53990 MESSAGE 256; VOICE SAYS SATARH
.L53995 RETURN
 ;
.L54000
;
; SET STATUS
 S3=140;SET S3 TO THE FIRST CREATURE HERE (0 IF NONE)
.L54110 E2=LIST2(S3)
 IF E2=R2 THEN L54120
 ADD S3,C1
 IF S3<163 THEN L54110
 S3=0 
.L54120 ;
 RETURN
.L54500 ;
 ; KILL I2 IN COMBAT     
 GOSUB @L57500;KILL I2  
 E2=250
 LIST2(I2)=E2
 RETURN
.L54900 ;
 ; WHAT WITH?
 ;
.L54930 M1=262;WHAT WITH?
 GOSUB @L56500;TAKE +SORT **** maybe some code missing - lost in transfer?
 if processed<>false then ZeroI2 ; >>mike 15/2/88
 I2=I1
 IF I2<80 THEN L54930;OBJECT ONLY
 X0=LIST2(I2)
 IF X0=R2 THEN L54990
 IF X0<C5 THEN @L63800;CARRIED/WORN?
.L54990 RETURN

.ZeroI2 ;>>mike 15/2/88
 i2=0
 return

.L55000 ;
 ; SET LIGHT FLAGS
 L3=0   
 IF F0>0 THEN L55485;RAISIN SUN?
 IF R2<77 THEN L55485;LIT ROOM
 IF L4=0 THEN L55490;LAMP LIT?
 X0=LIST2(127)
 IF X0=R2 THEN L55060
 IF X0<C5 THEN L55490;LAMP CARRIED WORN
.L55060 ;
.L55485 L3=1   
 L1=1 
 T2=0      
.L55490 RETURN
.L55500 ;; MESSAGE I1-I4
 ;MESSAGE 5  
 ;PRINT I1  
 ;MESSAGE 3 
 ;PRINT I2   
 ;MESSAGE 3      
 ;PRINT I3     
 ;MESSAGE 3     
 ;PRINT I4  
 ;MESSAGE 7   
 RETURN  

.yesornoNoM1
 m1=0 ;>>mike 15/2/88

.yesorno
.L56000 ;
; YES OR NO
 Y1=1;YES  
 nomoreinput=false ; >>mike 15/2/88

.L56060 GOSUB L56500;INPUT
 IF I1=1  THEN L56200;NO
 IF I1=31 THEN L56200;NEGATIVE
 IF I1=30 THEN L56300;YES
 if i1=70 then @RamLoad ; >>mike 16/2/88
 if i1<>71 then NotOops ;>>mike 16/2/88
 gosub @TryOops ;>>mike 16/2/88
 if result=true then @AfterRestore ;>>mike 16/2/88

.NotOops
 MESSAGE 56;YES or NO
 GOTO L56060    
.L56200 Y1=0;NO  
.L56300 RETURN


.L56500 ;
; INPUT
.ALOOP ;>> ANDY 30/6/86
 MESSAGE 5;CR
 MESSAGE M1;PROMPT
;>>mike 20/6/87 - was MESSAGE 49; ? CR 
;>>.L56540 GOSUB @GETI1I2I3I4 ;*INPUT I1,I2,I3,I4
;>>mike 20/6/87 - was GOSUB @ASKINPUT ;>> ANDY 30/6/86
.aloop1
 gosub @getnextword
 gosub @checknoun
 if processed<>false then aloop2 ;>>mike 15/2/88
 if object<>0 then aloop2

 searchtype=verbtype
 gosub @checktype
 object=value
 if value<>0 then aloop2
.aloop1a
 if nomoreinput=false then aloop1
 nomoreinput=false ; allow further input
 goto aloop1

.aloop2
; got an object returned
; return it in i1
 i1=object
;>>mike 20/6/87- was IF RESULT=FALSE THEN ALOOP ;>> ANDY 30/6/86
 RANDOM R0
;>> IF I4=0 THEN L56540  
;>>mike 20/6/87-was GOSUB @L59500;; SORT INPUT
 RETURN



.L57000 ;
 ; MESSAGE M1, OBJECTS IN R2
 ; O5=NUMBER
 O5=0    
 E1=80 
.L57032 E2=LIST2(E1)
 IF E2<>R2 THEN L57035
 ADD O5,C1 
.L57035 ADD E1,C1
 IF E1<181 THEN L57032 ;>>mike 20/6/87 - was 230
 ;
 IF O5=0 THEN L57490 
 MESSAGE M1;E.G YOU CAN SEE
 O3=1  
 E1=80
.L57150 E2=LIST2(E1)
 IF E2<>R2 THEN L57450
 ;
 IF E1<>S3 THEN L57210;INHABITANT?
 MESSAGE G1;GHOST?
 ;
.L57210
 MESSAGE E1;OBJECT NAME  
 ;
 IF E1<>174 THEN L57305;GATE
 IF F2>0 THEN L57305
 MESSAGE 190;LOCKED
 ;
.L57305 IF E1<>125 THEN L57310;HANDLE
 MESSAGE F3;INSERTED?
 ;
.L57310 IF E1<>127 THEN L57315;LAMP  
 MESSAGE L4;SHINING?
 ;
.L57315 IF E1<>93 THEN L57320;BOTTLE
 MESSAGE F7;FULL OF WATER?
 ;
.L57320 ;
 ;
 IF O3=O5 THEN L57460
 ADD O3,C1
 IF O3=O5 THEN L57420
 MESSAGE 3;COMMA
 GOTO L57450
.L57420 MESSAGE 6;AND
.L57450 ADD E1,C1
 IF E1<230 THEN L57150
.L57460 MESSAGE 4;DOT SPACE
 ;
.L57490 RETURN
;---
.L57500 ;
 ; KILL I2 
 IF I2<>105 THEN L57530;BRACERS
 IF O4<7 THEN L57530
 GOSUB @L63900;CAN'T, TO BRACERS   
 GOTO @L8000
 ;
.L57530 E2=LIST2(I2)
 IF E2<C5 THEN L57560
 SUB O1,C1
.L57560 IF O1<99 THEN L57900
 O1=0
.L57900 LIST2(I2)=C8
 IF I2<140 THEN L57920;RESET HITS ON DEATH
 IF I2>160 THEN L57920
 E1=I2
 E2=140
 SUB E1,E2
 E2=LIST5(E1)
 ADD E1,C1
 LIST6(E1)=E2  
 G1=0;CAN'T BE A GHOST ABOUT
.L57920 RETURN
.L58000 ;
 ; FIND I2 IF HIDDEN
 MESSAGE 66;YOU FIND
 IF I2<80 THEN L58060
 E2=LIST2(I2)
 IF E2=C7 THEN L58100;HIDDEN
.L58060 O2=0 
 MESSAGE 1;SPACE
 MESSAGE 60;NOTHING  
 RETURN
.L58100
 GOSUB @L64650;I2 . 
 i1=0 ; prevent confirmation message >>mike 12/7/87
 GOSUB @L43500;CARRY I2 IF POSS
 O2=1
 RETURN
.L58500 ;
 ; RIGHT TURN
 X1=D4
 ADD D4,C2
 ;
 IF X1=2  THEN L58552
 IF X1<>4 THEN L58560
.L58552 ADD D4,C1
 ;
.L58560 IF X1=3  THEN L58562
 IF X1<>5 THEN L58570
.L58562 SUB D4,C1
 ;
.L58570 IF D4<9 THEN L58900
 X1=8
 SUB D4,X1
 ;
.L58900 RETURN
.L59000 ;
 ; D5=#DOORS E5=#VISIBLE EXITS
 D5=0   
 E5=0  
 D1=1 
.L59060 GOSUB @L47500;EXIT. S4=DOOR? S5=VISIBLE?
 ADD D5,S4
 ADD E5,S5
 ADD D1,C1
 IF D1<14 THEN L59060
 ;
 RETURN
.L59500 ;
 ; SORT INPUT (BUT LEAVE I1 ALONE IF IT'S ALREADY A VERB)
 IF I1<80 THEN L59900;LEAVE ALONE IF STARTS WITH VERB
.L59570 IF I3=0 THEN L59580;SWAP I2 & I3?        
 IF I2<I3 THEN L59580
 X1=I2
 I2=I3
 I3=X1
.L59580 IF I2=0 THEN L59900 
 IF I1>I2 THEN L59585
 GOTO L59900
.L59585 X1=I1
 I1=I2
 I2=X1
 GOTO L59570;I1/I2 SWAPPED, SO TRY I2/I3 AGAIN  
.L59900 RETURN
.L60000 ;
 ; O2=HERE/WORN/CARRIED
 GOSUB L60500;O2=HERE?
 IF O2=0 THEN L61000;O2=WORN/CARRIED?   
.L60500 ;
 ; O2=ON THE GROUND?
 IF I2<>252 THEN L60540;DOOR?
 O2=D5
 RETURN
.L60540 O2=1 
 if i1=75 then l60800 ;>>mike 2/8/87 - always present for cheat mode
 IF I2<80 THEN L60700
 IF I2>190 THEN L60700   
 E2=LIST2(I2)
 IF E2=R2 THEN L60800 
.L60700 O2=0   
.L60800 RETURN
.L61000 ;
 ; O2=WORN/CARRIED?
 O2=1  
 IF I2<80 THEN L61100 
 IF I2>190 THEN L61100   
 E2=LIST2(I2)
 IF E2>C7 THEN L61200
.L61100 O2=0    
.L61200 RETURN
.L61500 ;
 ; I2 HERE/CARRIED?
 GOSUB L61000;O2=WORN/CARRIED? 
 IF O2=0 THEN L62000;HERE? 
 RETURN
.L62000 ;
 ; IS I2 HERE?
 GOSUB @L60500;O2=HERE?
 IF O2=0 THEN L63800;CAN'T SEE
 RETURN
.L62500 ;
 ; IS I2 CARRIED?
 GOSUB @L61000;O2=WORN/CARRIED?
 IF O2=0 THEN L63700;DON'T HAVE    
 RETURN
.L63000 ; ALREADY OPEN
 MESSAGE 64
 RETURN
.L63100 ; ALREADY CLOSED
 MESSAGE 65
 RETURN
.L63200 ; WIRTS MIRT BUD?
 MESSAGE 260
 RETURN

.L63300 ; HELP! - GET CLUESHEET     
 MESSAGE 248
 RETURN

.brief
 PreventExits=true ; >>mike 15/2/88 - default to exits on
 briefmode=true
.done
 message ok
 return

.verbose
 PreventExits=false ; >>mike 15/2/88 - default to exits off
 briefmode=false
 goto done

.L63400 ; ALREADY HAVE I2
 MESSAGE 43;ALREADY HAVE
 GOTO L64650;I2 .

.L63500 ; CAN'T
 MESSAGE 39
 GOTO @L8000

.L63600 ; SILLY
 MESSAGE 40
 GOTO @L8000

.L63700 ; DON'T HAVE I2
 MESSAGE 42
 GOTO L64600

.L63800 ; CAN'T SEE I2
 MESSAGE 41
 GOTO L64600

.L63900 ; CAN'T TO I2 . 
 M1=77
 GOSUB @L64500
 GOTO L64650

.L64000 ; NOTHING UNDERSTOOD
 M1=29
 GOTO L64500

.L64100 ; MISSING OBJECT
 M1=32
 GOTO L64500

.L64200 ; UNRECOGNISED OBJECT
 M1=35
 GOTO L64500

.L64300 ; TOO COMPLEX!
 MESSAGE 38
 GOTO @L6000

.L64400 ; NOTHING HAPPENS
 M1=50
.L64500 ; MESSAGE ERROR MSG M + 0 TO 2
 ADD M2,C1
 IF M2<3 THEN L64560
 M2=0   
.L64560 ADD M1,M2
 MESSAGE M1;ERROR MESSAGE
 RETURN
.L64600 ; MESSAGE I2 . GOTO @L8000    
 GOSUB L64650
 GOTO @L8000
.L64650 ; MESSAGE I2 .
 MESSAGE 1
 IF I2<80 THEN L64800;THAT
 IF I2>190 THEN L64800;THAT  
 IF I2<>S3 THEN L64675;INHABITANT?
 MESSAGE G1;GHOST?
.L64675 MESSAGE I2 
 MESSAGE 4;DOT
 RETURN
 ; MESSAGE I3 .
 MESSAGE 1
 IF I3<80 THEN L64800;THAT
 IF I3>190 THEN L64800;THAT     
 IF I2<>S3 THEN L64725;INHABITANT?    
 MESSAGE G1;GHOST? 
.L64725 MESSAGE I3        
 MESSAGE 4;DOT C
 RETURN
.L64800 ; THAT
 MESSAGE 44
 RETURN
.L64900 ;
 MESSAGE 316;YOU FAIL
 RETURN
.L64930 ; ALREADY DONE THAT       
 MESSAGE 320
 GOTO @L8000
.L64960 ; OK !
 MESSAGE 75
 RETURN
;---
.ISOBJECTHERE
 if verb<>101 then iohNotCheat
 if object=260 then iomtrue
 if object=261 then iomtrue

.iohNotCheat
 if verb=65 then iomtrue ; cast
 if object<226 then ioh1
 if object<252 then iomtrue ; spells, magic words etc.
.ioh1
 i2=object
 gosub @l60500 ; is i2 on ground?
 result=o2
 return

;; return RESULT=TRUE if OBJECT is on floor in current room
; IF OBJECT>MAXOBJECT THEN IOMFALSE ;>> NICK 30/6/86
; IF OBJECT<MINOBJECT THEN IOMFALSE ;>> NICK 30/6/86
; IF OBJECT>197 THEN IOMFALSE
; IF OBJECT>153 THEN IOTUNTAKEABLE
; RESULT=LIST2(OBJECT)
; IF RESULT=R1 THEN IOMTRUE
; GOTO IOMFALSE
;---
.IOTUNTAKEABLE
 RESULT=LIST1(OBJECT)
 IF RESULT=R1 THEN IOMTRUE
 GOTO IOMFALSE
 ;---
.ISOBJECTCARRIED
 i2=object
 gosub @l61000 ; o2=i2 worn/carried
 result=o2
 return
;---

; return RESULT=TRUE if OBJECT is carried by player
 IF OBJECT>MAXOBJECT THEN IOMFALSE ;>> NICK 30/6/86
 IF OBJECT<MINOBJECT THEN IOMFALSE ;>> NICK 30/6/86
 IF OBJECT>153 THEN IOMFALSE
 RESULT=LIST2(OBJECT)
 IF RESULT>253 THEN IOMTRUE
 IF RESULT=W7 THEN IOMTRUE
 GOTO IOMFALSE
;---
.IOMFALSE
 RESULT=FALSE
 RETURN
.IOMTRUE
 RESULT=TRUE
 RETURN
;--- 
.SETUPROOM
 ROOM=R1
 RETURN
;---
.AUTHORS
 MESSAGE 2319
 RETURN
;---
;;---
.DESCOBJPARSEX1
 LASTWORDPRINTED=PARSEX1
 IF PARSEX1>179 THEN DESCOBJRET
 PARSEX2=OBJECTDESCBASE
 ADD PARSEX2,PARSEX1
 MESSAGE PARSEX2
.DESCOBJRET
 RETURN
;;---
.pictureforr2
 cif IncludePictures

 x1=18 ; red mountain pic for volcano ;>>Mike 20/2/88
 if r2<9 then sdrawPictureX1
 x1=26 ; river cave ;>>mike 17/2/88
 if r2<73 then NotRiverCave ;>>mike 17/2/88
 if r2<87 then sdrawPicturex1 ;>>mike 17/2/88

.NotRiverCave
 x1=20 ; mists of time
 if r2=53 then sdrawpicturex1
 if r2=199 then sdrawpicturex1

 x1=12 ; beside ruined cottage
 if r2=15 then sdrawpicturex1

 x1=16 ; grassy plain
 if r2<17 then sdrawpicturex1

 x1=17 ; forest
 if r2=17 then sdrawpicturex1

 x1=19 ; castle
 if r2<20 then sdrawpicturex1

 x1=17 ; forest
 if r2<23 then sdrawpicturex1

 x1=21 ; panelled corridor
 if r2=27 then sDrawPicturex1
 if r2<43 then notcorridor
 if r2<46 then sDrawPicturex1

.notcorridor
 x1=28 ; spiral staircase
 if r2=87 then sDrawPicturex1
 if r2=96 then sDrawPicturex1
 if r2=131 then sDrawPicturex1
 if r2=132 then sDrawPicturex1
 if r2=182 then sDrawPicturex1
 if r2<31 then notstaircase
 if r2<34 then sDrawPicturex1
 goto NotStairCase
;---
.sDrawPictureX1
 goto @DrawPictureX1
;---
.notstaircase
 x1=25 ; cave temple
 if r2=47 then sDrawPicturex1
 if r2=54 then sDrawPicturex1
 if r2=72 then sDrawPicturex1

 x1=15 ; lakeshore
 if r2=70 then sDrawPicturex1
 if r2=71 then sDrawPicturex1
 if r2=73 then sDrawPicturex1

 x1=24 ; store room
 if r2=99 then sDrawPicturex1

 x1=31 ; big cave
 if r2<72 then sDrawPicturex1

 x1=27 ; cave tunnel
 if r2<108 then sDrawPicturex1

 x1=24 ; store room
 if r2=155 then sDrawPicturex1
 if r2<110 then sDrawPicturex1

 x1=23 ; long room
 if r2<145 then notpanelled
 if r2<149 then sDrawPicturex1
 if r2=159 then sDrawPicturex1

.notpanelled
 x1=22 ; wood room
 if r2=153 then sDrawPicturex1

 x1=29 ; corridor
 if r2=152 then sDrawPicturex1
 if r2=157 then sDrawPicturex1
 if r2=158 then sDrawPicturex1
 if r2<193 then notsquarecorridor
 if r2<196 then notsquarecorridor

.notsquarecorridor
 x1=27 ; cave tunnel
 if r2<159 then sDrawPicturex1

;>>>159
 x1=31 ; big cave
 if r2<215 then sDrawPicturex1

; anything else just leaves current picture displayed
 cend
 return
;---
.rude
 message 1302 ; response
 goto @l8000
;-----