; Price of Magik Asource
;
; M.J.Austin 10/2/86
;
; Verb Handlers
;
;>>mike 17/2/88 - all vars moved to acore.txt
;VAR
; CANDLEBURNING
; SHIELDSTRENGTH ARMOURSTRENGTH MAILSTRENGTH
; ATTACKER TARGET WEAPON WEAPONSTRENGTH
; SAVEHIPOS SAVEPOS SAVEOBJECT
; SANITYOFFSET AGE
; HORRORCOMING PANELOPENED CANDLELIFE
; STONEUNLOCKED
; PLAINROOM
; GENERALSAVE ADDSCORESAVE
; GIFTGIVEN
; SolvedTMandRedMoon
;
;;>>mike 17/2/88 - moved from ASPECIAL.TXT
; MANDRAKEPULLED
; OTPOS ; OTHIPOS
; SPELLDURATION
; MOONTOUCHES
; ExitsOn
;

BEGIN

.CONJUGATEX1
; depending on the object represented by X1,
; return RESULT= CONJA, CONJAN, CONJSOME, CONJNOTHING
; and ANIMATE=TRUE or FALSE
 ANIMATE=TRUE
 IF X1=USER THEN SCONJNOTHING
 IF X1=ANTS THEN SCONJSOME
 IF X1=MYGLAR THEN SCONJNOTHING
 IF X1=FISH THEN SCONJA ; animate, so need to specify it here
 ANIMATE=FALSE
 IF X1=NASTY THEN SCONJNOTHING
 IF X1=AXE THEN SCONJAN
 IF X1=ELDERCROSS THEN SCONJAN
 IF X1=EYEBRIGHT THEN @SCONJSOME
 IF X1=ASHES THEN SCONJSOME
 IF X1=ARMOUR THEN SCONJSOME
 IF X1=BANDAGES THEN SCONJSOME
 IF X1=BONES THEN SCONJSOME
 IF X1=BOOKS THEN SCONJSOME
 IF X1=CHEST THEN SCONJSOME
 IF X1=GOLD THEN SCONJSOME
 IF X1=OILYRAG THEN SCONJAN
 IF X1=ROBES THEN SCONJSOME
 IF X1=ALTARCHAPEL THEN SCONJAN
 IF X1=ALTARTEMPLE THEN SCONJAN
 IF X1=INSCRIPTION THEN SCONJAN
 IF X1=OIL THEN SCONJSOME
 IF X1=SNOW THEN SCONJSOME
 IF X1=SILVERMAIL THEN SCONJSOME
.SCONJA
 RESULT=CONJA
 RETURN
;
.SCONJNOTHING
 RESULT=CONJNOTHING
 RETURN
;
.SCONJAN
 RESULT=CONJAN
 RETURN
;
.SCONJSOME
 RESULT=CONJSOME
 RETURN
;---
.DESCANOBJX1
; for obj 'X1', print a obj or an obj
 GOSUB CONJUGATEX1
 IF RESULT=CONJSOME THEN @DOSOME
 IF RESULT=CONJAN THEN @DOAN
 IF RESULT=CONJNOTHING THEN DOOBJ
 MESSAGE 20 ; a_
.DOOBJ
 GOTO @DESCOBJX1
;---
.WORDS
 SCREEN T ; TURN OFF GRAPHICS SCREEN. THIS COMMENT IS ESSENTIAL
 GOTO @DONE
;---
.PICTURE
 SCREEN G C0 ; TURN ON GRAPHICS SCREEN. THIS COMMENT IS ESSENTIAL
 LASTPICTURE=0
 GOTO @DESCRIBEROOM
;---
.MOVE
; MOVEMENT. CLIMB AND OTHER TRANSITIVE MOVEMENT VERBS DO NOT COME HERE
 IF PREP<>0 THEN @DONTUNDERSTAND
 IF OBJECT<>NULLOBJECT THEN @DONTUNDERSTAND
 DIR=VERB
.MOVEDIR
 FROM=ROOM
 GOSUB @CHECKEXIT
 IF DEST=0 THEN NOEXIT
; check if player is carrying too much - if so, drop something
; IF WEIGHTCARRIED<CARRYCAPACITY THEN MOVEOK
; SEARCHPOS=PLAYER
; HISEARCHPOS=NONSPECIFIC
; GOSUB INITGETOBJ
;.MOVE3
; GOSUB GETNEXTOBJECT
; IF OBJECT=0 THEN MOVEOK
; POS=ROOM
; HIPOS=0
; CONFIRMMESSAGE=0
; GOSUB MOVEOBJECT
; IF RESULT=FALSE THEN MOVE3 ; can't drop that, so try another object
; MESSAGE 2170 ; a you move,_
; X1=OBJECT
; GOSUB DESCTHEOBJX1
; MESSAGE 2271 ; slips from your hand

.MOVEOK
 GOTO @NEWLOCATION
;
.NOEXIT
 M1=2114 ; NO EXIT
 GOTO @ERRORM1
;---
.WIN
 GOSUB @ABSCANCELINPUT
 SCOREINDEX=SCEND
 GOSUB @ADDSCORE
 MESSAGE 2670 ; win outright - want nice ending or nasty ending ?
 GOSUB @YESORNO
 M1=2672 ; nasty ending
 IF RESULT=FALSE THEN WINNICE
 M1=2671 ; nice ending
.WINNICE
 MESSAGE M1

 message cr ;>>Mike 21/2/88
 gosub @Score ;>>mike 21/2/88
;>>mike 21/2/88 if solvedTMandRedMoon=false then notsolvedall
;>>mike 21/2/88 message 2316 ; nice message

.notsolvedall
; GOSUB @SCORE
 GOTO RESTARTORRESTORE
;---
.DEATHAGE
 MESSAGE 3528 ; die of old age

.DEATH
 gameover=true
 MESSAGE 2206 ; you are dead
 CONFIRMMESSAGE=3558 ; and felt (second part of message)
 GOSUB @ABSSCORE

.RESTARTORRESTORE
 MESSAGE 2209 ; type restart or restore
 GOSUB @ABSCANCELINPUT
.ROR1
 GOSUB DEADCALLVERB
; if get back here, option failed
 GOTO ROR1
;---
.DEADCALLVERB
 SUPRESSCHECKING=TRUE
 NOMOREINPUT=FALSE
 GOSUB @GETNEXTWORD
 SEARCHTYPE=VERBTYPE
 GOSUB @CHECKTYPE
 IF VALUE=IRESTORE THEN @DORESTORE
 IF VALUE=IQUIT THEN @STARTOver ;>>mike 17/2/88 - was STARTGAME
 IF VALUE=IOOPS THEN @OOPS
 if value=iramSave then @RamSave ;>>mike 17/2/88


.quitret
 RETURN
;
;---
.QUIT
 MESSAGE 2200 ; really restart ?
 GOSUB @YESORNO
 IF RESULT=FALSE THEN QUITRET
.QUITPLAYAGAIN
 GOSUB @ABSCANCELINPUT
 MESSAGE 2201 ; press space to play again
.QUITLOOP
 X1=3 ; OSRDCH
 LINPUT(C0)=X1
 DRIVER
 X1=LINPUT(C1)
 IF X1<>32 THEN QUITLOOP
 MESSAGE CR
 GOTO @STARTGAME
;---
;.SAVE
; SAVE
;.SAVERET
; RETURN
;;---
;.RESTORE
; MESSAGE 2202 ; really restore ?
; GOSUB @YESORNO
; IF RESULT=FALSE THEN RESTOREEND
;
;.DORESTORE
; GOSUB @LENSLOK
; IF RESULT=FALSE THEN AFTERRESTORE
;
; RESTORE
; OOPSPOS=1 ; musn't do an oops now
; OOPSPOSEND=0
.gameAFTERRESTORE
 LASTPICTURE=0 ; allow picture to be drawn
;.RESTOREEND
 GOSUB @DESCRIBEROOM
 NOMOREINPUT=FALSE
 EOL=FALSE
 GOSUB @GETNEXTWORD
 GOTO @CANCELINPUT
;---
.EXAMINE
 IF PREP<>0 THEN @WHERE
 IF OBJECT<MAXOBJECTPLUSONE THEN EXAMINE1
 MESSAGE 2112 ; can't tell you any more
.EXAMRET
;.RESTORERET
 RETURN

.EXAMINE1
 GOSUB @SPECIALPREEXAMINE
 IF RESULT=TRUE THEN EXAMRET ; something prevents main examine message
 X1=EXAMINEMESSAGES
 ADD X1,OBJECT
 MESSAGE X1
 MESSAGE DOT
 GOSUB @SPECIALEXAMINE
 SEARCHPOS=OBJECT
 HISEARCHPOS=NONSPECIFIC
 GOTO @PRINTOBJECTS
;---
.INVENTORY
 SEARCHPOS=PLAYER
 HISEARCHPOS=NONSPECIFIC
 GOSUB @PRINTOBJECTS
 IF TOTALOBJECTFOUND<>0 THEN INVENTRET
; Nothing found, so print appropriate null message
 MESSAGE 2106 ; nothing carried
.INVENTRET
 RETURN
;---
;.CHECKIFPLAYERON
;; See if player can manipulate 'OBJECT'
;; i.e. is player standing on it or something
;; Return RESULT=TRUE if ok to take it
; OBJECTSAVE=OBJECT
; X1=HICURRENTPOS(PLAYER)
; IF X1=0 THEN CIPOOK
; X1=CURRENTPOS(PLAYER)
; IF X1=OBJECT THEN @CIPOFAIL
;; Now scan objects contained by what he's standing on
;; to see if there is any clash
; GOSUB INITGETOBJ
; OBJECT=0
; HISEARCHPOS=NONSPECIFIC
; SEARCHPOS=OBJECT
;.CIPO1
; X1=PLAYER
; GOSUB GETNEXTOBJX1 ; X1=MAXOBJECT
; IF OBJECT=0 THEN CIPOOK
; IF0OBJECT=PLAYER THEN CIPO1
;.CIP_FAIL
; RESULT=FALSE
; OBJECT=_BJECTSAVE
; REDURN
;.CIPOOK
; OBJECT=OBJECTSAVE
; RESULT=TRUE
; RETURN
;---
.TAKE
; Chesk for prepositions e.g. GET ON,0GET OFF
 IF PREP<>0 THEN @TAKEFROM
.TAKEIT
 OBJECT=NOUN1
 POS=PLAYER
 HIPOS=CARRIED
 CONFIRMMESSAGE=2067 ; takes
 IF TALKINGTONPC=TRUE THEN @MOVEOBJECT
 CONFIRMMESSAGE=2150 ; taken
 GOTO @MOVEOBJECT
;
.TAKEFROM
; got a preposition e.g. TAKE noun FROM ; or TAKE noun IN etc.
 OBJECT=NOUN1
 POS=NOUN2
 SAVEHISEARCHPOS=HISEARCHPOS
 HIPOS=NONSPECIFIC
 GOSUB @CHECKOBJECTPOS
 HISEARCHPOS=SAVEHISEARCHPOS
 OBJECT=NOUN2
 IF RESULT=FALSE THEN @ITSNOTTHERE
 GOTO TAKEIT
;---

.STAND
 IF PREP=ON THEN STANDON
 IF PREP=OFF THEN STANDOFF
 IF PREP=UP THEN STANDUP
 IF PREP=IPIN THEN STANDIN
 IF PREP=IPOUT THEN STANDOUT
 GOTO @DONTUNDERSTAND
;---
.STANDOUT ; get out (e.g. of boat)
.STANDOFF
.STANDUP
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN @SILLY
 DEST=ROOM
 HIDEST=0
 GOTO @NEWLOCATION
;
.BOARD ; e.g. boat
 PREP=ON

.STANDON
.STANDIN
 HIDEST=PREP ; ON, IN ETC.
 GOTO STANDDEFAULTS
;---
.CANTSEEOBJECT ; can't see object to VERB PREP. (e.g. STAND ON)
 MESSAGE 2042 ; can't see anything to
 M1=VERBOFFSET
 ADD M1,VERB
 MESSAGE M1
 M1=PREPOFFSET
 ADD M1,PREP
 MESSAGE M1
 MESSAGE 2045 ; that.
 RETURN
;---
.LIE
 HIDEST=LIEON
 GOTO STANDDEFAULTS
.SIT
 HIDEST=SITON

.STANDDEFAULTS
; e.g. SIT DOWN, LIE DOWN
 IF PREP<>0 THEN STANDDEF1
 PREP=ON

.STANDDEF1
 PROCESSED=FALSE ; will be set true when has stood on something
;
 OBJECT=WOODPILE
 GOSUB TRYON
 OBJECT=BOAT
 GOSUB TRYON
 OBJECT=OTABLE
 GOSUB TRYON
 OBJECT=BLUEBOX
 GOSUB TRYON
 OBJECT=DESK
 GOSUB TRYON
;
; IF VERB<>ILIE THEN SNONOTLIE
;; things which can only default for lieing on

;.SNONOTLIE
 IF VERB<>ISIT THEN SNONOTSIT
; things which can only default for sitting on

.SNONOTSIT
 IF PROCESSED=FALSE THEN CANTSEEOBJECT
 RETURN
;---
.TRYON
 IF NOUN2=OBJECT THEN STANDONOK
 GOSUB TRYOBJECT
 IF RESULT=TRUE THEN STDEFAULTOK
 RETURN
;---
.TRYOBJECT
; return RESULT=TRUE if OBJECT is present, or NOUN2=OBJECT
; used in input parsing when NOUN2 may or may not be specified
; read in conjunction with TRYON etc.
 IF PROCESSED=TRUE THEN TRYOBJFAIL
 GOSUB @CHECKIFPRESENT
 IF RESULT=FALSE THEN TRYOBJFAIL
 IF NOUN2=NULLOBJECT THEN TRYOBJTRUE
 IF NOUN2=OBJECT THEN TRYOBJTRUE
.TRYOBJFAIL
 RESULT=FALSE
 RETURN
.TRYOBJTRUE
 RESULT=TRUE
 RETURN
;---
;.ECHOCOMMAND
; MESSAGE 2190 ; (
; X1=VERBOFFSET
; ADD X1,VERB
; MESSAGE X1
; X1=NOUN1
; GOSUB DESCOBJX1
; X1=PREPOFFSET
; ADD X1,PREP
; MESSAGE X1
; X1=OBJECT
; GOSUB DESCTHEOBJX1
; MESSAGE 2191 ; )
; RETURN

.STDEFAULTOK
; GOSUB ECHOCOMMAND
.STANDONOK
 PROCESSED=TRUE
 HIDEST=PREP ;???HIPOS
 DEST=OBJECT
 GOTO @NEWLOCATION
;---
.DONE
 MESSAGE 2153 ; done
 RETURN
;---
.WEAR
 X1=HICURRENTPOS(NOUN1)
 IF X1<>WORN THEN WEAR1
 X1=CURRENTPOS(NOUN1)
 IF X1<>PLAYER THEN WEAR1
 MESSAGE 2147 ; already wearing that
 RETURN
.WEAR1
 IF PREP<>0 THEN WHERE
 POS=PLAYER
 HIPOS=WORN
 CONFIRMMESSAGE=2152 ; worn
 GOTO @MOVEOBJECT
;---
.REMOVE
 POS=PLAYER
 HIPOS=CARRIED
 CONFIRMMESSAGE=OK
 GOTO @MOVEOBJECT
;---
.DROP
; DROP prep is filtered off by CONVERTVERB
 POS=ROOM
 HIPOS=0
 CONFIRMMESSAGE=2066 ; drops
 IF TALKINGTONPC=TRUE THEN @MOVEOBJECT
 CONFIRMMESSAGE=2151 ; dropped
 GOTO @MOVEOBJECT
;---
.WHAT
 MESSAGE 2132 ; WHAT?
 RETURN
;;;---
.WHERE
 MESSAGE 2133 ; WHERE?
 RETURN
;---
.ATTACK
 ATTACKER=PLAYER
; IF NOUN1=DOOR THEN @KNOCK
 IF NOUN1<>LARGEMIRROR THEN ATNMIRROR
 M1=2510 ; smash mirror
 GOSUB @PRINTM1
 CURRENTPOS(LARGEMIRROR)=C0
 RETURN

.ATNMIRROR
 IF NOUN1=USER THEN ATNPC
 IF NOUN1<NPCBASE THEN ATNNPC
 IF NOUN1>NPCMAXPLUSONE THEN ATNNPC
.ATNPC
; ATTACKER=PLAYER
 TARGET=NOUN1
 WEAPON=NOUN2
 GOSUB @PLAYERCHOOSEBESTWEAPON
.ATFIGHT1
 GOSUB @ASSESSWEAPON
 GOTO @GENERALBLOW

.ATNNPC
 IF NOUN1<>CURTAIN THEN ATNCURTAIN
 X1=CURRENTPOS(CURTAIN)
 IF X1<>139 THEN WASTEOFTIME
 GOSUB @PLAYERCHOOSEBESTWEAPON
 IF WEAPON=NULLOBJECT THEN @WHATWITH
 MESSAGE 2571 ; curtain falls down
 CURRENTPOS(CURTAIN)=C0
 RETURN

.ATNCURTAIN
.WAKE
.WASTEOFTIME
 IF TALKINGTONPC=TRUE THEN @NPCNOTUNDERSTOOD
 M1=2144 ; don't bother group
 GOTO @VARYMESSAGE

;---
.PUT
 IF NOUN1=NULLOBJECT THEN WHAT
 IF NOUN2=NULLOBJECT THEN WHERE
 OBJECT=NOUN2 ; * is this code necessary ?
 GOSUB @CHECKIFACCESSIBLE
 IF RESULT=FALSE THEN CANTPUTTHERE
 OBJECT=NOUN1

 IF PREP=ON THEN PUTON
 IF PREP=UNDER THEN PUTUNDER
 IF PREP=IN THEN PUTIN

.CANTPUTTHERE
 MESSAGE 2123 ; CAN'T PUT ANYTHING THERE
 RETURN
;
.PUTON
; CHECK IF DESTINATION CAN HAVE things put on top of it
 PROCESSED=FALSE
 OBJECT=WOODPILE
 GOSUB @TRYOBJECT
 IF RESULT=FALSE THEN CANTPUTTHERE
; drop through to putok
;
.PUTOK
 OBJECT=NOUN1
 POS=NOUN2 ; DESTINATION
 HIPOS=PREP
 CONFIRMMESSAGE=OK
 GOTO @MOVEOBJECT

.PUTIN
; IF NOUN1>NOUN2 THEN PITWONTFIT  ; too big
 IF NOUN2=CAGE THEN PUTOK
 IF NOUN2=DESK THEN PUTOK
 IF NOUN2=HOLE THEN PUTOK
 IF NOUN2=BAG THEN PUTOK
 IF NOUN2=WARDROBE THEN PUTOK
 IF NOUN2=BLUEBOX THEN PUTOK
 GOTO CANTPUTTHERE

;.PUTINOK
;; see if there is room i~ this container.
;; At present< is there an object already in yt ?
; GOSUB INITGETOBJ
; OBJECT=0
; HISEARCHPOS=NONSPECIFIC
; SEARCHPOs=NOUN2
; X1=PLAYER ; ?? MAXOBJECT
; GOSUB GETNEXTOBJX1
; IF OBJECT=0 THEN PUTOK
;.PIDWONTFIT
; MESSAGE 2936 ; won't fit
; RETURN

.PUTUNDER
; IF NOUN2=TREE THEN PUTOK
 GOTO CANTPUTTHERE

;---
.SETUPROOM
; Return 'ROOM'=current position of player
; If player is contained, trace back until find the
; location in which the container(s) rests.
 X4=PLAYER
 IF ESPROOM=0 THEN SETUPROOMX4 ; * SPECIAL TO MAGIK
 ROOM=ESPROOM
.SETUPROOMRET
 RETURN

.SETUPROOMX4
; Return 'ROOM'=current pos of object X4
 ROOM=CURRENTPOS(X4)
 X1=HICURRENTPOS(X4)
 IF X1=0 THEN SETUPROOMRET
 X4=ROOM
 GOTO SETUPROOMX4
;---
.POSITIONMOBILE
; Return X1=TRAMLINES pointer for mobile in room 'POS'
; whose tramline block starts at 'TRAMPTR'
 X3=TRAMPTR
.POSMOB1
 X2=TRAMLINES(X3)
 IF X2=POS THEN POSMOBEND
 IF X2=0 THEN POSMOBEND ; not found, return start of list
 ADD X3,C1
 GOTO POSMOB1
.POSMOB2
 X3=0
.POSMOBEND
 TRAMPTR=X3
 RETURN
;---
.MOVEMOBILE
; move mobile 'OBJECT' whose tramline pointer is TRAMPTR
; Describe object movement in relation to player.
 X1=CURRENTPOS(OBJECT)
 IF X1<>ROOM THEN MMNOTRANDMESSAGES
 RANDOM X1
 IF X1>160 THEN MMNOTRANDMESSAGES
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE SPACE
 GOSUB @VARYMESSAGE
 MESSAGE DOT

.MMNOTRANDMESSAGES
.MOVEMOBGO
 FROM=TRAMLINES(TRAMPTR) ;???TO (AND NEXT LINE)
 IF FROM=255 THEN @MOVEMOBRET ; object now stationary
 X1=CURRENTPOS(OBJECT)
 IF X1<>FROM THEN MOVEMOBRET ; object dead or stationary
 ADD TRAMPTR,C1
 IF FROM<>0 THEN MOVEMOB1 ; ??? TO
 TRAMPTR=TRAMBASE
 GOTO MOVEMOBGO

.MOVEMOB1
 TO=TRAMLINES(TRAMPTR)
 IF TO<>0 THEN MOVEMOBFROMTO
 TRAMPTR=TRAMBASE
 GOTO MOVEMOB1

.MOVEMOBFROMTO
; move OBJECT (an NPC) from FROM to TO
; and describe motion
 IF TO=255 THEN MOVEMOBRET ; object now stationary
 CURRENTPOS(OBJECT)=TO
 HICURRENTPOS(OBJECT)=C0 ; ensure it is on ground

 IF FROM=TO THEN @MOVEMOBRET

 M1=3542 ; leaves, going
 X1=OBJECT
 GOSUB @CONJUGATEM1X1

 IF FROM=ROOM THEN MOVEMOB2
 IF TO<>ROOM THEN MOVEMOBNODESC ; have already moved it, but don't describe
; describe mobile moving towards player 

 M1=3540 ; enters from
 X1=OBJECT
 GOSUB @CONJUGATEM1X1

 X1=TO   ; swap over to/from for direction calculation
 TO=FROM ; for mobile entering player's room
 FROM=X1
.MOVEMOB2
; first print description of object
 X1=OBJECT
 IF M1>3541 THEN MOVEMOBTHE ; mobile is leaving
.MOVEMOBA
 GOSUB @DESCANOBJX1
 GOTO MOVEMOB2A
.MOVEMOBTHE
 GOSUB @DESCTHEOBJX1
.MOVEMOB2A
 MESSAGE M1
 GOSUB @CALCDIRECTION ; now calculate direction
 X1=EXITDESCBASE ; direction verbs offset
 ADD X1,DIR
 MESSAGE X1
 MESSAGE DOT
;
 GOSUB @CHECKEXIT ; see if there was a door in the way
 IF DOOR=FALSE THEN MOVEMOBNODESC
 MESSAGE 2116 ; the door closes behind
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE DOT

.MOVEMOBNODESC
; Have described movement ( if visible )

; Now do any special cases to do with movement of mobiles

.MOVEMOBRET
 RETURN
;---

.ISOBJECTMOVEABLE
; return RESULT=TRUE if OBJECT can be moved
 RESULT=FALSE
 IF OBJECT=BAT THEN ISOMTRUE ; when pacified: see OBJECTTRIGGER
 IF OBJECT>93 THEN ISOMRET
 IF OBJECT<NPCBASE THEN ISOMTRUE
 IF OBJECT<85 THEN ISOMRET
.ISOMTRUE
 RESULT=TRUE
.ISOMRET
 RETURN
;---
;.COUNTCONTENTS
;; count number of objects immediately in container POS - answer in X3
; X3=0
; X1=1
;.CC1
; X2=HICURRENTPOS(X1)
; IF X2=0 THEN CC2
; X2=CURRENTPOS(X1)
; IF X2<>POS THEN CC2
; ADD X3,C1 ; another thing inside
;.CC2
; ADD X1,C1
; IF X1<MAXMOVEAPLUSONE THEN CC1
; RETURN
;---
;.CONTAINERFULL
;; container POS cannot contain any more. Print appropriate message
; MESSAGE 2138 ; you can't carry any more
; RETURN
;;
;.CF1
; X1=POS
; GOSUB @DESCTHEOBJX1
; MESSAGE 2139 ; is full
; RETURN
;---
.CHECKIFCONTAINED
; trying to put OBJECT in position HIPOS,POS
; if this is a container, remove any possible loopy
; connections between the two containers which might crash the game
 IF HIPOS=0 THEN CICRET
; is POS contained by OBJECT ?
 X4=POS
.CIC1
 X1=HICURRENTPOS(X4)
 IF X1=0 THEN CICRET
 X3=X4 ; preserve latest contents for use below if necessary
 X4=CURRENTPOS(X4)
 IF X4<>OBJECT THEN CIC1
; yep, so what do we do now ?
; I think we can be a bit clever - remove the connection
; and tell the player what we're doing
; sever the link between X3 and X4
; (where X3 is the contents)
; X3 has to go the the immediate position of X4
; (we know by now that OBJECT is moveable)
 X1=CURRENTPOS(OBJECT)
 CURRENTPOS(X3)=X1
 X1=HICURRENTPOS(OBJECT)
 HICURRENTPOS(X3)=X1
 MESSAGE 2055 ; (removing
 X1=X3
 GOSUB @DESCTHEOBJX1
 MESSAGE 2056 ; from the 
 X1=X4
 GOSUB @DESCTHEOBJX1
 MESSAGE 2057 ; first)
.CICRET
 RETURN
;---
.KILLOBJECT
 POS=0
 HIPOS=0
 CONFIRMMESSAGE=0
; fall through to moveobject
.MOVEOBJECT
; Move 'OBJECT' to 'POS', 'HIPOS'
; Return RESULT=TRUE if move was successful
; must have CONFIRMMESSAGE set to the message to printed
; as confirmation if the move is successful
 GOSUB ISOBJECTMOVEABLE
 IF RESULT=FALSE THEN @IMMOVABLE
;
; see if container is big enough to contain it
; (this is limited by GETNEXTOBJECT)
; at present, allow maximum of six objects to be carried
; (though more can be carried inside containers)
; GOSUB COUNTCONTENTS
; IF X3>5 THEN CONTAINERFULL
;
;
; is the contents already contained by the container (or V-V) ?
 GOSUB CHECKIFCONTAINED
; IF RESULT=TRUE THEN RET

;
 IF HIPOS=0 THEN MONOTPLAYER
; player to gain this object, check if is able to carry it
; actually this code is now used whenever an object
; goes to a position not on the ground. This
; change is to catch people trying to avoid SPECIALTAKES
; GOSUB WEIGHOBJECT
; ADD WEIGHTCARRIED,WEIGHT
; IF WEIGHTCARRIED>CARRYCAPACITY THEN HANDSFULL
 IF HIPOS<>WORN THEN MOVEOBJ1
; is it wearable ?
 IF OBJECT=SILVERMAIL THEN MOVEOBJ1
 IF OBJECT=ARMOUR THEN MOVEOBJ1
 IF OBJECT=ROBES THEN MOVEOBJ1
 GOTO @CANTWEAR
.MOVEOBJ1
; Now check if the player is standing on it or some similar problem
 OBJECTSAVE=OBJECT
 DEST=POS ; save value
 HIDEST=HIPOS ; save it
;
 POS=OBJECT
 HIPOS=NONSPECIFIC
 OBJECT=PLAYER
 GOSUB @CHECKOBJECTPOS
;
 OBJECT=OBJECTSAVE
 POS=DEST ; restore value
 HIPOS=HIDEST ; restore value
 IF RESULT=TRUE THEN @MOVEOBJFAIL
 RESULT=TRUE
 GOSUB @SPECIALTAKES ; wame-specific take messages
 IF RESULT=FALSE THEN @MOVEOBJRET

;

.MONOTPLAYER
 X1=CURRENTPOS(OBJECT)
 IF X1<>PLAYER THEN MMNOTLOSE
 X1=HICURRENTPOS(OBJECT)
 IF X1=0 THEN MMNOTLOSE
; player is to lose object
 IF X1<>WORN THEN MMLOSENOTWORN
; player is wearing object, if it was specified using EVERYTHING,
; then don't drop it as this could cause embarrasment
 IF EVERYTHING=FALSE THEN MMLOSENOTWORN
 MESSAGE 2134 ; you're wearing it
 RESULT=FALSE
 RETURN

.MMLOSENOTWORN
 GOSUB @SPECIALDROPS
 IF RESULT=FALSE THEN MOVEOBJRET

.MMNOTLOSE
 IF HIPOS=0 THEN MMNL1
 IF POS=OBJECT THEN @CANTPUTTHERE
.MMNL1
; any other special cases ?
;
; is it going to a NPC ?
 IF HIPOS=0 THEN MONTONPC
 IF POS<NPCBASE THEN MONTONPC
 IF POS>NPCMAX THEN MONTONPC
; code here is to avoid NPCs having things put on them etc.
; or the statue / bloodworm / bat being given things
 IF POS<>STATUE THEN MONSTATUE
 IF OBJECT=SILVERMAIL THEN MONSTATUE
 MESSAGE 2073 ; statue doesn't want it
 GOTO MOPREVENT
.MONSTATUE
; no special cases for giving to NPC, so make it carry it
 IF HIPOS=WORN THEN MONTONPC ; unless it is going to wear it
 HIPOS=CARRIED

.MONTONPC

 FROM=CURRENTPOS(OBJECT)
 CURRENTPOS(OBJECT)=POS
 HICURRENTPOS(OBJECT)=HIPOS
;
; now decribe the move
;
 IF TALKINGTONPC=TRUE THEN MONPC
 MESSAGE CONFIRMMESSAGE
 GOTO MONOTNPC

.MONPC
; show the NPC doing something
 SAVEHISEARCHPOS=HISEARCHPOS
 SAVEPOS=POS
 GOSUB @ISNPCVISIBLE
 HISEARCHPOS=SAVEHISEARCHPOS
 IF RESULT=FALSE THEN MONOTNPC
 X1=PLAYER
 GOSUB @DESCTHEOBJX1
 MESSAGE CONFIRMMESSAGE ; 'drops' or 'takes' etc.
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 POS=SAVEPOS
 IF CONFIRMMESSAGE<>2068 THEN MONOTGIVES ; gives
 MESSAGE 93 ; to
 X1=POS
 GOSUB @DESCTHEOBJX1
.MONOTGIVES
 MESSAGE DOT

.MONOTNPC
;
; now some odd special cases for after the move has been completed
;
;
; end of special cases
;
 RESULT=TRUE
.MOVEOBJRET
 RETURN

.MOVEOBJFAIL
 MESSAGE 2131 ;  you're on it
.MOPREVENT
 RESULT=FALSE
 RETURN

;.HANDSFULL
; MESSAGE 2107
; RETURN
;---
.IMMOVABLE
 IF TALKINGTONPC=TRUE THEN IMMRET
 M1=2120 ; INVALID OBJECT GROUP
 GOTO @VARYMESSAGE
;---
.CANTWEAR
 MESSAGE 2142 ; Can't wear group
.IMMRET
 RETURN
;---
.SCORE
 MESSAGE 2524 ; you are
 CONFIRMMESSAGE=3557 ; and feel

.ABSSCORE
 GOSUB CALCSCORE
 PRINT X4
 MESSAGE 3553 ; % sane ;  2205 ; out of 1000
;
; get message 2250-2260 inclusive for score
;
 M1=2250 ; archmage
 IF X4=0 THEN GOTRATING
; M1=2250=mage-1
 SUB X4,C1 ; shift score down so 1-10 is first cat, then 11-20 etc.
.SCORE1
 X1=10
 SUB X4,X1
 ADD M1,C1
 IF X4<NEGATIVE THEN SCORE1

.GOTRATING
 MESSAGE COMMA
 MESSAGE M1 ; rating
; MESSAGE DOTCR
; MESSAGE 2524 ; you are 
; PRINT X4 ; sanity
; MESSAGE 3553 ; % sane
; MESSAGE SPACE
 MESSAGE CONFIRMMESSAGE ; and feel
 PRINT AGE
 MESSAGE 3556 ; years old

;>>Mike 21/2/88 if solvedTMandRedMoon=false then scoreret
;>>mike 21/2/88 message 2315 ; and you solved the previous part
.scoreret
 RETURN
;---
.CALCSCORE
 X4=100 ; SPECIAL TO MAGIK - score (sanity) counts down
 X1=SPELLTIME(SANSPELLTARGET)
 IF X1=PLAYER THEN CALCSCORERET
 X3=SCSTART
 X2=1

.CALCSCORE1
 X1=SCORETABLE(X3)
 IF X1=0 THEN CALCSCORE2
 SUB X4,C1
 IF X3>SCMAXTWOPOINTS THEN CALCSCORE2 ;learning spells gives two points
 SUB X4,C1

.CALCSCORE2
 ADD X3,C1
 IF X3<SCMAXPLUSONE THEN CALCSCORE1
;
; is MAD spell in operation ?
 X1=SPELLTIME(MADSPELLTARGET)
 IF X1<>PLAYER THEN CSNMAD
 X1=30
 SUB X4,X1

.CSNMAD
; any permament offset to sanity ?
 ADD X4,SANITYOFFSET
; check the remote possibility of a negative score
 IF X4>30000 THEN CALCSZERO
 IF X4>100 THEN CALCSCOREMAX ; >100% - SPECIAL TO MAGIK
.CALCSCORERET
 RETURN

.CALCSZERO
 X4=0
 RETURN

.CALCSCOREMAX
 X4=100
.CALCSRET
 RETURN
;---
.BADOBJECT
 IF NOUN1=NULLOBJECT THEN @WHAT ;???
 MESSAGE 2044 ; I can't
 M1=VERBOFFSET
 ADD M1,VERB
 MESSAGE M1
 MESSAGE 2045 ; that
 RETURN
;---
.essentialinit
 list9(0)=thispart
 list9(1)=solvedTMandRedMoon
 CLEAR ; clears all variables
 thispart=list9(0)
 solvedTMandRedMoon=list9(1)
 thisgame=timeandmagik
; initialise some variables which retain their values throughout
; the game
 C1=1
 C2=2
 C3=3
; C4=4
; c6=6
; c8=8
; C10=10
 NOMOREINPUT=TRUE ; have come to end of input line
 return
;---
;-----

.STARTGAME
;
 message space
 message cr
 if thisgame=timeandmagik then start2
 gosub @essentialinit ; set up vars etc.
 if c0=diskversion then start1
 message 2312 ; wrong game.
 parttochain=1
 thisgame=0 ; cause p1 to restart
 goto @chainparttochain

.start1
; cassette start with junk data in memory
 message 2319 ; you'll have to restore
 goto @dorestore

.start2
; this was definately chained from some part of KO
; the part it was chained from is "THISPART"

 if parttochain<>constantpartnum then notchained
 if thispart<>constantpartnum then notchained
 x1=list2(0)
 if x1<>236 then notsolvedTMandRedMoon
 solvedTMandRedMoon=true

.notSolvedTMandRedMoon
; data is a position saved from this part.
; must have loaded a saved position into another part
; which caused this part to be chained in.
 goto @afterrestore ;cancelinput

.notchained
; if thispart=constantpartnum then initall ; restarting this part
; we are chaining in from another part...
; x2=thispart ; set up THISPART.
; thispart=parttochain
; gosub @setuppartdependentobjects
; if x2<>1 then @cancelinput ; (chaining in from alternate
;			      of P2/P3, so just go straight into game)

; we are chaining in from part 1

.StartOver
.INITALL
 gosub essentialinit ;>>mike 20/7/87 - was clear
 thispart=constantpartnum
; MESSAGE 2100 ; welcome to game
 screen g c0

 x1=1 ; part1 title page starts drawing
 gosub @dp3 ;rawpicturex1 ; start picture drawing (at position 0,0)
;---
;
;-------
;
; if thisgame<>timeandmagik then notchained
; if parttochain<>constantpartnum then notchained
; if thispart=constantpartnum then notchained
;; must have loaded a saved pos which caused
;; this part to be chained in.
; thispart=parttochain
; message space
; message cr
; prs "going to afterrestore. " ;*
; goto @afterrestore
;
;.notchained
; prs "Notchained. " ;*
; CLEAR ; clears all variables
; screen g c0
;; initialise some variables which retain their values throughout
; the game
; thisgame=timeandmagik
; thispart=constantpartnum
;---


 MESSAGE 2100 ; welcome to game

 C0=0
 C1=1
 C2=2
 C3=3
;
; C4=4
; C10=10

; SCREEN G C0
; start picture drawing immediately
; X1=1 ; start room
; GOSUB @DRAWPICTUREX1 ; in room X1
;
 ITWORD=NULLOBJECT ; * first object seen
 ITNUMBER=NULLNUMBER
 NOMOREINPUT=TRUE ; have come to end of input line
; CARRYCAPACITY=NORMALCAPACITY
 GOSUB INITOBJECTS
 HOUR=5
 SKELETONTRAMPTR=1
 SOMETHINGPROCESSED=TRUE ; force printing of WHAT NEXT ?
 DEST=1
; HIDEST=0
 CANDLEBURNING=TRUE
 DESCRIPTIONMODE=IVERBOSE
 SHIELDSTRENGTH=30
 ARMOURSTRENGTH=50
 MAILSTRENGTH=30
 PLAYER=USER ; set up to be in your own body
 GOSUB @SETUPROOM
; SANITYOFFSET=0
 AGE=20
 CANDLELIFE=15
 OOPSPOS=1
;
; initialise all chests to be MAGIKAL, except one
 X1=CHESTBASE
 X2=CHESTMAGICAL
.STARTGAME1
 CHESTTABLE(X1)=X2
 ADD X1,C1
 IF X1<CHESTMAXPLUSONE THEN STARTGAME1

; one chest is not magik:
 GOSUB @RAND10
 X1=CHESTBASE
 ADD X1,NUMBER
 X2=CHESTCLOSED
 CHESTTABLE(X1)=X2

 GOSUB @ANL2 ;??? ABSNEWLOCATION

; CanSeeInDark=true
; room=1
;.DemoPics1
; gosub @DescribeRoom
; input x1 x1 x1 x1
; c1=1
; add room,c1
; currentpos(player)=room
; goto DemoPics1

 GOTO @CANCELINPUT
;---
.INITOBJECTS
 GOSUB @INITOOPS

 OBJECT=0
.INITOBJ2
 GOSUB @INITANOBJECT
 ADD OBJECT,C1
 IF OBJECT<MAXOBJECTPLUSONE THEN INITOBJ2
; and clear out SCORETABLE
 X1=0
.INITOBJ3
 SCORETABLE(X1)=C0
 IF X1>32 THEN INITOBJ4
 OBJECTTABLE(X1)=C0
 LINPUT(X1)=C0
.INITOBJ4
 IF X1>38 THEN INITOBJ5 ; NUMSPELLS x 2
 SPELLTIME(X1)=C0
.INITOBJ5
 ADD X1,C1
 IF X1<SCMAXPLUSONE THEN INITOBJ3
; and set up NPCCURRENT
 X1=0
 X3=NPCCURRENTOFFSET
.INITNPC1
 X2=NPCINITIAL(X1)
 NPCCURRENT(X3)=X2
 ADD X1,C1
 ADD X3,C1
 IF X1<NPCTABLESIZE THEN INITNPC1
.sdescRet
 RETURN
;---
.NOTHINGHAPPENS
 M1=2109
 GOTO @VARYMESSAGE
;---
.SPECIALDESC
; come here after all objects and exits printed
; no return necessary
 IF ROOM<65 THEN SDESCRET
 IF ROOM>88 THEN SDESCRET
 MESSAGE 2730 ; there is a feeling of evil
 SCOREINDEX=SCEVILROOM
 GOto @ADDSCORE
;---
.SPECIALEXITS
; Given 'FROM' 'DIR' 'DEST'
; Modify if an exit is blocked for some reason
 HIDEST=0 ; normally can't move onto a container through an exit
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN SEORDINARY
; Player is on something - so exits may be different


; Standing on something, but can take as being an
; ordinary exit from the room in which the container is ('ROOM')
.SEORDINARY
; now check any other modified exits
 IF DEST<>120 THEN SENMIRROR
 X1=CURRENTPOS(LARGEMIRROR)
 IF X1<>0 THEN SEFALSE ; large mirror is in the way

.SENMIRROR
 IF DEST<>135 THEN SENSTORE
 IF PANELOPENED=FALSE THEN SEFALSE ; secret panel door

.SENSTORE
 IF DEST<>69 THEN SENSANCTUM
; is curtain blocking exit ?
 X1=CURRENTPOS(CURTAIN)
 IF X1<>139 THEN SENSANCTUM
 EXITVISIBLE=FALSE

.SENSANCTUM

; no problems found
RETURN

.SEFALSE
; Exit is not present
 DEST=0
 EXITVISIBLE=FALSE
; DOOR=FALSE
 RETURN
;---
.SPECIALDROPS
; trying to drop 'OBJECT'. Print appropriate message if desired
; and return RESULT=FALSE if player is to be prevented from dropping it

 IF OBJECT=MONKEY THEN SDMONKEY
 RESULT=TRUE ; ok
 RETURN

.SDMONKEY
 MESSAGE 2702 ; can't drop it
 RESULT=FALSE ; prevent it being dropped
 RETURN

;.SDFAIL
; MESSAGE 2128 ; can't drop that
;.SDFALSE
; RESULT=FALSE
; RETURN
;---
.SPECIALTAKES
 GOSUB ABSSPECIALTAKES
 GOto @PRINTM1
;---
.ABSSPECIALTAKES
; trying to take 'OBJECT'. Return M1 as message to print if desired
; and return RESULT=FALSE if player is to be prevented from taking it
 M1=0
 RESULT=TRUE
 IF VERB<>IWEAR THEN STNOTWEAR

; special code for wearing certain items
;
;        drop through to STOK
.STNOTWEAR
;
; Code for taking items (part of SPECIALTAKES)
;
 IF POS<>BAT THEN STNBAT ; ?? was PLAYER<>BAT
; bat can't take anything (for obvious reasons !)
 MESSAGE 2614 ; bat can't take it
 GOTO STPREVENT

.STNBAT
 IF POS<>FISH THEN STNFISH ; ?? was PLAYER<>BAT
 M1=2615 ; fish can't take it (for obvious reasons)
 IF TALKINGTONPC=TRUE THEN STFISH1
 M1=2616 ; you (fish) can't take it
.STFISH1
 MESSAGE M1
 GOTO @CANCELINPUT

.STNFISH
.STOK
 RESULT=TRUE
.STRET
 RETURN
.STPREVENT
 RESULT=FALSE
 RETURN
;---
.POSITIONSHADOWS
; Make objects follow the player around where applicable. e.g. DOORS.
; and other objects which are in several places at once
 IF ROOM=134 THEN POPANEL
 IF ROOM<>129 THEN PONPANEL
.POPANEL
 CURRENTPOS(PANEL)=ROOM

.PONPANEL
 IF ROOM<>1 THEN PONDOOR
; kill general door, so front door becomes visible
 CURRENTPOS(GENERALDOOR)=C0

.PONDOOR
 X1=CURRENTPOS(FERRYMAN)
 IF X1=MURDERED THEN PONFERRY
 IF ROOM=48 THEN POFERRY
 IF ROOM<>52 THEN PONFERRY
.POFERRY
 CURRENTPOS(BOAT)=ROOM ; ferryman is in boat

.PONFERRY
 RETURN
;---
.FASTEN
; NOUN1 TO NOUN2
 GOTO @CANTPUTTHERE
.FASTENOK
 CURRENTPOS(NOUN1)=NOUN2
 X1=FASTENEDTO
 HICURRENTPOS(NOUN1)=X1
 GOTO @DONE
;---

.JUMP
 IF PREP=IPOUT THEN @STANDOUT
 IF PREP=IPIN THEN @BOARD
 IF PREP=IPON THEN @BOARD
 MESSAGE 2129 ; whee - jump on spot
 RETURN
;---
.CLIMB
;
 IF ROOM<>87 THEN CLIMBNBELFRY
 HIDEST=0
 DEST=122
 GOTO NEWLOCATION

.CLIMBNBELFRY
 DIR=IVUP
 IF NOUN1=VINE THEN @MOVEDIR

 MESSAGE 2127 ; can't climb that
.CLIMBRET
 RETURN
;---

.NEWLOCATION
; Given HI,CURRENTPOS(PLAYER)=old location, HI,DEST=new location
; print any messages which appear on moving through doors etc.
 GOSUB @SETUPROOM
 FROM=ROOM
 IF HIDEST=0 THEN NEWLOCNORMAL
 IF DEST<>BOAT THEN ABSNEWLOCATION
; will ferryman allow you into boat ?
 IF FERRYMANSTATUS<>WAITINGFORMONEY THEN ABSNEWLOCATION
 MESSAGE 2711 ; fee is one object
 RETURN

.NEWLOCNORMAL
 GOSUB @SPECIALMOVES
 IF RESULT=FALSE THEN @NLRET
;
.ABSNEWLOCATION
; as for newlocation, but don't do any checking for special
; case code. Use this routine where the move is forced, so
; don't want to trip any movement traps
 FROM=ROOM
; describe door closing (if there is one)
 IF HIDEST<>0 THEN ANL2 ; skip checks for moving into container
 DIR=VERB
 SAVERESULT=DEST
 GOSUB @CHECKEXIT
 DEST=SAVERESULT
 IF DOOR=FALSE THEN ANLDOOR
 IF DOOROPEN=TRUE THEN ANLDOOR
 M1=2115 ; DOOR IN WAY
 GOTO @ERRORM1
.ANLDOOR
 IF TALKINGTONPC=FALSE THEN ANLD1
 SAVEDEST=DEST
 GOSUB NLNPCMOVE ; describe motion of NPC
 DEST=SAVEDEST

.ANLD1
 IF TALKINGTONPC=TRUE THEN ANL2 ; handled by NLNPCMOVE for NPCs
 IF DOOR=FALSE THEN ANL2
 MESSAGE 2116 ; the door closes behind
 X1=PLAYER
 GOSUB @DESCTHEOBJX1
 MESSAGE DOT

.ANL2
;
 LASTROOM=ROOM
 CURRENTPOS(PLAYER)=DEST
 HICURRENTPOS(PLAYER)=HIDEST
 DOOROPEN=FALSE
 TIMEINROOM=0
 dest=room ;>>mike 20/2/88 - not really dest
 gosub @SetUpRoom ;>>Mike 20/2/88
 GOSUB @SETUPDOOR
 goto NLEnd2

.NLEND
 DEST=ROOM ; not really DEST
 GOSUB @SETUPROOM

.NlEnd2
 IF TALKINGTONPC=TRUE THEN @AFTERMOVE
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN NLDESC
; on something, so just describe position
; player gets full description on LOOK
 GOSUB @DESCPLAYER
 GOTO @AFTERMOVE
;
.NLDESC
 GOSUB @DESCRIBEROOM
; now print any messages to be printed after entering certain moves
 GOTO @AFTERMOVE
;
.NLNPCMOVE
; decribe the motion of a NPC (moving from FROM to DEST, direction DIR)
 OBJECT=PLAYER
 PLAYER=USER
 GOSUB @SETUPROOM
 TO=DEST
 GOSUB @MOVEMOBFROMTO
 PLAYER=OBJECT
 GOSUB @SETUPROOM
 GOTO @AFTERMOVE
;---
.EAT
.NOEAT
 MESSAGE 2154 ; Yeuch!
.EATRET
.ANLRET
.DRINKRET
 RETURN
;---
.DRINK
 IF NOUN1<>WATER THEN NOEAT ; yeuch !
 IF ROOM>55 THEN @OBJECTNOTHERE
 IF ROOM<44 THEN @OBJECTNOTHERE
 MESSAGE 2157 ; the water is delicious
 IF ROOM<>44 THEN DRINKRET
 SCOREINDEX=SCLAKE
 GOTO @ADDSCORE
;---
.TICKCLOCK
 GOSUB @SETUPROOM ; shouldn't be necessary *
;  PREVIOUSDAY=DAY
;  PREVIOUSHOUR=HOUR
;  PREVIOUSMINUTE=MINUTE
; add on time a minute at a time
.TCADDMINUTES
 GOSUB @TIMEDEPENDENTCODE
; ADD TIME,C1
 ADD TIMEINROOM,C1
 ADD MINUTE,C1
 SUB TIMEPASSED,C1
 IF TIMEPASSED=0 THEN TCADDMIN1
 IF TIMEPASSED<NEGATIVE THEN TCADDMINUTES

.TCADDMIN1
 IF MINUTE<60 THEN TICKCLOCK2
.TICKCLOCKMINUTE
 X1=60
 SUB MINUTE,X1
 ADD HOUR,C1
 IF MINUTE>59 THEN TICKCLOCKMINUTE
; do all hourly activities here
 IF HOUR<24 THEN TICKCLOCK2
.TICKCLOCKHOUR
 X1=23
 SUB HOUR,X1
; ADD DAY,C1
 IF HOUR>11 THEN TICKCLOCKHOUR

.TICKCLOCK2
 TIMEPASSED=1 ; TIME PASSED IN NEXT MOVE (UNLESS OVER-RIDDEN)
;
; All time-dependant code comes here
;
.TCRET
 RETURN
;---
.WAIT
 MESSAGE 2155 ; time passes
 TIMEPASSED=10
 RETURN
;---
;.SLEEP
;; voluntarily go to sleep
; MESSAGE 2595 ; not sleepy
; RETURN
;
;.SLEEP1
; X1=CURRENTPOS(PLAYER)
; IF X1<>BED THEN SLEEPNOTONBED
; X1=HICURRENTPOS(PLAYER)
; IF X1=0 THEN SLEEPNOTONBED
; MESSAGE 2600 ; go to sleep in bed
; GOTO ABSSLEEP
;.SLEEPNOTONBED
; MESSAGE 2601 ; go to sleep where player is
; GOTO ABSSLEEP
;---
;.FORCESLEEP
; MESSAGE 2594 ; player drops from exhaustion
;
;.ABSSLEEP
;; M1=2815 ; goodnight
;; GOSUB DAGGETMESSAGE
;;
;.SLEEPNOTARRESTED
; GOSUB DOSLEEP
; GOTO CANCELINPUT
;---
;.DOSLEEP
; GOSUB SLEEPDREAM
;; X1=CURRENTPOS(PLAYER)
;; IF X1<>BED THEN SLEEPNOTBED
;; X1=HICURRENTPOS(PLAYER)
;; IF X1<>0 THEN SLEEPNOTBED
; MESSAGE 2604 ; awake refreshed
;; SCOREINDEX=SCSLEEP
;; GOSUB ADDSCORE
; GOTO SLEEPEND
;.SLEEPNOTBED
;; MESSAGE 2605 ; awake not refreshed
;.SLEEPEND
; MINUTE=1 ; can't use TICKCLOCK due to scenery messages etc.
; ADD HOUR,C4
; IF HOUR<10 THEN SLEEPRET
; SUB HOUR,C10
;; ADD DAY,C1
;.SLEEPRET
; RETURN
;---
;.SLEEPDREAM
; RANDOM X1
; IF X1>80 THEN SDCLUEDREAM
; IF SCOREDTODAY=TRUE THEN SDSWEETDREAM
;; hurry-up dream
; M1=2613
; GOTO SDEND
;.SDSWEETDREAM
; SCOREDTODAY=FALSE
; M1=2610
;.SDEND
; GOSUB VARYMESSAGE
; MESSAGE DOT
; RETURN
;
;.SDCLUEDREAM
; M1=0
; IF M1=0 THEN SDSWEETDREAM ; no clues to give
; MESSAGE M1
; MESSAGE DOT
; RETURN
;---
.CONVERTVERB
; Given VERB,PREP, convert to standard forms of certain commands
; e.g. LOOK AT => EXAMINE ;     GET ON => STAND ON  ;  GET OFF => LEAVE
; This is called as soon as the first noun is found - so
; if it is necessary to separate prepositions before+after nouns,
; this is a good place to do it

 IF VERB<>ILOOK THEN CVNOTLOOK
 IF PREP=0 THEN CVNOTLOOK
 VERB=IEXAMINE
 PREP=0

.CVNOTLOOK
;>>mike 20/2/88 IF VERB<>IGET THEN CVNOTSTAND ; get on
 IF VERB<>Itake THEN CVNOTSTAND ; get on ;>>mike 20/2/88
 IF PREP=IPOUT THEN CVSTAND
 IF PREP=IPOFF THEN CVSTAND
 IF PREP=IPIN THEN CVSTAND
 IF PREP<>IPON THEN CVNOTSTAND
.CVSTAND
 VERB=ISTAND

.CVNOTSTAND
 IF VERB<>IVON THEN CVNOTON
 PREP=IPON
 VERB=ISTAND

.CVNOTON
 IF VERB<>IVOFF THEN CVNOTOFF
 PREP=IPOFF
 VERB=ISTAND
.CVNOTOFF

.CVNOTONOFF
; this code doesn't seem to work, so it is commented out at present:
; IF VERB<>IDROP THEN CVNDROP
; IF PREP=0 THEN CVNDROP
; IF NOUN2=NULLOBJECT THEN CVNDROP ; ignore PREP if no second noun
;; DROP noun1 PREP noun2
;; probably equivalent to PUT noun PREP noun2
; VERB=IPUT

.CVNDROP
 IF VERB<>ITAKE THEN CVNTAKE
; take off something (REMOVE something)
 IF PREP<>IPOFF THEN CVNTAKE
 IF NOUN1<>NULLOBJECT THEN CVNTAKE
 VERB=IREMOVE
 PREP=0

.CVNTAKE
 IF VERB<>IPUT THEN CVNPUT
; put on something (WEAR something)
 IF PREP<>IPON THEN CVNPUTON
 IF NOUN1<>NULLOBJECT THEN CVNPUT
 VERB=IWEAR
 PREP=0
 RETURN
.CVNPUTON
 IF PREP<>IPOUT THEN CVNPUT
; put out => extinguish
 VERB=IEXTINGUISH ; put candle out
 IF NOUN1<>NULLOBJECT THEN CVNPUT
 NOUN1=NOUN2 ; put out candle

.CVNPUT
;>>mike 20/2/88 IF VERB<>IGET THEN CVNGET
;>>mike 20/2/88 VERB=ITAKE

.CVNGET
 RETURN
;---
.SELECTOBJECTPOS
; given VERB,PREP
; set up HISEARCHPOS to show where the objects which follow on
; the input line must be - i.e.
; NOUNONGROUND, NOUNCARRIED or NONSPECIFIC
 HISEARCHPOS=NOUNONGROUND
 IF VERB=ITAKE THEN SOPRET
 IF VERB=ISTAND THEN SOPRET
 HISEARCHPOS=NOUNCARRIED
 IF VERB=IDROP THEN SOPRET
 IF VERB=ICAST THEN SOPRET
 HISEARCHPOS=NONSPECIFIC
.SOPRET
 RETURN
;---
.yesOrNoNoM1
.YESORNO
 IF TALKINGTONPC=TRUE THEN YONNO ; NPCs always decline
 NOMOREINPUT=FALSE
;
 SUPRESSCHECKING=TRUE
 GOSUB @GETNEXTWORD
.YON1
 IF EOL=TRUE THEN YESORNO
 SEARCHTYPE=NOUNTYPE
 GOSUB @CHECKTYPE
 IF VALUE=IYES THEN YONYES
 IF VALUE=INO THEN YONNO
 IF VALUE=INORTH THEN YONNO

 SEARCHTYPE=VERBTYPE
 GOSUB @CHECKTYPE
 IF VALUE=IOOPS THEN @OOPS
 IF VALUE=IRAMLOAD THEN @RAMLOAD ;???
 IF VALUE<>IRAMSAVE THEN YONNRAMSAVE
 GOSUB @GETNEXTWORD
 SEARCHTYPE=VERBTYPE
 GOSUB @CHECKTYPE
 IF VALUE=IRAMLOAD THEN @RAMLOAD ;??
 GOTO YON1

.YONNRAMSAVE
;
 MESSAGE 2160 ; yes or no please
 GOSUB @ABSCANCELINPUT
 GOTO YESORNO
.YONYES
 y1=1 ; for benefit of book protection only.
 RESULT=TRUE
 RETURN
.YONNO
 y1=0 ; for benefit of book protection only.
 RESULT=FALSE
 RETURN
;---
.INITANOBJECT
; Note these are the other way round to Worm
; due to a mistake whilst entering the data
 X4=OBJECT
 ADD X4,X4
 X1=OBJECTSTART(X4)
 GOSUB @STRIPX1 ; remove top two bits
 HICURRENTPOS(OBJECT)=X1

 ADD X4,C1
 X1=OBJECTSTART(X4)
 CURRENTPOS(OBJECT)=X1
 RETURN
;---
.POSSLOOP
; move everything contained by POS to DEST,HIDEST
; if HIDEST=NONSPECIFC, then leave HICURRENTPOS alone
 OBJECT=0
.POSS1
 X1=CURRENTPOS(OBJECT)
 IF X1<>POS THEN POSS2
 X2=HICURRENTPOS(OBJECT)
 IF X2=0 THEN POSS2
 CURRENTPOS(OBJECT)=DEST
 IF HIDEST=NONSPECIFIC THEN POSS2
 HICURRENTPOS(OBJECT)=HIDEST
.POSS2
 ADD OBJECT,C1
 IF OBJECT<MAXOBJECTPLUSONE THEN POSS1
 RETURN
;---
.PUSH
 IF OBJECT<>PANEL THEN PUSHNPANEL
 IF ROOM<>134 THEN @NOTHINGHAPPENS
 MESSAGE 2544 ; the door swings open
 PANELOPENED=TRUE
 RETURN

.PUSHNPANEL
; is the object floating (due to a FLY spell)
 X1=SPELLTIME(FLYSPELLTARGET)
 IF X1<>OBJECT THEN PUSHNFLOATING
 MESSAGE 3351 ; it swings gently in the breeze
 RETURN

.PUSHNFLOATING
 GOTO @IMMOVABLE
;---
.RAND100
; return NUMBER=a random number between 0 and 99
; actually gives 0 .. 128, but high values are retried
 X1=2
 GOSUB RANDX1
 IF NUMBER>99 THEN RAND100
 RETURN
;---
.RAND10
; return NUMBER=a ranom number between 0 and 9
 X1=25
 GOSUB RANDX1
 IF NUMBER>9 THEN RAND10
 RETURN
;---
.RANDX1
; produce NUMBER based up to INT(256/X1)
 NUMBER=0
 RANDOM X2
.RANDX1A
 IF X2>NEGATIVE THEN RANDX1RET
 IF X2<X1 THEN RANDX1RET
 ADD NUMBER,C1
 SUB X2,X1
 GOTO RANDX1A
.RANDX1RET
 RETURN
;---
;.RAND3
;; return NUMBER=a random number between 0 and 2
;; no other variables to be corrupted
; RANDOM NUMBER
; IF NUMBER>80 THEN RAND3A
; NUMBER=0
; RETURN
;.RAND3A
; IF NUMBER>160 THEN RAND3B
; NUMBER=1
; RETURN
;.RAND3B
; NUMBER=2
; RETURN
;---
.VRING
 IF NOUN1<>BELL THEN @SILLY
 GOTO RINGBELL

.PULL
 IF NOUN1<>ROPE THEN PULLNBELL
.RINGBELL
 MESSAGE 2545 ; bell rings
 GOTO @SUMMONHORROR

.PULLNBELL
 IF NOUN1<>LEVER THEN @DONTNEEDTODOTHAT
; pull lever to release sword ?
 X1=CURRENTPOS(SWORDPOINT)
 IF X1<>135 THEN @DONE ; let player pull lever all they want
 MESSAGE 2565 ; sword point drops, hear clattering noises
 CURRENTPOS(SWORDPOINT)=C0
 CURRENTPOS(SWORDHILT)=C0
 X1=160 ; make sword appear
 CURRENTPOS(SWORD)=X1
 RETURN
;---
;.DISPLAYTIME
; MESSAGE 2570 ; time is_
; PRINT HOUR
; MESSAGE COLONNOSPACE
; PRINT MINUTE
; RETURN
;---
.ASK
 MESSAGE 2156 ; said
 GOSUB @TALKTOSOMEONE
 GOTO SAYNOUN1

.SAY
 MESSAGE 2156 ; said
 PROCESSINGSAY=TRUE
 GOSUB @TALKTOSOMEONE
 GOSUB @GETNEXTWORD
; specifically check for 'SAY TO ... '
 SEARCHTYPE=PREPTYPE
 GOSUB @CHECKTYPE
 IF VALUE=0 THEN @SAYNOTNPC

.SAYNOUN1 ; ask NPC about ...
; just the same as 'SAY TO' NPC
 PROCESSINGSAY=TRUE
 GOSUB @GETNEXTWORD
 SEARCHTYPE=NOUNTYPE
 GOSUB @CHECKTYPE
 IF VALUE<NPCBASE THEN @SAYNOTNPC
 IF VALUE>NPCMAX THEN @SAYNOTNPC
 PLAYER=VALUE ; talking to a character
 TALKINGTONPC=TRUE
 GOTO SAYNPCLOOP

.SAYNPC
 GOSUB @GOBACK
.SAYNPCLOOP
 PROCESSINGSAY=TRUE
 GOSUB @GETNEXTWORD
; now try calling verb parser
; first set up next word to be the verb if appropriate
 GOSUB @GOBACK
 SEARCHTYPE=VERBTYPE
 GOSUB @CHECKTYPE
 IF VALUE<>0 THEN SAYNPC2
 GOSUB @GETNEXTWORD
.SAYNPC2
 GOSUB @PARSEINPUT
 GOSUB @SELECTOBJECTPOS ; * SHOULD NOT BE NECESSARY
 GOSUB @NPCCALLVERB
 IF NOMOREINPUT=FALSE THEN SAYNPCLOOP
.SAYEND
 IF SAYRESPONSE=TRUE THEN SAYEND1
 GOSUB @ISNPCVISIBLE
 IF RESULT=FALSE THEN SAYEND1

; print a random action along the lines of 'the ferryman ignores you'
 LASTWORDPRINTED=0 ; prevent use of IT
 X1=PLAYER
 GOSUB @DESCTHEOBJX1
;
; if target is stone, has it been woken up ?
 IF PLAYER=STATUE THEN SAYNPCSTONE
 IF PLAYER<>IDOL THEN SAYNPCNSTONE
.SAYNPCSTONE
; is it woken up ?
 X1=SPELLTIME(BOMSPELLTARGET)
 IF X1=PLAYER THEN SAYNPCNSTONE
 MESSAGE 3446 ; looks stony
 GOTO SAYEND1

.SAYNPCNSTONE
 GOSUB @RAND10
 X1=3850 ; ignore messages
 ADD X1,NUMBER
 IF PLAYER<>ANTS THEN SAYNANTS
 X1=3860 ; ants only have one reponse
.SAYNANTS
 MESSAGE X1 ; ignores you
 MESSAGE DOT
.SAYEND1
 GOTO @CANCELINPUT
;---
.TALKTOSOMEONE
; set up PLAYER if there is anyone around to hear
 GOSUB ANYBODYHERE
 IF OBJECT=NULLOBJECT THEN TTSORET
 PLAYER=OBJECT
 TALKINGTONPC=TRUE
.TTSORET
 RETURN
;---
.ANYBODYHERE
; return OBJECT=first NPC present
 OBJECT=NPCBASE
.ABH1
 GOSUB @CHECKIFPRESENT
 IF RESULT=TRUE THEN ABHRET
 ADD OBJECT,C1
 IF OBJECT<NPCMAXPLUSONE THEN ABH1
 OBJECT=NULLOBJECT
.ABHRET
 RETURN
;---
.SAYNOTNPC
; not talking to NPC, but still need to check for sayings
 PROCESSINGSAY=TRUE
 GOSUB @GOBACK
; is there anyone around to hear ?
 GOSUB TALKTOSOMEONE

.SAYNOTNPCLOOP
 PROCESSINGSAY=TRUE
 GOSUB @GETNEXTWORD
 IF NOMOREINPUT=FALSE THEN SAYNOTNPCLOOP
 GOTO @CANCELINPUT
;---
.TRIGGERWORDS
; check current word to see if it is a trigger word
 SEARCHTYPE=NOUNTYPE
 GOSUB @CHECKTYPE
; VALUE is a trigger word ( a saying )
; see if there is any action to take on it
 IF VALUE=IHELLO THEN NPCHELLO
 IF VALUE=ARMOUR THEN TRIGGERGHOST
 IF VALUE=BONES THEN TRIGGERGHOST
 IF VALUE=KNUCKLEBONE THEN TRIGGERGHOST
 IF VALUE=SKULL THEN TRIGGERGHOST
 IF VALUE=IANSWER THEN RIDDLEANSWER
 IF VALUE=SILVERMAIL THEN TRIGGERGOLEM
.TGRET
 RETURN
;---
;
; Now miscellaneous code to do with talking to characters
; this is essentially an Eliza-type program without 
; the psychoanalysis bit
;
;---
.TRIGGERGOLEM
 IF PLAYER<>GOLEM THEN TGRET
 X1=CURRENTPOS(SILVERMAIL)
 IF X1<>PLAYER THEN TGRET
 MESSAGE 2680 ; give me something else to where, and you may have the mail
 GOTO @CANCELINPUT
;--
.RIDDLEANSWER
.FEAR
 IF ROOM<>85 THEN RANSEND
 OBJECT=GARGOYLE
 CORRECTANSWER=TRUE
 M1=2642 ; correct
 GOSUB @NPCSAYS
 SAYRESPONSE=TRUE
.RANSEND
 GOTO @CANCELINPUT
;---
.NPCHELLO
; PLAYER is the person the user is trying to talk to, who is here
 IF TALKINGTONPC=FALSE THEN NPCHELLOEND
 LASTWORDPRINTED=0 ; prevent use of IT
 X1=PLAYER
 GOSUB @DESCTHEOBJX1
 X1=NPCBASE
 X2=PLAYER
 SUB X2,X1
 X1=3700 ; cherub 'hello' response-1
 ADD X1,X2
 MESSAGE X1 ; response
 MESSAGE DOT
 SAYRESPONSE=TRUE
.NPCHELLOEND
 GOTO @CANCELINPUT
;---
.TRIGGERGHOST
; have got a trigger word of ARMOUR, BONES etc.
; which the ghost may well be interested in
 RANDOM X1
 M1=3570 ; take pity on me, adventurer
 IF X1<80 THEN TGPRINT
 M1=3573 ; bury them in the crypt
.TGPRINT
 OBJECT=GHOST
 GOSUB @NPCSAYS
 SAYRESPONSE=TRUE
 GOTO @CANCELINPUT
;---
.NPCCALLVERB
; is target of conversation listening ?
 IF VERB=IHELLO THEN NCVRET
 X1=SPELLTIME(HYPSPELLTARGET)
 IF PLAYER=GOLEM THEN NCVRET ; Golem will listen for trigger words
 IF X1=PLAYER THEN NCV2
.NCVRET
 RETURN

.NCV2
 IF VERB=0 THEN SAYRET
 IF VERB<16 THEN @PRESENTMULTIPLE
 IF VERB=ITAKE THEN @PRESENTMULTIPLE
 IF VERB=IDROP THEN @PRESENTMULTIPLE
 IF VERB=IATTACK THEN @PRESENTMULTIPLE
 IF VERB=IGIVE THEN @PRESENTMULTIPLE
 IF VERB=IOPEN THEN @PRESENTMULTIPLE
 GOTO @NPCNOTUNDERSTOOD
;---
.OPEN
 IF OBJECT=FRONTDOOR THEN OPEN1
 IF OBJECT<>GENERALDOOR THEN OPENNDOOR
.OPEN1
 GOSUB @CHECKIFANTSPREVENT
 IF RESULT=TRUE THEN OPENRET
 IF DOOROPEN=TRUE THEN @ALREADYOPEN
 IF ROOM<>220 THEN OPENNSTONEROOM
; trying to open door in stone room (which is locked from other side)
 IF STONEUNLOCKED=TRUE THEN OPENNSTONEROOM
 M1=2636 ; seems to be locked from other side
 GOTO @ERRORM1

.OPENNSTONEROOM
 DOOROPEN=TRUE
 IF TALKINGTONPC=FALSE THEN @DONE
 if player=idol then OpenIdol ;>>mike 20/2/88 - kludge to fix bug
 GOSUB @ISNPCVISIBLE
 IF RESULT=FALSE THEN OPENDOORRET

.OpenIdol
 X1=PLAYER
 GOSUB @DESCTHEOBJX1
 IF PLAYER<>IDOL THEN OPENNIDOL
 MESSAGE 2635 ; leans across and
 STONEUNLOCKED=TRUE

.OPENNIDOL
 MESSAGE 2069 ; opens
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE DOT
.OPENDOORRET
.SAYRET
.OPENRET
 RETURN
;
.OPENNDOOR
; trying to open a chest ?
 IF OBJECT<>CHEST THEN OPENNCHEST
 IF TALKINGTONPC=TRUE THEN @NPCNOTUNDERSTOOD
 IF INUMBER=NULLNUMBER THEN WHICHONE
 IF INUMBER>9 THEN @CANTSEETHAT
 X1=CHESTBASE
 ADD X1,INUMBER
 X2=CHESTTABLE(X1)
 IF X2=CHESTOPEN THEN ALREADYOPEN
 IF X2=CHESTDESTROYED THEN @CANTSEETHAT
 IF X2<>CHESTMAGICAL THEN OPENCHEST2
; BOOM !
 X2=CHESTDESTROYED
 CHESTTABLE(X1)=X2
 MESSAGE 2552 ; explodes
 BLOWSTRENGTH=20
 TARGET=PLAYER
 GOTO @DODAMAGE
; RETURN
.OPENCHEST2
 X2=CHESTOPEN
 CHESTTABLE(X1)=X2
 X1=CHEST
 CURRENTPOS(SALT)=X1
 X1=IN
 HICURRENTPOS(SALT)=X1
 GOSUB @DONE
 SCOREINDEX=SCCHEST
 GOTO @ADDSCORE

.OPENNCHEST
.DONTNEEDTODOTHAT
 M1=2148 ; don't need to do that
 GOTO @ERRORM1
;---
.WHICHONE
 MESSAGE 2136 ; which one ?
 GOTO @CANCELINPUT
;---
.ALREADYOPEN
 M1=2117 ; already open
 GOTO @ERRORM1
;---
.DRESS
; similar to give, but noun1/noun2 are reversed
 X1=NOUN2
 NOUN2=NOUN1
 NOUN1=X1
 HIPOS=WORN
 GOTO GIVE1

.GIVE
 HIPOS=CARRIED
.GIVE1
 IF NOUN2=USER THEN GIVENPC
 IF NOUN2<NPCBASE THEN NOBODYWANTSIT
 IF NOUN2<NPCMAXPLUSONE THEN GIVENPC
 IF NOUN2<>NULLOBJECT THEN NOBODYWANTSIT
 GOSUB @ANYBODYHERE
 IF OBJECT=NULLOBJECT THEN NOBODYWANTSIT
 NOUN2=OBJECT
 GOTO GIVENPC

.NOBODYWANTSIT
 M1=2172 ; nobody wants it
 GOTO @ERRORM1
;---
.GIVENPC
; giving NOUN1 to NPC NOUN2
 IF NOUN2=NULLOBJECT THEN NOBODYWANTSIT
 POS=NOUN2
 OBJECT=NOUN1
 IF NOUN2<>GOLEM THEN GIVENGOLEM
 IF NOUN1<>ROBES THEN GIVEGOLEMDISPLEASED
 CURRENTPOS(SILVERMAIL)=PLAYER
 X1=CARRIED
 HICURRENTPOS(SILVERMAIL)=X1
 CONFIRMMESSAGE=2681 ; just what I wanted !
 GOTO GIVENPC1

.GIVEGOLEMDISPLEASED
 M1=2682 ; I'm not wearing that !
 GOTO @NPCSAYS

.GIVENGOLEM
 IF NOUN2<>FERRYMAN THEN GIVENFERRYMAN
 CONFIRMMESSAGE=2712 ; ok, hop in
 GOSUB @MOVEOBJECT
 IF RESULT=FALSE THEN GIVERET
; set ferryman off in appropriate direction
 FERRYMANSTATUS=ROWINGSOUTH
 IF ROOM=48 THEN GIVERET
 FERRYMANSTATUS=ROWINGNORTH
.GIVERET
 RETURN

.GIVENFERRYMAN
 CONFIRMMESSAGE=2068 ; gives
.GIVENPCOK
 IF TALKINGTONPC=TRUE THEN GIVENPC1
 CONFIRMMESSAGE=OK
.GIVENPC1
 GOTO @MOVEOBJECT
;---
.THROW
 IF NOUN2<>SLUG THEN THROWNSLUG
 IF NOUN1<>SALT THEN @DROP
; throw salt at slug, drive it off
 MESSAGE 2625 ; slug rushes off
 CURRENTPOS(SLUG)=C0
 CURRENTPOS(SALT)=C0
 RETURN

.THROWNSLUG
 GOTO @DROP
;---
.SQUEEZE
 GOTO @BADOBJECT ; can't squueze that
;---
.SPECIALPLAYER
; describe player
; Print 'You are on' if outside
;   or  'You are in' if inside
 MESSAGE CR
 IF ROOM=2 THEN SPLAYERIN
 IF ROOM<47 then SPlayerOut ;>>mike 17/2/88 - 55 THEN SPLAYEROUT
 if room=203 then SPlayerOut ;>>mike 17/2/88
 if room=204 then SPlayerOut ;>>mike 17/2/88
 if room=205 then SPlayerOut ;>>mike 17/2/88
 if room=193 then splayerOut ;>>mike 20/2/88
 if room=173 then splayerOut ;>>mike 20/2/88
 if room=165 then SPlayerOut ;>>Mike 20/2/88
 if room=127 then splayerOut ;>>mike 20/2/88
 if room=49 then SPlayerOut ;>>mike 17/2/88
 if room=51 then SPlayerOut ;>>mike 17/2/88
 if room=52 then SPlayerOut ;>>mike 17/2/88
.SPLAYERIN
 MESSAGE 2001 ; You are in
 RETURN
.SPLAYEROUT
 MESSAGE 2000 ; You are on
 RETURN
;---
.ADDSCORE
 X1=SCORETABLE(SCOREINDEX)
 IF X1<>0 THEN ADDSCORERET ; already scored for that
 SCORETABLE(SCOREINDEX)=C1
; SCOREDTODAY=TRUE
 SCOREDTHISMOVE=TRUE ; your sanity is shaken
; add to age
 X1=1
 IF SCOREINDEX>SCMAXTWOPOINTS THEN GETOLDER
 X1=2 ; learning spells gives two points
.GETOLDER
 ADD AGE,X1
; MESSAGE 3557 ; you feel older
 IF AGE>99 THEN @DEATHAGE
.ADDSCORERET
 RETURN
;---
;.GETME
; GOSUB @GETNEXTWORD
; SEARCHTYPE=NOUNTYPE
; GOSUB @CHECKTYPE
; IF VALUE=0 THEN GETME1
; CURRENTPOS(VALUE)=PLAYER
; X1=CARRIED
; HICURRENTPOS(VALUE)=X1
;.GETME1
; IF NOMOREINPUT=FALSE THEN GETME
; X1=10
; SUB SANITYOFFSET,X1
; GOTO @CANCELINPUT
;---
;.GOTO
;.GOTOLOOP
; GOSUB @GETNEXTWORD
; SEARCHTYPE=NUMBTYPE
; GOSUB @CHECKTYPE
; DEST=VALUE
; HIDEST=0  ;
; GOSUB @ABS;NEWLOCATION
; GOTO @CANCELINPUT
;---
.RUB
 if noun1<>eyes then rubNEyebright2
 if noun2=eyebright then rubEyebright

.RubNEyebright2
 IF NOUN1<>EYEBRIGHT THEN RUBNEYEBRIGHT
 IF NOUN2<>EYES THEN @SILLY
.rubEyebright
 MESSAGE 2508 ; you rub the eyebright into your eyes
 CANSEEINDARK=TRUE
 GOSUB @KILLOBJECT
 GOTO @DESCRIBEROOM

.RUBNEYEBRIGHT
 IF NOUN1<>TALISMAN THEN @NOTHINGHAPPENS
 HIDEST=0
 MESSAGE 2665 ; there is a flash, and you move ...
 IF PLAINROOM=0 THEN RUBGOTOPLAIN
 IF NOUN1<>TALISMAN THEN RUBNTALISMAN
 IF ROOM<26 THEN RUBGOTOPLAIN
 IF ROOM>208 THEN RUBGOTOPLAIN
 IF ROOM<30 THEN RUBGOFROMPLAIN
 IF ROOM>201 THEN RUBGOFROMPLAIN
 IF ROOM=44 THEN RUBGOFROMPLAIN
 IF ROOM=187 THEN RUBGOFROMPLAIN
 IF ROOM>29 THEN RUBGOTOPLAIN
.RUBGOFROMPLAIN
 DEST=PLAINROOM
 GOTO @NEWLOCATION

.RUBGOTOPLAIN
 PLAINROOM=ROOM
 DEST=187
 GOTO @NEWLOCATION

.RUBNTALISMAN
 IF PREP<>0 THEN @DONTUNDERSTAND
 MESSAGE 2176 ; polish it to a shine
 RETURN
;---
.CHECKIFLIGHT
; Return RESULT=TRUE if room is illuminated
; (either naturally or by objects etc.)
 RESULT=TRUE
 IF ROOM<55 THEN CILRET
 SCOREINDEX=SCDARKROOM
 GOSUB @ADDSCORE
 RESULT=TRUE
 IF CANSEEINDARK=TRUE THEN CILRET
 RESULT=FALSE
.CILRET
 RETURN
;---
.KNOCK
 IF ROOM<>1 THEN KNOCKNDOOR
 MESSAGE 2515 ; knocks on door
 GOTO @SUMMONHORROR

.KNOCKNDOOR
 IF NOUN1=PANEL THEN KNOCKPANEL
 IF NOUN2<>PANEL THEN @BADOBJECT
.KNOCKPANEL
 IF ROOM<>134 THEN KNOCKPANEL1
 MESSAGE 2542 ; panels sound hollow
 RETURN
.KNOCKPANEL1
 MESSAGE 2543 ; panels sound normal
 RETURN
;---
.BRIEF
.VERBOSE
 DESCRIPTIONMODE=VERB
 if descriptionMode=IBrief then ExitsOff
; drop through to default to verbose+exits on
.SwitchExitsOn
 ExitsOn=true
 goto afterExits ; confirm
;---
.exits ;>>mike 17/2/88
 if prep=1 then SwitchExitsOn
 if prep<>66 then @PrintExits ;>>mike 17/2/88 DontUnderstand
.ExitsOff
 ExitsOn=false
.AfterExits
 goto @Done
;---
.CUT
 IF NOUN1<>LARGEMIRROR THEN CUTNMIRROR
 IF NOUN2<>RING THEN WHATWITH
 MESSAGE 2511 ; cut small mirror from large mirror
 CURRENTPOS(LARGEMIRROR)=C0
 CURRENTPOS(SMALLMIRROR)=ROOM
 HICURRENTPOS(SMALLMIRROR)=C0
 RETURN

.CUTNMIRROR
 IF NOUN1<>VINE THEN CUTNVINE
 GOSUB @PLAYERCHOOSEBESTWEAPON ; ??ASSESSWEAPON
 IF WEAPONSTRENGTH=0 THEN CUTWHATWITH
 X1=CURRENTPOS(STAFF)
 IF X1<>0 THEN @WASTEOFTIME
 MESSAGE 2582 ; you cut a staff from the vine
 CURRENTPOS(STAFF)=ROOM
 RETURN

.CUTNVINE
 IF NOUN1<>WEB THEN @ATTACK
 GOSUB @PLAYERCHOOSEBESTWEAPON ;??ASSESSWEAPON
 IF WEAPONSTRENGTH=0 THEN WHATWITH
 MESSAGE 2732 ; cutting web destroys it and spider
 CURRENTPOS(WEB)=C0
 RETURN

.CUTWHATWITH
;>>mike 20/2/88 IF NOUN2<>NULLOBJECT THEN WWRET

.WHATWITH
 MESSAGE 2135 ; what with ?
.WWRET
 RETURN
;---
.LIGHT
; check fo specific fire sources

 IF NOUN2=CANDLE THEN LWITHOBJECT ; must try it whetherburning or not
 IF CANDLEBURNING=FALSE THEN LNWCANDLE ; don't default if not burnng
 OBJECT=CANDLE
 GOSUB @TRYOBJECT
 IF RESULT=TRUE THEN LWITHOBJECT
.LNWCANDLE

.NOFIRE
 MESSAGE 2512 ; no source of fire
 RETURN


.WONTBURN
 MESSAGE 2507 ; won't burn
.LIGHTRET
 RETURN

.LWITHOBJECT
 IF CANDLEBURNING=FALSE THEN NOFIRE
 IF NOUN1<>WOODPILE THEN WONTBURN
 MESSAGE 2506 ; woodpile bursts into flames
 CURRENTPOS(WOODPILE)=C0
 CURRENTPOS(ASHES)=ROOM
 RETURN
;---
.EXTINGUISH
 IF NOUN2=CANDLE THEN EXTINGUISHCANDLE
 IF NOUN1<>CANDLE THEN @WASTEOFTIME
.EXTINGUISHCANDLE
 MESSAGE 2514 ; candle goes out
 CANDLEBURNING=FALSE
 RETURN
;---
.SUMMONHORROR
 IF HORRORCOMING<>0 THEN SUMMONHORRET
 HORRORCOMING=1
.SUMMONHORRET
 RETURN
;---
.ERRORM1
 IF TALKINGTONPC=FALSE THEN ERRORM1A
 GOSUB ISNPCVISIBLE
 IF RESULT=FALSE THEN @CANCELINPUT ; stop NPC
 X1=PLAYER
 GOSUB @DESCTHEOBJX1
 M1=2062 ; NPC stops, looking confused
 GOSUB @VARYMESSAGE
 GOTO @CANCELINPUT

.ERRORM1A
 MESSAGE M1
.ERRORRET
 RETURN
;---
.PRINTM1
; print message M1
; this may refer to something a NPC is doing
 IF TALKINGTONPC=TRUE THEN PRINTM1NPC
 MESSAGE M1
 RETURN

.PRINTM1NPC
; NPC is doing M1, can the player see it happen ?
 SAVERESULT=RESULT
 GOSUB ISNPCVISIBLE
 IF RESULT=FALSE THEN PRINTM1END
 MESSAGE M1
.PRINTM1END
 RESULT=SAVERESULT
 RETURN
;---
.ISNPCVISIBLE
; return RESULT=TRUE if USER can see PLAYER
; USER is the person sitting at the keyboard,
; PLAYER in this case will be a NPC
 RESULT=TRUE
 IF TALKINGTONPC=FALSE THEN INVRET ; always true for player!
 SAVEHIPOS=HIPOS
 SAVEPOS=POS
 SAVEOBJECT=OBJECT

 OBJECT=USER
 GOSUB @CHECKIFPRESENT

 POS=SAVEPOS
 HIPOS=SAVEHIPOS
 OBJECT=SAVEOBJECT
.INVRET
 RETURN
;---
.RUDEWORD
; have got a rude word - reprimand / punish player
; IF FOULMOUTHED=TRUE THEN RUDEWORD2
.RUDEWORD1
 MESSAGE 2180 ; first warning
; FOULMOUTHED=TRUE
 GOTO @CANCELINPUT

;.RUDEWORD2
;; see if we can destroy something the player is carrying
; OBJECT=1
;.RUDEWORDLOOP
; X1=HICURRENTPOS(OBJECT)
; IF X1=0 THEN RWL1
; X1=CURRENTPOS(OBJECT)
; IF X1=PLAYER THEN RUDEWORDZAP
;.RWL1
; ADD OBJECT,C1
; IF OBJECT<MAXOBJECTVISIBLE THEN RUDEWORDLOOP
; GOTO RUDEWORD1
;.RUDEWORDZAP
; CURRENTPOS(OBJECT)=C0
; HICURRENTPOS(OBJECT)=C0
; M1=2181 ; object destroyed - tough luck
; GOSUB ERRORM1
; GOTO CANCELINPUT
;---
.BURY
; bury object (maybe creating a hole)
 IF NOUN1=NULLOBJECT THEN @WHAT
 X1=CURRENTPOS(HOLE)
 IF X1=ROOM THEN BURYEXISTINGHOLE
 GOSUB DIGHOLE
 MESSAGE 2575 ; dig hole
.BURYEXISTINGHOLE

 OBJECT=NOUN1
 POS=HOLE
 HIPOS=IN
 CONFIRMMESSAGE=2577
 GOSUB @MOVEOBJECT
 IF RESULT=FALSE THEN BURYRET
 X1=NOUN1
 GOSUB @DESCTHEOBJX1
 MESSAGE 2578 ; in
.BURYOBJECT
; filling in hole - so see if any objects are destroyed which
; will make the ghost happy
 MESSAGE 2576 ; you fill in the hole
 CURRENTPOS(HOLE)=C0
 POS=HOLE
 DEST=BURIED
 HIDEST=0
 GOSUB @POSSLOOP ; kill everything
 GOSUB @AREBONESBURIED
 M1=3571 ; why not complete the job ?
 IF RESULT=FALSE THEN BURYOBJPRINT
 M1=3572 ; thanks
.BURYOBJPRINT
 GOSUB @GHOSTAPPEARS
 OBJECT=GHOST
 GOSUB @NPCSAYS
 CURRENTPOS(GHOST)=C0
.BURYRET
 RETURN
;
;---
.DIG
; dig a hole
 IF NOUN1=NULLOBJECT THEN @DIG1
 IF NOUN1<>HOLE THEN @SILLY
.DIG1
 GOSUB DIGHOLE
 MESSAGE 2575 ; dig a hole
 RETURN
;---
.DIGHOLE
 OBJECT=SHOVEL
 GOSUB @CHECKIFACCESSIBLE
 IF RESULT=TRUE THEN DIGHOLE1
 MESSAGE 2581 ; nothing to dig with
 GOTO @CANCELINPUT

.DIGHOLE1
 IF ROOM=71 THEN DIGHOLE2
 IF ROOM=3 THEN DIGHOLE2
 MESSAGE 2580 ; ground too hard
 GOTO @CANCELINPUT

.DIGHOLE2
 X1=CURRENTPOS(HOLE)
 IF X1=0 THEN DIGHOLEOK
 MESSAGE 2579 ; fill in your last hole first
 GOTO @CANCELINPUT

.DIGHOLEOK
 CURRENTPOS(HOLE)=ROOM
 GOTO @DONE
;---
.FILL
; probably hole
 IF NOUN2=HOLE THEN FILL2
 IF NOUN1<>HOLE THEN @DONTNEEDTODOTHAT
.FILL2
 GOTO BURYOBJECT
;---
.FILLINFIRST
 MESSAGE 2579 ; must fill in last hole first
 RETURN
;---
.PLAY
 IF NOUN1<>TRUMPET THEN PLAYNTRUMPET
 MESSAGE 2731 ; toot toot
 RETURN

.PLAYNTRUMPET
 IF NOUN2=CANDLE THEN @EXTINGUISHCANDLE
 GOTO @SILLY
;---
.HELP
 M1=2271 ; try going up or in
 IF ROOM<4 THEN HELPPRINT
 GOSUB @CHECKIFLIGHT
 IF RESULT=TRUE THEN HELP1
 M1=2273 ; the eyebright may be useful here
 IF CANSEEINDARK=FALSE THEN HELPPRINT
.HELP1
 M1=2272 ; perhaps you need some magikal assistance ?
 IF ROOM=130 THEN HELPPRINT ; near chests
 M1=2274 ; you don't have to smash the mirror
 IF ROOM=119 THEN HELPPRINT
 M1=2270 ; sorry, can't help
.HELPPRINT
 MESSAGE M1
 RETURN
;---
;----------------------------------------------------------
;-----------------------------------------------------------
;----------------------------------------------------------
