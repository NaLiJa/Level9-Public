; Price of Magik special code handlers
;
; ASPECIAL.TXT
;
; M.J.Austin 8/4/86
;
CONST
 HOSTILEOFFSET=0
 DODGEOFFSET=1 ; within INITIAL table
 ATTACKOFFSET=2
 HITPOINTOFFSET=3
 GIFTREQUESTED=1
 GIFTACCEPTED=2

;>>mike 17/2/88 - moved to averb.txt VAR
; MANDRAKEPULLED
; OTPOS ; OTHIPOS
; SPELLDURATION
; MOONTOUCHES
; ExitsOn

BEGIN


; now print any messages to be printed after entering certain moves
.AFTERMOVE
; reset nameless horror if player moves
 IF TALKINGTONPC=TRUE THEN AMNUSER
 IF ROOM=122 THEN AMNHORROR ; going to belfry, so horror should appear
 IF HORRORCOMING=FALSE THEN AMNHORROR
 HORRORCOMING=FALSE
 MESSAGE 2523 ; the squelching noises die away
.AMNHORROR
 IF ROOM<>84 THEN AMNICEROOM
 X1=SPELLTIME(FLYSPELLTARGET)
 IF X1=PLAYER THEN AMNICEROOM
 MESSAGE 3353 ; the floor is so cold that you jump back
 DEST=LASTROOM
 HIDEST=0
 GOTO @NEWLOCATION

.AMNICEROOM
.AMNUSER
 GOSUB @SETUPROOM
.NLRET
 RETURN
;---
.SPECIALMOVES
;
; Given ROOM, HIDEST, DEST, player is to move that way
; and exit has been validated as possible (and hence described)
; in SPECIALEXITS. The purpose of code here is when something
; happens on moving. If exit is to be blocked, code here should
; print the appropriate message and set RESULT=FALSE.
;
 GOSUB @SETUPROOM  ; necessary ?
;>>mike 17/2/88 gosub @SaveOops
;
 RESULT=FALSE ; blocked unless get to end
;
; any hostile NPCs to block motion?
 IF DEST=LASTROOM THEN SMNBLOCKED ; going back, so NPC not bothered
 OBJECT=NPCBASE
.SMCNPC1
 GOSUB @CHECKIFPRESENT
 IF RESULT=FALSE THEN SMCNPC2
;
; creatures which can be scared by objects
 IF OBJECT<>BAT THEN SMCNPCNBAT
 GOSUB @ISBATPACIFIED
 IF RESULT=TRUE THEN SMCNPC2
 GOSUB @ISBATCAGED
 IF RESULT=TRUE THEN SMCNPC2

.SMCNPCNBAT
 IF OBJECT<>WEREWOLF THEN SMNWEREWOLF
 GOSUB @ISWEREWOLFPACIFIED
 IF RESULT=TRUE THEN SMCNPC2

.SMNWEREWOLF
 GOSUB @GETNPCATTRIBUTES
 X1=NPCCURRENT(X4)
 IF X1<>PLAYER THEN SMCNPC2 ; not hostile to player
 X1=SPELLTIME(IBMSPELLTARGET)
 IF X1=OBJECT THEN SMCNPC2 ; NPC is afraid
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE 2071 ; won't let you
 RESULT=FALSE ; ensure movement in blocked
 RETURN

.SMCNPC2
 ADD OBJECT,C1
 IF OBJECT<NPCMAX THEN SMCNPC1

.SMNBLOCKED

 gosub @SaveOops ;>>mike 17/2/88


 IF DEST<>120 THEN SMNMIRROR
 X1=CURRENTPOS(LARGEMIRROR)
 IF X1=0 THEN SMNMIRROR
 MESSAGE 2509 ; the mirror is in the way
 RETURN

.SMNMIRROR
 IF DEST<>122 THEN SMNBELFRY
 MESSAGE 2516 ; gingerly climb rope
 GOSUB @RINGBELL

.SMNBELFRY
 ROOM=DEST
 GOSUB @SETUPROOM ; set it back for use by rest of code

 IF DEST<>69 THEN SMNCURTAIN
 X1=CURRENTPOS(CURTAIN)
 IF X1<>139 THEN SMNCURTAIN
 MESSAGE 2570 ; the curtain is in the way
 RETURN ; prevent movement

.SMNCURTAIN
 IF DEST<>59 THEN SMNAXE
 X1=CURRENTPOS(AXE)
 IF X1<>AXEHANGING THEN SMNAXE
; player is going to get hit by axe
 CURRENTPOS(AXE)=DEST
 TARGET=PLAYER
 BLOWSTRENGTH=50
 IF TALKINGTONPC=TRUE THEN SMAXENPC
 MESSAGE 2630 ; hit by axe
 GOSUB @GBNDODGE ; print message etc.
 GOTO @SMOK

.SMAXENPC
 GOSUB @NLNPCMOVE ; describe motion of NPC
 ROOM=DEST ; don't see NPC get hurt
 MESSAGE 2631 ; you hear a scream
 GOSUB @DODAMAGE
 GOTO @SMOK

.SMNAXE
 IF CORRECTANSWER=TRUE THEN SMNRIDDLE
 X1=CURRENTPOS(GARGOYLE)
 IF X1<>ROOM THEN SMNRIDDLE
 IF DEST<>222 THEN SMNRIDDLE
; gargoyle stops player who wants to see Myglar
 OBJECT=GARGOYLE
 M1=2640 ; answer ?
 GOSUB @NPCSAYS

.SMFAIL
 RESULT=FALSE
 RETURN

.SMNRIDDLE
 IF DEST<>222 THEN SMNMYGLAR ; going to see Myglar
 IF TALKINGTONPC=FALSE THEN SMNMYGLAR ; player will find out soon enough
; NPC dies instantly
 MESSAGE 2631 ; hear scream
 CURRENTPOS(PLAYER)=C0
 GOTO @CANCELINPUT

.SMNMYGLAR
 IF DEST<>216 THEN SMNANTS
 IF FROM<>217 THEN SMNANTS
 GOSUB @CHECKIFANTSPREVENT
 IF RESULT=TRUE THEN @SMFAIL
 GOTO SMOK

.SMNANTS
 IF DEST<>18 THEN SMNGATEWAY
 IF FROM<>83 THEN SMNGATEWAY
 GOSUB @CALCSCORE; get sanity in X4
 IF X4<50 THEN SMBRAVE
 M1=2690 ; not brave enough to go through gate
 GOTO @ERRORM1
.SMBRAVE
 M1=2691 ; charge toward gate
 GOSUB @ERRORM1 ; don't let NPCs through
 IF GIFTGIVEN=GIFTACCEPTED THEN SMGATEOK
 MESSAGE 2692 ; stopped, asked for gift
 GIFTGIVEN=GIFTREQUESTED
 GOTO SMFAIL

.SMGATEOK
; monkey drops on back ?
 X1=CURRENTPOS(MONKEY)
 IF X1<>MONKEYWAITING THEN SMNGATEWAY
 MESSAGE 2700 ; yup
 CURRENTPOS(MONKEY)=PLAYER
 X1=CLINGINGTO
 HICURRENTPOS(MONKEY)=X1

.SMNGATEWAY
 IF DEST<>50 THEN @SMNRIVER
;>>mike 25/11/87  IF PLAYER=FISH THEN SMRIVER1
 IF FROM=49 THEN SMNFish ;>>mike 17/2/88 - NRIVER ; can re-surface from underwater
 IF PLAYER=FISH THEN SMRIVER1 ; >>mike 25/11/87
 X1=HICURRENTPOS(PLAYER)
 IF X1<>0 THEN SMRIVER1 ; in boat, can jump out when human

 X1=CURRENTPOS(FERRYMAN)
 IF X1<>BOAT THEN SMRIVER1
 MESSAGE 2720 ; ferryman won't let you
 OBJECT=FERRYMAN
 RETURN ; GOTO @NPCINTERESTED ; prevent processing

.SMRIVER1
 MESSAGE 2717 ; flop into water
 GOTO SMOK

.SMNRIVER
 IF PLAYER<>FISH THEN SMNFISH
 IF FROM=50 THEN SMNFISH ; going off river
 M1=2719 ; flop about a bit
 GOTO @ERRORM1 ; prevent motion

.SMNFISH

.SMOK
; one-way exit ?
 X4=FROM
 X5=DEST
 X6=DIR
 if dest=90 then smNChute ;>>mike 16/2/8 - using ZEN
 FROM=X5
 TO=X4
 GOSUB @CALCDIRECTION
 GOSUB @CHECKEXIT
 IF DEST<>0 THEN SMNCHUTE
 if player=fish then smFish1 ;>>mike 16/2/88
 if player<>user then smNChute ;>>mike 16/2/88
.smFish1
 MESSAGE 2149 ; slide down a chute

.SMNCHUTE
; restore
 FROM=X4
 DEST=X5
 DIR=X6
;
 RESULT=TRUE
 RETURN
;
;---
.POSTCOMMAND
; come here after command has been fully executed,
; just before getting another from the user
; MESSAGE CR
 GOSUB @SETUPROOM
;
 X1=CURRENTPOS(SKELETON)
 IF X1=ROOM THEN PCNSKELETON
; skeleton only moves when not attacking player - i.e. not in same room!
 TRAMBASE=1
 TRAMPTR=SKELETONTRAMPTR
 OBJECT=SKELETON
 M1=3800 ; random skeleton messages
 GOSUB @MOVEMOBILE
 SKELETONTRAMPTR=TRAMPTR

.PCNSKELETON
;
 IF ROOM<>66 THEN PCNOTWHEEL
; kick the Warlock's wheel every so often (if it is here)
 IF WHEELFOUND=TRUE THEN PCNOTWHEEL
 RANDOM X1
 IF X1>80 THEN PCNOTWHEEL
 MESSAGE 2540 ; you kick the wheel

.PCNOTWHEEL
 GOSUB @NPCACTIONS
 GOTO @GETCOMMAND

;---

.TIMEDEPENDENTCODE
; called for every minute that goes past
 IF CANDLEBURNING=FALSE THEN TDNCANDLE
 SUB CANDLELIFE,C1
 IF CANDLELIFE<>0 THEN TDNCANDLE
; candle goes out
 CANDLEBURNING=FALSE
 OBJECT=CANDLE
 GOSUB @CHECKIFPRESENT
 CURRENTPOS(CANDLE)=C0
 IF RESULT=FALSE THEN TDNCANDLE
 MESSAGE 2505 ; the candle goes out

.TDNCANDLE
; reset any spells that terminate this minute
 IF PLAYER=IDOL THEN TDNSPELL ; alright, so this is a terrible
; Kludge - but it does have the effect of allowing the player
; to see what the idol is doing behind the door, without all
; sorts of nasty effects
 SPELL=0
.TDSPELL1
 X1=SPELLTIME(SPELL)
 IF X1=0 THEN TDSPELL2
 SUB X1,C1
 SPELLTIME(SPELL)=X1
 IF X1<>0 THEN TDSPELL2
 GOSUB @SPELLWEARSOFF
.TDSPELL2

 ADD SPELL,C1
 IF SPELL<NUMSPELLSPLUSONE THEN TDSPELL1
;
.TDNSPELL
 IF HORRORCOMING=FALSE THEN TDNHORROR
 X1=2519 ; horror messages
 ADD X1,HORRORCOMING
 MESSAGE X1
 ADD HORRORCOMING,C1
 IF X1<>2522 THEN TDNHORROR
; horror appears !
 SCOREINDEX=SCHORROR
 GOSUB @ADDSCORE
 HORRORCOMING=FALSE

.TDNHORROR
 IF MINUTE=1 THEN TDCHIME
 IF MINUTE=16 THEN TDCHIME
 IF MINUTE=31 THEN TDCHIME
 IF MINUTE<>46 THEN TDNCHIME
.TDCHIME
 IF ROOM<152 THEN TDNCHIME
 IF ROOM>158 THEN TDNCHIME
; can hear clock chiming
 M1=2620 ; hear a clock chime nearby
 IF ROOM<>154 THEN TDCHIME1
 M1=2621 ; the clock goes BOM
 SCOREINDEX=SCLEARNBOM
 GOSUB @ADDSCORE

.TDCHIME1
 MESSAGE M1

.TDNCHIME
; in cold room ?
 IF ROOM=84 THEN TDCOLD ; ice room near Myglar
 IF ROOM<>182 THEN TDNCOLD ; cold store
.TDCOLD
 M1=2703 ; you or monkey is cold
 IF TIMEINROOM=2 THEN TDCOLDPRINT
 M1=2705 ; you or monkey is very cold
 IF TIMEINROOM=4 THEN TDCOLDPRINT
 IF TIMEINROOM<>6 THEN TDNCOLD
 X1=CURRENTPOS(MONKEY)
 IF X1<>PLAYER THEN TDNCOLD
 MESSAGE 2701 ; loud crack
 CURRENTPOS(MONKEY)=C0 ; monkey jumps off
 X1=ROOM
 X2=0
 IF ROOM<>182 THEN TDCOLD2
; in store room, so put the ball into the snow
 X1=SNOW
 X2=IN
.TDCOLD2
 CURRENTPOS(BLACKBALL)=X1
 HICURRENTPOS(BLACKBALL)=X2
 CURRENTPOS(CRYSTALBALL)=X1
 HICURRENTPOS(CRYSTALBALL)=X2
 RETURN

.TDCOLDPRINT
 X1=CURRENTPOS(MONKEY)
 IF X1<>PLAYER THEN TDCPRINT1
 ADD M1,C1 ; monkey cold messages
.TDCPRINT1
 MESSAGE M1

.TDNCOLD
 SCOREINDEX=SCLEARNDET
 IF ROOM=145 THEN TDSCORE
 SCOREINDEX=SCBASEMENT
 IF ROOM=165 THEN TDSCORE
 SCOREINDEX=SCCAVERNS
 IF ROOM=193 THEN TDSCORE
 SCOREINDEX=SCPLAIN
 IF ROOM=187 THEN TDSCORE
 SCOREINDEX=SCSTONEHENGE
 IF ROOM=42 THEN TDSCORE
 SCOREINDEX=SCENTRANCEHALL
 IF ROOM=154 THEN TDSCORE
 SCOREINDEX=SCHUGEHALL
 IF ROOM=218 THEN TDSCORE
 SCOREINDEX=SCTOMB
 IF ROOM=222 THEN TDSCORE
 SCOREINDEX=SCTREASURY
 IF ROOM=147 THEN TDSCORE
 IF ROOM<>137 THEN TDNLAB
 SCOREINDEX=SCLEARNSEE
.TDSCORE
 GOSUB @ADDSCORE

.TDNLAB
 X1=HICURRENTPOS(PLAYER)
 IF X1=0 THEN TDNFERRY
 X1=CURRENTPOS(PLAYER)
 IF X1<>BOAT THEN TDNFERRY
 IF FERRYMANSTATUS=0 THEN TDNFERRY ; not rowing anywhere
 MESSAGE 2713 ; ferryman pulls on oars
 POS=50 ; middle of river
 IF FERRYMANSTATUS<>ROWINGSOUTH THEN TDNSOUTH
 IF ROOM=48 THEN TDMOVEBOAT
 POS=52 ; south bank
.TDLANDBOAT
 MESSAGE 2714 ; ferryman pulls boat up
 FERRYMANSTATUS=0
.TDMOVEBOAT
 CURRENTPOS(BOAT)=POS
 GOSUB @SETUPROOM
 GOSUB @DESCRIBEROOM
 GOTO TDNFERRY
.TDNSOUTH
 IF ROOM=52 THEN TDMOVEBOAT ; just left south bank
 POS=48 ; noth bank
 GOTO TDLANDBOAT

.TDNFERRY
 IF PLAYER=FISH THEN TDNWATER
 IF ROOM<>49 THEN TDNWATER
 MESSAGE 2718 ; can't hold breath, shoot to surface
 DEST=50
 HIDEST=0
 GOSUB @NEWLOCATION

.TDNWATER
 IF ROOM<>77 THEN TDNLEARNFLY
 SCOREINDEX=SCLEARNFLY
 GOSUB @ADDSCORE
 SCOREINDEX=SCOMINOUS
 GOSUB @ADDSCORE

.TDNLEARNFLY


 IF SCOREDTHISMOVE=FALSE THEN TDNSCORED
 MESSAGE 2208 ; your sanity is shaken
 SCOREDTHISMOVE=FALSE

.TDNSCORED
 RETURN
;---
.SPELLWEARSOFF
; SPELL is to be cancelled, so notify player if can see it happen
 X1=SPTARGETBASE
 ADD X1,SPELL
 OBJECT=SPELLTIME(X1)
 IF OBJECT=0 THEN WORET
;
 IF SPELL<>FINSPELL THEN SWONFIN ; target is NEVER visible for FIN

 CURRENTPOS(OBJECT)=ROOM
 HICURRENTPOS(OBJECT)=C0
 CURRENTPOS(FISH)=C0
 CURRENTPOS(STONEFISH)=C0
 IF OBJECT<>USER THEN SWONFIN
 PLAYER=USER
;
.SWONFIN

 GOSUB @CHECKIFPRESENT
 IF RESULT=FALSE THEN WEARSOFFNODESC

.SWODESC
; SPELL wears off
;
; special messages for spells wearing off
; do GOTO WEARSOFFNODESC if printed
;
 IF SPELL<>FLYSPELL THEN WONFLY
 IF OBJECT<>WEIGHT THEN WOFLYNWEIGHT
 MESSAGE 2660 ; the weight drops, crushing altar
 CURRENTPOS(ALTARTEMPLE)=C0
 CURRENTPOS(WEIGHT)=ROOM
 HICURRENTPOS(WEIGHT)=C0
 GOTO WEARSOFFNODESC

.WOFLYNWEIGHT
.WONFLY
 MESSAGE 3529 ; the existing
 X1=SPELLBASE
 ADD X1,SPELL
 X2=NOUNOFFSET
 ADD X1,X2
 MESSAGE X1 ; spell
 MESSAGE 3516 ; wears off

.WEARSOFFNODESC
 X1=SPTARGETBASE
 ADD X1,SPELL
 SPELLTIME(X1)=C0 ; kill entry in target table
 SPELLTIME(SPELL)=C0 ; kill time remaining on spell
; finally, any special cases for bjects returning to ground ?
;
 IF OBJECT<>REDMOON THEN WONMOON
 X1=ALTARCHAPEL
 CURRENTPOS(REDMOON)=X1
 X1=ON
 HICURRENTPOS(REDMOON)=X1
.WONMOON

 IF SPELL<>ESPSPELL THEN WONESP
; ESP spell wears off
 MESSAGE 3323 ; you return to your body
 TALKINGTONPC=FALSE
 PLAYER=USER
 ESPROOM=0
 GOSUB @SETUPROOM
 GOSUB @DESCRIBEROOM

.WONESP
.WORET
 RETURN
;---
.PRECOMMAND
; Come here after command entered, but before it has been processed

 RETURN
;---
.OBJECTTRIGGER
; Trigger on OBJECT
 OTPOS=CURRENTPOS(OBJECT) ; remain set throughout this routine
; OTHIPOS=HICURRENTPOS(OBJECT)

 RESULT=TRUE ; prevent processing unless reset
 IF VERB=IEXAMINE THEN @OTEXAMINE
 IF VERB=ICAST THEN @OTEXAMINE
;
 IF OBJECT<>MANDRAKE THEN OTNMANDRAKE
 IF MANDRAKEPULLED=TRUE THEN OTNMANDRAKE
 IF VERB=IPULL THEN OTMANDRAKE
 IF VERB<>ITAKE THEN OTNMANDRAKE
.OTMANDRAKE
 IF TALKINGTONPC=TRUE THEN OTPREVENT
 MESSAGE 2501 ; MANDRAKE SCREAMS
 MESSAGE 2207 ; do you want to continue ?
 GOSUB @YESORNO
 IF RESULT=TRUE THEN OTPULLMANDRAKE
 MESSAGE 2503 ; RELEASE MANDRAKE
 GOTO OTPREVENT

.OTPULLMANDRAKE
 MESSAGE 2502 ; WRENCH MANDRAKE
 CURRENTPOS(SKULL)=ROOM
 CURRENTPOS(KNUCKLEBONE)=ROOM
 MANDRAKEPULLED=TRUE
 CURRENTPOS(MANDRAKE)=PLAYER
 X1=CARRIED
 HICURRENTPOS(MANDRAKE)=X1
; drop through to prevent processing

.OTPREVENT
 RESULT=TRUE ; Prevent processing (only need if RESULT altered)
 RETURN

.OTNMANDRAKE
 IF OBJECT<>WHEEL THEN OTNWHEEL
 IF WHEELFOUND=TRUE THEN OTNWHEEL
 IF PLAYER=BAT THEN OTBATGETSWHEEL
 IF TALKINGTONPC=TRUE THEN @NPCNOTUNDERSTOOD
 M1=2541 ; can't find wheel
 GOTO @ERRORM1

.OTBATGETSWHEEL
; player is batty i.e. controlling the bat, so let him find the wheel
; in the mist
 MESSAGE 2539 ; bat drags wheel over to you
 WHEELFOUND=TRUE
 GOTO OTPREVENT ; nothing more to do

.OTNWHEEL
 IF OBJECT<SPELLBASE THEN OTNSPELL
 IF OBJECT>SPELLMAX THEN OTNSPELL
 IF VERB=ICAST THEN OTNSPELL
 IF TALKINGTONPC=TRUE THEN OTPREVENT
 MESSAGE 3555 ; can only CAST spells
 RETURN ; prevent processing

.OTNSPELL
; is the object carried by an NPC ?
 X1=HICURRENTPOS(OBJECT)
 IF X1=0 THEN OTNCARRIED
 IF OTPOS<NPCBASE THEN OTNCARRIED
 IF OTPOS>NPCMAX THEN OTNCARRIED
 IF PLAYER=OTPOS THEN OTNCARRIED
 X1=OTPOS
 GOSUB @DESCTHEOBJX1
 M1=2072 ; has too firm a grip on it
 IF OBJECT<>CLAYMORE THEN OTNSTATUE
 IF OTPOS=STATUE THEN OTSTATUE
.OTNSTATUE
 M1=2071 ; won't let you.
.OTSTATUE
 MESSAGE M1
 GOTO OTPREVENT

.OTNCARRIED
 IF OBJECT<>BAT THEN OTNBAT
 IF VERB=IDROP THEN OTNBAT
 IF VERB=IATTACK THEN OTNBAT
 IF VERB=ICAST THEN OTNBAT
; IF VERB=IEXAMINE THEN OTNBAT
; trigger if trying to move bat when not pacified
 GOSUB @ISBATPACIFIED
 IF RESULT=TRUE THEN @OTOK
 MESSAGE 2613 ; the bat deftly avoids your grasp
 GOTO @OTPREVENT

.OTM1PREVENT
 MESSAGE M1
 RESULT=TRUE ; prevent processing
 RETURN

.OTM1OK
 MESSAGE M1
 RESULT=FALSE ; continue processing
 RETURN

.OTNBAT
 IF OBJECT<>SWORDPOINT THEN OTNSWORDPOINT
 MESSAGE 2566 ; could cut yourself playing with sword points
 RETURN ; prevent processing

.OTNSWORDPOINT
 IF OBJECT<>SWORDHILT THEN OTNSWORDHILT
 M1=2562 ; despite standing on something, still too far away
 X1=HICURRENTPOS(PLAYER)
 IF X1<>0 THEN OTM1PREVENT
 X1=SPELLTIME(FLYSPELLTARGET)
 M1=2567 ; hilt firmly stuck
 IF X1=PLAYER THEN OTM1PREVENT
 MESSAGE 2560 ; too far away
 RETURN

.OTNSWORDHILT
 IF OBJECT<>ARMOUR THEN OTNARMOUR
 GOSUB @AREBONESBURIED
 OBJECT=ARMOUR ; restore it
 IF RESULT=TRUE THEN OTNARMOUR
 X1=CURRENTPOS(GHOST)
 IF X1=MURDERED THEN OTNARMOUR
 GOSUB @GHOSTAPPEARS
 OBJECT=GHOST
 M1=3574 ; the armour is mine. Hands off!
 GOSUB @NPCSAYS
 GOTO @OTPREVENT

.OTNARMOUR
 IF OBJECT<>REDMOON THEN OTNMOON
; touching red moon first 3 times resets age,
; after that adds 5% to sanity
 ADD MOONTOUCHES,C1
 IF MOONTOUCHES>3 THEN OTMOONHURTS
 MESSAGE 2627 ; touch red moon, feel youthful, drop it
 AGE=20
 RETURN

.OTMOONHURTS
 MESSAGE 2628 ; moon hurts you, drop it
 X1=5
 ADD SANITYOFFSET,X1
 RETURN

.OTNMOON
 IF ROOM<>83 THEN OTNGATE
; IF VERB=IDROP THEN OTGATE
 IF VERB<>IGIVE THEN OTNGATE
; GOTO OTNGATE

; .OTGATE
 IF OBJECT=VOICE THEN OTNGATE
 IF OBJECT=NULLOBJECT THEN OTNGATE
; offering gift at glowing gate ?
 IF GIFTGIVEN=GIFTACCEPTED THEN OTGATEALREADY
 IF OBJECT>STAFF THEN OTGATERUBBISH
 MESSAGE 2694 ; that will do
 LASTWORDPRINTED=0 ; prevent 'it' - could be confused with the voice
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE 2695 ; dissappears
 CURRENTPOS(OBJECT)=C0
 GIFTGIVEN=GIFTACCEPTED
 GOTO @OTPREVENT

.OTGATERUBBISH
 MESSAGE 2693 ; rubbish
 GOTO @OTPREVENT
.OTGATEALREADY
 MESSAGE 2696 ; one is quite sufficient
 GOTO @OTPREVENT

.OTNGATE
 IF OBJECT<>BOAT THEN OTNBOAT
 IF VERB=ISTAND THEN OTNBOAT ; allowed to get in/out
 IF VERB=IJUMP THEN OTNBOAT ; e.g. JUMP IN
 IF VERB=IBOARD THEN OTNBOAT
 MESSAGE 2710 ; ferryman not too pleased at your interference with boat
 RETURN ; prevent processing
.OTNBOAT
 IF OBJECT<>SMALLMIRROR THEN OTNMIRROR
 X1=CURRENTPOS(MOONBEAST)
 IF X1<>ROOM THEN OTNMIRROR
 MESSAGE 2733 ; the moon beast runs away
 CURRENTPOS(MOONBEAST)=C0
 RETURN ; prevent processing

.OTNMIRROR


.OTEXAMINE
 ; usually no action
 IF OBJECT<>TALISMAN THEN OTNTALISMAN
 IF OTPOS<>ALTARTEMPLE THEN OTNTALISMAN
 X1=HICURRENTPOS(OBJECT)
 IF X1=0 THEN OTNTALISMAN
; talisman on altar - maybe nasty
 IF VERB<>IEXAMINE THEN OTTALISMANNEXAMINE
 MESSAGE 2655 ; talisman shakes
 GOTO @OTOK

.OTTALISMANNEXAMINE
 MESSAGE 2656 ; you hear a click
; is the weight flying ?
 M1=2659 ; weight hangs where it is
 X1=SPELLTIME(FLYSPELLTARGET)
 IF X1=WEIGHT THEN TALISMANOK ; weight hangs where it is
; was player warded ?
 X1=SPELLTIME(DETSPELLTARGET)
 IF X1<>PLAYER THEN WEIGHTCRUSHES
 MESSAGE 2658 ; you jump back in panic
 M1=2660 ; weight crushes altar, not you
; drop through to WEIGHTMISSES

.WEIGHTMISSES
 CURRENTPOS(ALTARTEMPLE)=C0 ; kill altar
 CURRENTPOS(WEIGHT)=ROOM ; and put weight in its place
 HICURRENTPOS(WEIGHT)=C0
.TALISMANOK
 MESSAGE M1
 IF VERB<>ITAKE THEN @CANCELINPUT ; don't bother doing it, 
; as altar has been crushed, talisman lost
 CURRENTPOS(TALISMAN)=PLAYER
 X1=CARRIED
 HICURRENTPOS(TALISMAN)=X1
 GOTO @CANCELINPUT

.WEIGHTCRUSHES
 MESSAGE 2657 ; weight crushes you
 GOTO @DEATH

.OTNTALISMAN
 IF OBJECT<>NASTY THEN OTNNASTY
 MESSAGE 2513 ; nasty oozes away
 CURRENTPOS(NASTY)=C0
 SCOREINDEX=SCNASTY
 GOSUB @ADDSCORE
 GOTO @OTPREVENT

.OTNNASTY
; is it a focus ?
 X1=OBJECT
 IF OBJECT<>AXE THEN @OTNAXE
 X1=6 ; axe is in weapons sequence, not focus sequence
.OTNAXE
 IF X1>STAFF THEN OTNFOCUS
; have a spell focus, so give player a score for it
 IF PLAYER<>USER THEN OTNFOCUS ; but not NPCs
 SCOREINDEX=SCFOCUSBASE
 ADD SCOREINDEX,X1
 GOSUB @ADDSCORE

.OTNFOCUS
 IF OBJECT<>VOICE THEN OTNVOICE
 IF ROOM<>83 THEN @CANTSEETHAT
 IF VERB<>IGIVE THEN @CANTSEETHAT
; e.g. offering things to myglar (voice)

.OTNVOICE
 IF OBJECT<>SILVERMAIL THEN OTNMAIL
 SCOREINDEX=SCLEARNFIN
 GOSUB @ADDSCORE

.OTNMAIL
 IF OBJECT<>STATUE THEN OTNLEARNSPY
 SCOREINDEX=SCLEARNSPY
 GOSUB @ADDSCORE

.OTNLEARNSPY
 IF OBJECT<>BLUEBOX THEN OTNBLUEBOX
 SCOREINDEX=SCLEARNIBM
 GOSUB @ADDSCORE

.OTNBLUEBOX
 IF OBJECT<>INSCRIPTION THEN OTNINSCRIPTION
 X1=HICURRENTPOS(PLAYER)
 M1=2562 ; despite standing on something, still can't see it
 IF X1<>0 THEN @OTM1PREVENT
 X1=SPELLTIME(FLYSPELLTARGET)
 M1=2560 ; too far away
 IF X1<>PLAYER THEN @OTM1PREVENT
 M1=2561 ; it reads 'HYP'
 SCOREINDEX=SCLEARNHYP
 GOSUB @ADDSCORE
 GOTO @OTM1PREVENT

.OTNINSCRIPTION

.OTOK
 RESULT=FALSE ; PROCESSING MAY CONTINUE
 RETURN
;---
.FUNNIES
; Have found a bit of scenery
; Value of message in 'OBJECT' (relative to NOUNOFFSET)
; Also have 'VERB' and may have 'PREP' if one has been entered
; before this noun on the input line
; Already printed is 'OBJECT:'
; on return, DOTCR is printed
; Return RESULT=TRUE if a special message is printed in this routine
; which should prevent the printing of a standard message
; such as 'That's just scenery'

 RESULT=TRUE
 IF VERB<>ISMELL THEN FNOTSMELL
 MESSAGE 2174 ; smell nothing special
 RETURN

.FNOTSMELL
 IF VERB<>ILISTEN THEN FNOTHEAR
 MESSAGE 2173 ; hear nothing special
 RETURN

.FNOTHEAR
 RESULT=FALSE
 RETURN
;---
.SPECIALEXAMINE
; print any special examine messages which follow
; the basic message
 IF OBJECT<>MANDRAKE THEN SENMANDRAKE
 IF MANDRAKEPULLED=TRUE THEN SENMANDRAKE
 MESSAGE 2500 ; it is growing peacefully

.SENMANDRAKE
 IF OBJECT<>CANDLE THEN SENCANDLE
 IF CANDLEBURNING=FALSE THEN SECANDLE1
 MESSAGE 2504 ; the candle is burning
.SECANDLE1
 M1=2530 ; candle almost complete
 IF CANDLELIFE>7 THEN SECANDLEPRINT
 M1=2531 ; candle more than half burnt
 IF CANDLELIFE>4 THEN SECANDLEPRINT
 M1=2532 ; candle almost burnt out
.SECANDLEPRINT
 MESSAGE M1 ; describe how much of candle remains

.SENCANDLE
 X1=SPELLTIME(FLYSPELLTARGET)
 IF X1<>OBJECT THEN SENFLOATING
 GOSUB @DESCTHEOBJX1
 M1=3342 ; are floating
 IF X1=PLAYER THEN SEFLYPLAYER
 M1=3350 ; is floating
.SEFLYPLAYER
 MESSAGE M1

.SENFLOATING
 IF OBJECT<>BAT THEN SENBAT
 GOSUB @ISBATPACIFIED
 IF RESULT=FALSE THEN SENBAT
 MESSAGE 2611  ; the bat seems to be terrified of something

.SENBAT
 IF OBJECT<>CLOCK THEN SENCLOCK
 MESSAGE 2622 ; the time is
 M1=2623 ; am
 X1=HOUR
 IF HOUR<12 THEN SECLOCK1
 M1=2624 ; pm
 X2=12
 SUB X1,X2
.SECLOCK1
 PRINT X1 ; print hour
 MESSAGE COLON
 PRINT MINUTE
 MESSAGE M1 ; am/pm
 MESSAGE DOT ; CR

.SENCLOCK
 IF OBJECT<>WEIGHT THEN SENWEIGHT
 M1=2661 ; it is hanging above the altar
 X1=CURRENTPOS(ALTARTEMPLE)
 IF X1=ROOM THEN SEWEIGHTPRINT
 M1=2662 ; it is sitting on ground where altar was
.SEWEIGHTPRINT
 MESSAGE M1

.SENWEIGHT

 IF OBJECT<>ALTARCHAPEL THEN SENALTAR
 X1=SPELLTIME(FLYSPELLTARGET)
 IF X1<>REDMOON THEN SENALTAR
 MESSAGE 2626 ; on the altar is written: 'DED'
 SCOREINDEX=SCLEARNDED
 GOSUB @ADDSCORE

.SENALTAR
 SCOREINDEX=SCLEARNSAN
 IF OBJECT=PLAQUE THEN SELEARN ; examining plaque reveals SAN

 SCOREINDEX=SCLEARNDOW
 IF OBJECT=PENDULUM THEN SELEARN

.SENPENDULUM
 SCOREINDEX=SCLEARNXAM
 IF OBJECT=PRISM THEN SELEARN ; prism has XAM written on it

.SENPRISM
 SCOREINDEX=SCLEARNMAD
 IF OBJECT=OTABLE THEN SELEARN ; table has MAD written on it

 SCOREINDEX=SCLEARNZEN
 IF OBJECT=PARCHMENT THEN SELEARN ; has ZEN on it

 SCOREINDEX=SCLEARNESP
 IF OBJECT=FRONTDOOR THEN SELEARN

 RETURN

.SELEARN
; a vector for addscore
 GOTO @ADDSCORE
;---
.SPECIALPREEXAMINE
; before printing examine message / contents, check if
; anything should prevent this. return TRUE if details should
; NOT be printed
 RESULT=FALSE
 IF OBJECT<>CHEST THEN @SENCHEST
 IF INUMBER<>NULLNUMBER THEN SEPARTICULARCHEST
; describe the colours of chests present
 X5=CHESTBASE
 X6=NUMBEROFFSET
 X1=NOUNOFFSET
 ADD X6,X1
 OWTYPE=0
 TOTALOBJECTFOUND=0
.SECHEST2
 X1=CHESTTABLE(X5)
 IF X1=CHESTDESTROYED THEN SECHEST4
 ADD TOTALOBJECTFOUND,C1
 IF TOTALOBJECTFOUND<>2 THEN SECHEST3
 MESSAGE 2551 ; the chests are:
.SECHEST3
 WORD1=X6 ; colour
 WORD2=0
 IF X1<>CHESTOPEN THEN SECHESTPRINT
 WORD2=2554 ; (which is open)
.SECHESTPRINT
 GOSUB @OUTWORD12
.SECHEST4
 ADD X5,C1
 ADD X6,C1
 IF X5<CHESTMAXPLUSONE THEN SECHEST2
 X1=CHESTMAXPLUSONE
 IF TOTALOBJECTFOUND>1 THEN SECHEST5
 MESSAGE 2550 ; the chest is (actually printed before colour)
.SECHEST5

 WORD1=0
 WORD2=0
 GOSUB @OUTWORD12 ; finish up
 RESULT=TRUE ; don't print contents
 RETURN

.SEPARTICULARCHEST
 X1=CHESTBASE
 ADD X1,INUMBER
 X2=CHESTTABLE(X1)
 IF X2=CHESTDESTROYED THEN CANTSEETHAT
 IF X2=CHESTOPEN THEN SEPCHESTDESC
 MESSAGE 2553 ; it is closed
 RESULT=TRUE ; don't print contents
 RETURN

.SEPCHESTDESC
 SEARCHPOS=CHEST
 HISEARCHPOS=NONSPECIFIC
 GOSUB @PRINTOBJECTS
 IF TOTALOBJECTFOUND>0 THEN SEPCD1
 MESSAGE 2555 ; it is empty

.SEPCD1
 RESULT=TRUE ; don't print contents
 RETURN

.SENCHEST
 IF OBJECT<>SCROLL THEN SENSCROLL
 SCOREINDEX=SCKNOWMAGIK
 GOSUB @ADDSCORE

.SENSCROLL
 RESULT=FALSE ; proceed normally
 RETURN
;---
.CANTSEETHAT
 MESSAGE 2104 ; can't see that
 GOTO @CANCELINPUT
;---
.NPCACTIONS
; are NPC's going to do anything?
 OBJECT=NPCBASE
.ANOTHERNPC
 IF PLAYER<USER THEN NPCNMURDERED
 X1=CURRENTPOS(OBJECT)
 IF X1<>MURDERED THEN NPCNMURDERED
; NPC was murdered. Want a small chance of it re-appearing to
; haunt the player
 RANDOM X1
 IF X1>2 THEN NPCACTION1 ; not now
 MESSAGE 3530 ; with a flash,
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE 3531 ; reappears
 CURRENTPOS(OBJECT)=ROOM
 HICURRENTPOS(OBJECT)=C0
 GOSUB @GETNPCATTRIBUTES
; X4=pointer to start of NPCCURRENT for this NPC
 NPCCURRENT(X4)=PLAYER ; make NPC dislike user
 ADD X4,C1 ; skip current enemy
 X3=X4 ; pointer to NPCCURRENT
 X1=NPCCURRENTOFFSET
 SUB X4,X1 ; pointer to NPCINITIAL
 X2=3 ; number of bytes to copy

.INITNPCA
 X1=NPCINITIAL(X4)
 NPCCURRENT(X3)=X1
 ADD X3,C1
 ADD X4,C1
 SUB X2,C1
 IF X2>0 THEN INITNPCA
 GOTO NPCACTION1 ; another NPC

.NPCNMURDERED
 POS=ROOM
 HIPOS=0
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN NPCACTION1
 OBJECTSAVE=OBJECT
 GOSUB NPCPRESENT
 OBJECT=OBJECTSAVE
 GOSUB @NPCGOLDSINGING
 OBJECT=OBJECTSAVE

.NPCACTION1
 ADD OBJECT,C1
 IF OBJECT<NPCMAXPLUSONE THEN ANOTHERNPC
.NPCRET
 RETURN ; that's it for NPCs
;---
.ISWEREWOLFPACIFIED
; wolvesbane stops bat being hostile
 OBJECT=WOLVESBANE
 GOSUB @CHECKIFPRESENT
 OBJECT=WEREWOLF
 RETURN
;---
.ISBATPACIFIED
; Elder cross stops bat being hostile
 OBJECT=ELDERCROSS
 GOSUB @CHECKIFPRESENT
 OBJECT=BAT
 RETURN
;---
.ISBATCAGED
; Return RESULT=TRUE if bat is in cage
 POS=CAGE
 HIPOS=NONSPECIFIC
 GOTO @CHECKOBJECTPOS
;
;---
.NPCPRESENT
; special cases for NPCs who may be scared off by particular objects
; or particular circumstances
; if NPC is hypnotised, it does nothing at all (without instructions)
 X1=SPELLTIME(HYPSPELLTARGET)
 IF X1=OBJECT THEN NPCPBATRET
 IF OBJECT=CHERUB THEN NPCPBATRET

 IF OBJECT<>BAT THEN NPCPNBAT
 GOSUB ISBATPACIFIED
 IF RESULT=TRUE THEN NPCPNBATATTACK ; cross scares off bat
; bat not pacified, so if it is trapped, it should fly out of cage
 GOSUB ISBATCAGED
 IF RESULT=FALSE THEN NPCPBATRET
 MESSAGE 2610 ; the bat flies out of the cage
 CURRENTPOS(BAT)=ROOM
 HICURRENTPOS(BAT)=C0

.NPCPNBATATTACK
; Bat isn't going to attack player - but it might attack bloodowrm
 OBJECT=BLOODWORM
 POS=ROOM
 HIPOS=0
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN NPCPBATRET
; bloodworm here - bat kills it
 CURRENTPOS(BLOODWORM)=C0
 MESSAGE 2612 ; bat drives away bloodworm.
.NPCPBATRET
 RETURN

.NPCPNBAT
 IF OBJECT<>WEREWOLF THEN NPCPNWEREWOLF
 GOSUB ISWEREWOLFPACIFIED
 IF RESULT=TRUE THEN NPCPBATRET

.NPCPNWEREWOLF
; now special case for Myglar - who will ALWAYS
; attack player, regardless of what else is happening, or who
; its current enemy is
 IF OBJECT<>MYGLAR THEN NPCPNMYGLAR
; User has gone to see Myglar
 X1=SPELLTIME(SANSPELLTARGET)
 TARGET=PLAYER
 IF X1=MYGLAR THEN @NPCATTACK ; is Myglar sane ?
 MESSAGE 3362 ; Myglar casts ZAP at you
 TARGET=PLAYER
 GOSUB @DOESBLACKBALLABSORB
 IF RESULT=TRUE THEN NPCPBATRET
 GOTO @DEATH

.NPCPNMYGLAR
 GOSUB @GETNPCATTRIBUTES ; IN X4
 TARGET=NPCCURRENT(X4) ; current enemy - is it currently fighting anyone ?
 IF TARGET=0 THEN NPCP1
 X1=CURRENTPOS(TARGET)
 IF X1<>0 THEN @NPCATTACK
; its current enemy is dead - so try its natural enemy
.NPCP1
 GOSUB @GETINITHITPOINTS
 TARGET=NPCCURRENT(X4) ; who is its natural enemy ?
 IF TARGET<>0 THEN @NPCATTACK
; no attack - now check for other actions
.NPCPRET
 RETURN
;---
.NPCGOLDSINGING
; random messages said by NPC
; most characters pick a random one of three
; messages within a block, offset from start by NPC number

; GOSUB @GETNPCATTRIBUTES
; X1=BOREDOFFSET
; ADD X4,X1
; X1=NPCCURRENT(X4) ; time since NPC last did something interesting
; ADD X1,C1
; NPCCURRENT(X4)=X1
; IF X1<3 THEN @NPCRET
; NPCCURRENT(X4)=C0 ; reset time to avoid chattering on
 RANDOM X1
 IF X1>160 THEN NPCPRET

 M1=3600 ; base for ferryman,bat,ghost messages
 X2=0
 IF OBJECT=FERRYMAN THEN NPCGSINTERESTING
 X2=3
 IF OBJECT=BAT THEN NPCGSINTERESTING
 X2=6
 IF OBJECT=GHOST THEN NPCGSINTERESTING
 M1=3860
 IF OBJECT=ANTS THEN NPCSAYS
; other NPCs pick a message found by
; base + NPCnumber + RAND( 0 .. 2 )
;
 X2=OBJECT
 X1=GARGOYLE
 SUB X2,X1 ; X2:=offset of NPCs who can talk
 M1=3609 ; base for random messages

.NPCGSINTERESTING
 ADD M1,X2 ; X2:=base of messages for this NPC
 GOSUB @GETVARYM1 ; return one of the messages in M1
; drop through to NPCSAYS
;-
;
.NPCSAYS
; OBJECT says M1
 IF OBJECT=IDOL THEN NPCSAYSRET
 IF OBJECT=CHERUB THEN NPCSAYSRET
 IF OBJECT=STATUE THEN NPCSAYSRET ; can't talk
; IF OBJECT=MONKEY THEN NPCSAYSRET ; can't talk
 GOSUB @CHECKIFPRESENT
 IF RESULT=FALSE THEN NPCSAYSRET
 X1=OBJECT
 GOSUB @DESCTHEOBJX1
 MESSAGE M1
 MESSAGE DOT
.NPCSAYSRET
 RETURN 
;---
.NPCATTACK
; GOSUB @NPCINTERESTED
 ATTACKER=OBJECT
; right, we have an aggresive ATTACKER who hates TARGET
; check that attacker is not afraid
 X1=SPELLTIME(IBMSPELLTARGET)
 IF X1=ATTACKER THEN NPCSAYSRET
; before landing any blows, better check that TARGET
; is here as well !
 OBJECT=TARGET
 GOSUB @CHECKIFPRESENT
 IF RESULT=FALSE THEN @NPCRET ; not here - so nothing else to be done
;
 IF OBJECT<>BAT THEN NPCATNBAT
; if bat is pacified or in cage, don't attack
 GOSUB @ISBATPACIFIED
 IF RESULT=TRUE THEN NPCSAYSRET
 GOSUB @ISBATCAGED
 IF RESULT=TRUE THEN NPCSAYSRET

.NPCATNBAT
 IF OBJECT<>WEREWOLF THEN NPCATNWEREWOLF
 GOSUB @ISWEREWOLFPACIFIED
 IF RESULT=TRUE THEN NPCSAYSRET

.NPCATNWEREWOLF
;
 GOSUB @CHOOSEBESTWEAPON

.GENERALBLOW
; given TARGET,ATTACKER
; do an ordinary (non-magical) blow
 IF TARGET=ANTS THEN @ATTACKANTS
;
 IF ATTACKER=TARGET THEN @GBRET
; first calculate blow strength
 OBJECT=ATTACKER
 GOSUB @GETNPCATTRIBUTES
 ADD X4,C2 ; ATTACKOFFSET
 BLOWSTRENGTH=NPCCURRENT(X4)
 IF WEAPONSTRENGTH<BLOWSTRENGTH THEN GB1
 BLOWSTRENGTH=WEAPONSTRENGTH
.GB1
; does KIL (berzerk) spell increase strength ?
 X1=SPELLTIME(KILSPELLTARGET)
 IF X1<>ATTACKER THEN GB2
 ADD BLOWSTRENGTH,BLOWSTRENGTH

.GB2
 GOSUB @RANDOMIZEBLOWSTRENGTH

; DO A GENERALBLOW
 GOSUB @MAKEENEMIES
; now do the blow
; with strength BLOWSTRENGTH
 GOSUB @DESCRIBEATTACK ; first part of report
;
;; at this point, see if target/agressor is floating
; X1=SPELLTIME(FLYSPELLTARGET)
; IF X1=TARGET THEN DGBFLOATINW
; IF X1<>ATTACKER THEN DGBNFLOATING
;.DGBFLOATING
; MESSAGE 3352 ; but the blow has no force
; RETURN

.DGBNFLOATING
;; see if targed dodges
 OBJECT=TARGET
 GOSUB @GETNPCATTRIBUTES
 X1=DODGEOFFSET
 ADD X4,X1
 X1=NPCINITIAL(X4)
; X1 is the dodge %ge
 GOSUB @RAND100 ; returns NUMBER = 0..99
 X2=NPCCURRENT(X4)
 IF NUMBER>X2 THEN GBNDODGE
 M1=3517 ; but you dodge
 IF TARGET=PLAYER THEN GBHDODGEPRINT
 M1=3518 ; but it dodges
.GBHDODGEPRINT
 MESSAGE M1 ; it/you dodge
 RETURN

.GBNDODGE
 GOSUB @CHECKARMOUR
; returns X1=object number of armour worn (0 if none)
 IF X1=0 THEN GBH
; damage/destroy shield etc.
 M1=3511 ; blow strikes its
 IF TARGET<>USER THEN GBSHIELD
 M1=3510 ; blow strikes your
.GBSHIELD
 MESSAGE M1
 GOSUB @DESCOBJX1 ; describe armour etc.
 IF X1<>SHIELD THEN GBARMOUR
 SUB SHIELDSTRENGTH,BLOWSTRENGTH
 IF SHIELDSTRENGTH=0 THEN GBSHATTER
 IF SHIELDSTRENGTH>NEGATIVE THEN GBSHATTER
 X2=SHIELDSTRENGTH
.GBEND
 MESSAGE 3525 ; (which has
 PRINT X2 ; strength of armour remaining
 MESSAGE 3526 ; hit points left).
.GBRET
 RETURN

.GBARMOUR
 IF X1<>ARMOUR THEN GBCHAINMAIL
 SUB ARMOURSTRENGTH,BLOWSTRENGTH
 X2=ARMOURSTRENGTH
 IF ARMOURSTRENGTH=0 THEN GBSHATTER
 IF ARMOURSTRENGTH<NEGATIVE THEN GBEND

.GBSHATTER
 MESSAGE 3514 ; shattering it.
 CURRENTPOS(X1)=C0
 RETURN
;
.GBCHAINMAIL
 SUB MAILSTRENGTH,BLOWSTRENGTH
 X2=MAILSTRENGTH
 IF MAILSTRENGTH=0 THEN GBSHATTER
 IF MAILSTRENGTH<NEGATIVE THEN GBEND
 GOTO GBSHATTER

.GBH
; target not wearing armour or whatever,
; so do it some GBH
 MESSAGE 3504 ; causing
 PRINT BLOWSTRENGTH
 X1=3507 ; point of damage
 IF BLOWSTRENGTH=1 THEN GBHDISP
 X1=3505 ; points of damage
.GBHDISP
 MESSAGE X1

.DODAMAGE
 OBJECT=TARGET
 GOSUB @GETNPCATTRIBUTES
 X1=HITPOINTOFFSET ; find hit points
 ADD X4,X1
 X1=NPCCURRENT(X4)
 SUB X1,BLOWSTRENGTH
 NPCCURRENT(X4)=X1
 IF X1=0 THEN DODAMDEATH
 IF X1<NEGATIVE THEN DODAMEND
.DODAMDEATH
 IF TARGET=user THEN @DEATH ;>>mike 17/2/88 - was "=player"  !
 M1=3506 ; it is dead, the body vanishes
 GOSUB @PRINTM1NPC
 POS=TARGET
 DEST=ROOM
 HIDEST=0
 GOSUB @POSSLOOP ; drop everything
 X1=MURDERED
 CURRENTPOS(TARGET)=X1
 GOTO @CANCELINPUT ; don't let it continue with commands !!!

.DODAMEND
; describe hit points remaining on target
 X1=TARGET
 GOSUB @DESCTHEOBJX1
 X1=TARGET
 M1=3520 ; has
 GOSUB @CONJUGATEM1X1
 MESSAGE M1
 OBJECT=TARGET
 GOSUB GETHITPOINTS
 PRINT X1 ; hit points remaining
 MESSAGE 3522 ; hit points
 RETURN
;---
.GETHITPOINTS
; return X4=position in table, X1=no. of hit points for OBJECT
 GOSUB GETNPCATTRIBUTES
 X1=HITPOINTOFFSET
 ADD X4,X1
 X1=NPCCURRENT(X4)
 RETURN
;---
.GETINITHITPOINTS
 GOSUB GETHITPOINTS
 X1=NPCCURRENTOFFSET
 SUB X4,X1
 X1=NPCINITIAL(X4) 
 RETURN
;---
.DESCRIBEATTACK
; first part of damage report for ATTACKER vs TARGET
 X1=ATTACKER
 GOSUB @DESCTHEOBJX1
; X1=ATTACKER
; GOSUB CONJUGATEX1
; OBJECT=ATTACKER
 M1=3500 ; (you) or (ants) attack
 GOSUB @CONJUGATEM1X1
 MESSAGE M1
 X1=TARGET
 GOSUB @DESCTHEOBJX1
; have printed 'whoever attack(s) whoever'
; now do 'with the weapon' (if there is one !)
 IF WEAPON=NULLOBJECT THEN DARET
 MESSAGE 3502 ; with the 
 X1=WEAPON
 GOSUB @DESCOBJX1

.DARET
 RETURN
;---
.GETNPCATTRIBUTES
; return in X4 the pointer to the NPC 'OBJECT'
 X4=NPCCURRENTOFFSET
 IF OBJECT=USER THEN GNARET
 IF OBJECT=FISH THEN GNAFISH
 X4=OBJECT
 X2=NPCBASEMINUSONE
 SUB X4,X2
 ADD X4,X4
 ADD X4,X4
 X1=NPCCURRENTOFFSET
 ADD X4,X1
.GNARET
 RETURN

.GNAFISH
 X4=138 ; attributes for fish
 RETURN
;---
.RANDOMIZEBLOWSTRENGTH
; return BLOWSTRENGTH=1..BLOWSTRENGTH at random
 GOSUB @RAND10
 X4=NUMBER
 GOSUB @RAND10
 ADD X4,NUMBER
 IF X4>BLOWSTRENGTH THEN RANDOMIZEBLOWSTRENGTH
 IF X4=0 THEN RANDOMIZEBLOWSTRENGTH
 BLOWSTRENGTH=X4
 RETURN
;---
.CHECKARMOUR
; return X1=object number of armour worn by TARGET or X1=0 if none
 X1=ARMOUR
 X2=CURRENTPOS(X1)
 IF X2<>TARGET THEN CATRYSHIELD
 X2=HICURRENTPOS(X1)
 IF X2<>0 THEN CARET

.CATRYSHIELD
 X1=SHIELD
 X2=CURRENTPOS(X1)
 IF X2<>TARGET THEN CATRYMAIL
 X2=HICURRENTPOS(X1)
 IF X2<>0 THEN CARET

.CATRYMAIL
 X1=SILVERMAIL
 X2=CURRENTPOS(X1)
 IF X2<>TARGET THEN CAFAIL
 X2=HICURRENTPOS(X1)
 IF X2<>0 THEN CARET

.CAFAIL
 X1=0 ; no armour found

.CARET
 RETURN
;---
.SILLY
 M1=2020
 GOTO @VARYMESSAGE
;---
.PLAYERCHOOSEBESTWEAPON
; did player specify a weapon ?
; if so, is it a sensible one ? (using some a woodpile to
; attack someone is not very helpful, apart from making
; the game look pretty silly)
 ATTACKER=PLAYER
 IF NOUN2=NULLOBJECT THEN CHOOSEBESTWEAPON
 WEAPON=NOUN2
 IF NOUN2<KNIFE THEN PCBWDISREGARD
 IF NOUN2<POIGNARDPLUSONE THEN ASSESSWeapon ;>>mike 20/2/88 - CARET
.PCBWDISREGARD
 GOSUB SILLY
; drop through to CHOOSEBESTWEAPON
.CHOOSEBESTWEAPON
; select dhe best WEAPON that ATTACKER is carrying
 OBJECT=POIGNARD ; (best weapon)
 POS=ATTACKER
 HIPOS=NONSPECIFIC
.CBW1
 GOSUB @CHECKOBJECTPOS
 WEAPON=OBJECT
 IF RESULT=TRUE THEN ASSESSWEAPON
 SUB OBJECT,C1
 IF OBJECT>49 THEN CBW1
 WEAPON=NULLOBJECT
.ASSESSWEAPON
 WEAPONSTRENGTH=6
 IF WEAPON=KNIFE THEN CBWRET
 WEAPONSTRENGTH=8
 IF WEAPON=AXE THEN CBWRET
 WEAPONSTRENGTH=10
 IF WEAPON=SWORD THEN CBWRET
 WEAPONSTRENGTH=15
 IF WEAPON=CLAYMORE THEN CBWRET
 WEAPONSTRENGTH=20
 IF WEAPON=POIGNARD THEN CBWRET
 WEAPONSTRENGTH=0
; WEAPON=NULLOBJECT
.CBWRET
 RETURN
;---
.MAKEENEMIES
; make TARGET and ATTACKER be enemies
 OBJECT=TARGET
 GOSUB @GETNPCATTRIBUTES
 NPCCURRENT(X4)=ATTACKER
 OBJECT=ATTACKER
 GOSUB @GETNPCATTRIBUTES
 NPCCURRENT(X4)=TARGET
 RETURN
;---
.DOESBLACKBALLABSORB
; does black ball absorb the spell cast at TARGET
 RESULT=FALSE
 X1=CURRENTPOS(BLACKBALL)
 IF X1<>TARGET THEN DBBARET
 X1=HICURRENTPOS(BLACKBALL)
 IF X1=0 THEN DBBARET
 CURRENTPOS(BLACKBALL)=C0
 MESSAGE 2666 ; the black ball absorbs the spell
 RESULT=TRUE
.DBBARET
 RETURN
;--
.CAST
; does player know about magik yet ?
 X1=SCORETABLE(SCKNOWMAGIK)
 IF X1<>0 THEN CASTMAGIK
 MESSAGE 3527 ; you haven't learnt how to do that yet
 RETURN

.CASTMAGIK
; valid target ? (if one given)
 IF PREP=0 THEN CASTNOPREP
 IF NOUN2<>NULLOBJECT THEN CASTNOPREP
 MESSAGE 2137 ; at what ?
 RETURN

.CASTNOPREP
; cast spell (at target)
 IF NOUN1<SPELLBASE THEN DONTKNOWSPELL ; can't cast that
 IF NOUN1>SPELLMAXPLUSONE THEN DONTKNOWSPELL
 SPELL=NOUN1
 X1=SPELLBASE
 SUB SPELL,X1
 TARGET=NOUN2
;
; does player know this spell ?
 X1=SCLEARNSPELLBASE
 ADD X1,SPELL
 X2=SCORETABLE(X1)
 IF X2<>0 THEN CASTKNOWSPELL
.DONTKNOWSPELL
 MESSAGE 3519 ; you don't know that spell
 RETURN

.CASTKNOWSPELL
; is the focus for the spell here ?
;
 OBJECT=SPELL
 IF OBJECT<>6 THEN CAST1
 OBJECT=AXE ; axe is in weapons sequence, not focus sequence
.CAST1
 GOSUB @CHECKIFPRESENT
 IF RESULT=TRUE THEN CAST2
 MESSAGE 3550 ; that spell requires
 X1=OBJECT
 GOSUB @DESCANOBJX1
 MESSAGE 3551 ; as focus
 RETURN

.CAST2
; check if player is insane enough to cast this spell
 X1=SPELL
 GOSUB @DIVIDEX1BY2
; X1=spell level
 GOSUB @MULTX1BY10
 X6=111
 SUB X6,X1
 GOSUB @CALCSCORE ; in X4, corrupts X1,X2,X3
 IF X4<X6 THEN CAST3
; not insane enough
 MESSAGE 3552 ; that requires you to be less than
 PRINT X6
 MESSAGE 3553 ; % sane
 MESSAGE DOT
.CASTRET
 RETURN

.CAST3
;
 GOSUB @SPELLWEARSOFF

; was there a target specified ?
; if not - try parsing other possibilities
; specifically, CAST ESP TO NORTH etc.
; which currently returns CAST ESP TO 
 DIR=0
 IF NOUN2<>NULLOBJECT THEN CAST6 ; not this time
 IF SPELL<>ESPSPELL THEN CAST6
 GOSUB @GETNEXTWORD
 SEARCHTYPE=VERBTYPE
 GOSUB @CHECKTYPE
 IF VALUE<>0 THEN CAST4A
.CAST4FAIL
 GOSUB @GOBACK
 GOTO @SPELLFAIL
.CAST4A
; have got VALUE as a verb. Is it a valid direction ?
 IF VALUE>15 THEN CAST4FAIL ; maximum direction
; OK - fall through to spell code (eventually)
 DIR=VALUE

.CAST6
; is target carrying the black ball ? (which absorbs a spell)
 GOSUB @DOESBLACKBALLABSORB
 IF RESULT=TRUE THEN CASTRET
;
; score for casting spell
 SCOREINDEX=SCUSESPELLBASE
 ADD SCOREINDEX,SPELL
 GOSUB @ADDSCORE
;
; and get older
 X1=1
 GOSUB @GETOLDER
;
; now actually cast it
;
 SPELLDURATION=10 ; default time for spells
; first find M1=base for messages for this spell
 X1=SPELL
 GOSUB @MULTX1BY10
 ADD X1,X1
 M1=3080 ; base for spell effect messages - 20
 ADD M1,X1
; then set up X4 as the offset into this spell's messages
 X4=0 ; default is player's messages
; is the spell going to affect this target
;
 IF SPELL<>MADSPELL THEN CASTNMAD
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 IF TARGET<NPCBASE THEN @SPELLFAIL
 IF TARGET>NPCMAX THEN @SPELLFAIL
 X4=1
 GOTO @CASTANGER
;
.CASTNMAD
 SPELLDURATION=0
 IF SPELL<>DOWSPELL THEN CASTNDOW
; first check if pendulum is in a position to swing
 OBJECT=PENDULUM
 POS=PLAYER
 HIPOS=NONSPECIFIC
 GOSUB @CHECKOBJECTPOS
 X4=3 ; pendulum wriggles a bit
 IF RESULT=FALSE THEN DOWPENDULUM
 OBJECT=TARGET
 GOSUB @ISOBJECTMAGIC
 X4=2 ; pendulum swings side to side
 IF RESULT=FALSE THEN DOWPENDULUM
 X4=1 ; pendulum swings in a circle
.DOWPENDULUM
 TARGET=PENDULUM ; describe "the pendulum swings ..."
 GOTO @CASTDESCEFFECT

.CASTNDOW
 IF SPELL<>SEESPELL THEN CASTNSEE
 FROM=ROOM
 DIR=1
 NUMEXITS=0
.CASTSEE1
 GOSUB @ABSCHECKEXIT ; no SPECIALEXITS blocking code
 IF DEST=0 THEN CASTSEE2
; there is an exit here. Is is visible ?
 GOSUB @SPECIALEXITS
 IF DEST=0 THEN CASTSEEHIDDEN ; blocked by special code
 GOSUB @ABSCHECKEXIT
 IF EXITVISIBLE=TRUE THEN CASTSEE2

.CASTSEEHIDDEN
 MESSAGE 3140 ; you see a hidden exit leading
 X1=VERBOFFSET
 ADD X1,DIR
 MESSAGE X1
 MESSAGE DOT ; CR
 ADD NUMEXITS,C1

.CASTSEE2
 ADD DIR,C1
 IF DIR<14 THEN CASTSEE1
 IF NUMEXITS=0 THEN @SPELLFAIL
 RETURN

.CASTNSEE
 IF SPELL<>FINSPELL THEN CASTNFIN
; is the target wearing the chain mail ?
 SPELLDURATION=10
 OBJECT=SILVERMAIL
 POS=TARGET
 HIPOS=NONSPECIFIC
 GOSUB @CHECKOBJECTPOS
 IF RESULT=FALSE THEN @SPELLFAIL
; ok, we can turn the target into a fish
 X4=0
 IF TARGET=PLAYER THEN BECOMEFISH
 IF TARGET<>STATUE THEN @SPELLFAIL
 X4=1 ; statue becomes a fish
.BECOMEFISH
 DEST=ROOM
 HIDEST=0
 POS=TARGET
 GOSUB @POSSLOOP ; drop all possessions
 CURRENTPOS(SILVERMAIL)=TARGET ; target wearing it when FIN wears off
 X1=WORN
 HICURRENTPOS(SILVERMAIL)=X1
 CURRENTPOS(TARGET)=C0
 X1=FISH
 IF TARGET<>STATUE THEN CASTFISHNSTATUE
 X4=2 ; statue becomes a stone fish
 X1=STONEFISH
; should stone fish be permament ???
.CASTFISHNSTATUE
 CURRENTPOS(X1)=ROOM ; bring in appropriate fish
 IF TARGET<>USER THEN CASTFISHNUSER
 PLAYER=FISH
.CASTFISHNUSER
 GOTO @CASTDESCEFFECT
;
.CASTNFIN
; spellduration=0
 IF SPELL<>FIXSPELL THEN CASTNFIX
 IF TARGET=PLAYER THEN FIXTARGET
 IF TARGET<NPCBASE THEN @SPELLFAIL ; can only fix NPCs or player
 IF TARGET>NPCMAX THEN @SPELLFAIL  ; can only fix NPCs or player
 IF TARGET>DARKSPAWN THEN FIXUNDEAD ; ghoul is first undead
.FIXTARGET
 OBJECT=TARGET
 GOSUB @GETINITHITPOINTS
 VALUE=X1
 GOSUB @GETHITPOINTS
 NPCCURRENT(X4)=VALUE
 X1=TARGET
 GOSUB @CONJUGATEX1
 X4=0 ; look very healthy now
 IF RESULT=CONJSOME THEN @CASTDESCEFFECT
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 X4=1 ; looks healthy now
 GOTO @CASTDESCEFFECT

.FIXUNDEAD
; cause 20 points damage to undead
 X4=2 ; screams in agony
 GOSUB @CASTDESCEFFECT
 BLOWSTRENGTH=20
 ATTACKER=PLAYER
 GOTO @DODAMAGE

.CASTNFIX
; berserk
 IF SPELL<>KILSPELL THEN CASTNKIL
 SPELLDURATION=10
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 X4=1 ; NPC goes berserk
 IF TARGET<NPCBASE THEN @SPELLFAIL
 IF TARGET>NPCMAX THEN @SPELLFAIL
 GOSUB @CASTDESCEFFECT
 ATTACKER=PLAYER
 GOTO @MAKEENEMIES

.CASTNKIL
 IF SPELL<>BOMSPELL THEN @CASTNBOM
; spell wakes up the target ?
 SPELLDURATION=0
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 X4=2 ; stirs feebly, then stops
 IF TARGET=BONES THEN @CASTEFFECT
 IF TARGET=SKULL THEN @CASTEFFECT
 IF TARGET=KNUCKLEBONE THEN @CASTEFFECT
 X4=4 ; wake cherub
 IF TARGET=CHERUB THEN @WAKECHERUB
 SPELLDURATION=10
 X4=5 ; wake idol
 IF TARGET=IDOL THEN @CASTDESCEFFECT
; X4=6 ; wake gargoyle
; IF TARGET=GARGOYLE THEN CASTDESCEFFECT
 X4=7 ; wake statue
 IF TARGET=STATUE THEN @CASTDESCEFFECT

 SPELLDURATION=0
 IF TARGET<NPCBASE THEN CASTBOM2 ; not a creature
 X4=8 ; that's already awake
 IF TARGET<GHOUL THEN @CASTEFFECT ; other NPC
 X4=3 ; Myglar's already done that
 IF TARGET<NPCMAXPLUSONE THEN @CASTEFFECT ; undead

.CASTBOM2
 X4=13 ; go to stonehenge message
 IF TARGET=STONEHENGEPICTURE THEN CASTSTONEHENGE
 X4=TARGET
 X1=116 ; stonehenge picture-13 - find message number
 SUB X4,X1
 X1=DARKSPAWN ; creature to waken
 IF TARGET=DARKSPAWNPICTURE THEN WAKEDARKSPAWN
 IF TARGET=MOONBEASTPICTURE THEN @CASTEFFECT
 X1=GHOUL ; creature to waken
 IF TARGET=GHOULPICTURE THEN WAKECREATURE
; X1=WEREWOLF ; creature to waken
; IF TARGET=WEREWOLFPICTURE THEN WAKECREATURE
 IF TARGET<>MYGLARPICTURE THEN CASTBOMNMYGLAR
 MESSAGE 3238 ; myglar taunts you
 RETURN

.CASTBOMNMYGLAR
 IF TARGET=WEREWOLFPICTURE THEN @CASTEFFECT
 IF TARGET<>WARGAME THEN CASTBOMNWARGAME
 SCOREINDEX=SCLEARNKIL
 GOSUB @ADDSCORE
 SCOREINDEX=SCLEARNZAP
 GOSUB @ADDSCORE
 X4=11 ; wargame message
 GOTO @CASTEFFECT

.CASTBOMNWARGAME
 X4=1 ; waking THAT is beyond your power
 IF TARGET<>TAPESTRY THEN @CASTEFFECT
 X4=12 ; healer message
 GOSUB @CASTEFFECT
 SCOREINDEX=SCLEARNFIX
 GOTO @ADDSCORE

.CASTSTONEHENGE
 GOSUB @CASTEFFECT
 DEST=30 ; go to stonehenge
 HIDEST=0
 GOTO @ABSNEWLOCATION

.WAKEDARKSPAWN
 GOSUB @WAKECREATURE
 X1=CURRENTPOS(CLAW) 
 IF X1<>0 THEN WAKEDSRET
 MESSAGE 3239 ; strikes at you, leaving a claw
 CURRENTPOS(CLAW)=ROOM
.WAKEDSRET
 RETURN

.WAKECREATURE
; waken creature X1
 CURRENTPOS(X1)=ROOM
 HICURRENTPOS(X1)=C0
 GOTO @CASTEFFECT

.CASTNBOM
 IF SPELL<>XAMSPELL THEN CASTNXAM
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 IF TARGET=CHEST THEN XAMCHEST
 X1=TARGET
 IF X1<>AXE THEN CASTXAMNAXE
 X1=6 ; axe is in weapons sequence, not focus sequence
.CASTXAMNAXE
 IF X1>STAFF THEN CASTNFOCUS
 MESSAGE 3242 ; that's for the
 X2=ABSSPELLBASE
 ADD X2,X1
 MESSAGE X2
 MESSAGE 3243 ; spell
 RETURN

.CASTNFOCUS
 X4=1 ; it's not magical
 IF TARGET<NPCBASE THEN @CASTEFFECT
 IF TARGET>DARKSPAWN THEN @CASTEFFECT
 X4=4 ; it's animated by powerful magik
 GOTO @CASTEFFECT

.XAMCHEST
 GOSUB ISCHESTMAGIK
 X4=6 ; the chest is enchanted
 IF RESULT=TRUE THEN @CASTEFFECT
 X4=1 ; it's not magikal
 GOTO @CASTEFFECT

.ISCHESTMAGIK
 IF INUMBER=NULLNUMBER THEN @WHICHONE
 X1=CHESTBASE
 ADD X1,INUMBER
 X2=CHESTTABLE(X1)
 RESULT=TRUE
 IF X2=CHESTMAGICAL THEN ICMRET
 IF X2=CHESTDESTROYED THEN @CANTSEETHAT
 RESULT=FALSE
.ICMRET
 RETURN

.CASTNXAM
 IF SPELL<>DETSPELL THEN CASTNDET
 SPELLDURATION=10
 IF TARGET=PLAYER THEN @CASTDESCEFFECT ; also known as WARD spell
 GOTO @SPELLFAIL

.CASTNDET
 IF SPELL<>SPYSPELL THEN CASTNSPY
; print description of room where object is
 IF TARGET>MAXOBJECT THEN @SPELLFAILNULL ; dissipates in mid air
 MESSAGE 3280 ; you see a vision of
 X4=TARGET
 GOSUB @SETUPROOMX4
 IF ROOM=0 THEN SPYNULL
 IF ROOM>250 THEN SPYNULL
 GOSUB @ABSDESCROOM
 GOTO @SETUPROOM

.SPYNULL
 MESSAGE 3281 ; nothingness
 GOTO @SETUPROOM

.CASTNSPY
 IF SPELL<>ZENSPELL THEN CASTNZEN
 DEST=90 ; maze entrance
 HIDEST=0
 MESSAGE 3300 ; are transported to another place
 GOTO @NEWLOCATION

.CASTNZEN
 IF SPELL<>ESPSPELL THEN CASTNESP
; have DIR as direction
; it is in range 0..15
 IF DIR=0 THEN @SPELLFAIL
 IF ESPROOM<>0 THEN @SPELLFAIL ; can't allow player to chain ESPs together
 FROM=ROOM
 GOSUB @CHECKEXIT
 X4=0 ; you see the inside of a wall
 IF DEST=0 THEN @CASTEFFECT
 X4=1 ; you see another room
 SPELLDURATION=2
 TARGET=PLAYER
 GOSUB @CASTEFFECT
 ESPROOM=DEST
 GOSUB @DESCRIBEROOM
 GOTO @CANCELINPUT

.CASTNESP
 SPELLDURATION=10
 IF SPELL<>FLYSPELL THEN CASTNFLY
 IF TARGET=PLAYER THEN FLYDESCEFFECT
 IF TARGET<NPCBASE THEN CASTFLYNNPC
 IF TARGET>NPCMAX THEN CASTFLYNNPC
 IF TARGET<BLOODWORM THEN CASTFLYCANT ; statues can't fly
 X4=9 ; NPC flies, is unhappy about it

.CASTANGER
 GOSUB FLYDESCEFFECT
 ATTACKER=PLAYER
 GOTO @MAKEENEMIES

.CASTFLYNNPC
 OBJECT=TARGET
 GOSUB @ISOBJECTMOVEABLE
 X4=5 ; 'floats upwards'
 IF RESULT=TRUE THEN FLYDESCEFFECT
 IF TARGET=REDMOON THEN FLYDESCEFFECT
 X4=8 ; stays where it is
 IF TARGET=WEIGHT THEN @CASTDESCEFFECT
.CASTFLYCANT
 X1=TARGET
 GOSUB @DESCTHEOBJX1
 X4=8 ; stays where it is
 ADD M1,X4
 MESSAGE M1
 MESSAGE DOT
 RETURN

.FLYDESCEFFECT
; TARGET is going up in the world. There are one or two
; details to attend to as it loosens its earthly bonds
; first, if it is contained by something, it won't be now
 CURRENTPOS(TARGET)=ROOM
 HICURRENTPOS(TARGET)=C0
; now do standard spell stuff

 GOTO @CASTDESCEFFECT
 
.CASTNFLY
 IF SPELL<>ZAPSPELL THEN CASTNZAP
 SPELLDURATION=0
 IF TARGET=PLAYER THEN CASTZAP
 IF TARGET<NPCBASE THEN @SPELLFAIL
 IF TARGET>NPCMAX THEN @SPELLFAIL
.CASTZAP
 X1=TARGET
 GOSUB @CONJUGATEM1X1
 GOSUB @RAND10
 ADD NUMBER,C1 ; make sure its never 0
 BLOWSTRENGTH=NUMBER
 GOSUB @CASTDESCEFFECT
 X4=0 ; M1=actual message number now (after CONJUGATEM1X1)
 GOTO @DODAMAGE

.CASTNZAP
 IF SPELL<>DEDSPELL THEN CASTNDED
; first dispell spells
 IF ROOM<>222 THEN CASTDED0
 X4=1 ; Myglar dies
 GOSUB @CASTEFFECT
 GOTO @WIN

.CASTDED0
 X4=0 ; general magik is dispelled
 GOSUB @CASTEFFECT
 SPELL=1
.CASTDED1
 GOSUB @SPELLWEARSOFF
 ADD SPELL,C1
 IF SPELL<19 THEN CASTDED1
 RETURN

.CASTNDED
 IF SPELL<>SANSPELL THEN CASTNSAN
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 X4=1
 IF TARGET<>MYGLAR THEN @SPELLFAIL
; Myglar draws his sword
 X1=MYGLAR
 CURRENTPOS(POIGNARD)=X1
 GOTO @CASTDESCEFFECT

.CASTNSAN
 IF SPELL<>IBMSPELL THEN CASTNIBM
; FEAR spell
 SPELLDURATION=10
 IF TARGET=PLAYER THEN @CASTDESCEFFECT
 X4=2 ; ants cower back
 IF TARGET=ANTS THEN CASTDESCEFFECT
 IF TARGET<NPCBASE THEN @SPELLFAIL
 X4=1 ; looks frightened
 IF TARGET<NPCMAXPLUSONE THEN CASTDESCEFFECT
 GOTO @SPELLFAIL

.CASTNIBM
 IF SPELL<>HYPSPELL THEN @SPELLFAIL
 SPELLDURATION=0
 IF TARGET=PLAYER THEN CASTDESCEFFECT
 X4=1 ; doesn't seem to understand
 IF TARGET=BLOODWORM THEN CASTDESCEFFECT
 IF TARGET=MOONBEAST THEN CASTDESCEFFECT
 IF TARGET=SPIDER THEN CASTDESCEFFECT
 IF TARGET=SLUG THEN CASTDESCEFFECT
 IF TARGET=ANTS THEN CASTDESCEFFECT
 X4=2 ; ferryman tells you to get lost
 IF TARGET=FERRYMAN THEN CASTDESCEFFECT
 X4=3 ; Myglar tells you ..
 IF TARGET=MYGLAR THEN CASTDESCEFFECT

 SPELLDURATION=10
 X4=4 ; looks at you expectantly
 IF TARGET=MONKEY THEN CASTDESCEFFECT
 IF TARGET=BAT THEN CASTDESCEFFECT
 IF TARGET=WEREWOLF THEN CASTDESCEFFECT
 IF TARGET=DARKSPAWN THEN CASTDESCEFFECT
; and finally ... NPCs who must be woken first
 IF TARGET=IDOL THEN CASTHYPSLEEPING
 IF TARGET<>STATUE THEN SPELLFAIL
.CASTHYPSLEEPING
 X1=SPELLTIME(BOMSPELLTARGET)
 IF X1=TARGET THEN CASTDESCEFFECT
 X1=TARGET
 GOSUB @DESCTHEOBJX1
 MESSAGE 3446 ; looks stony
.CASTEFFECTRET
 RETURN

.WAKECHERUB
 CURRENTPOS(CHERUB)=C0
 CURRENTPOS(TRUMPET)=ROOM
 HICURRENTPOS(TRUMPET)=C0

.CASTDESCEFFECT
; describe the target first
 X1=TARGET
 GOSUB @DESCTHEOBJX1

.CASTEFFECT
; describe the effect of the spell
 ADD M1,X4
 MESSAGE M1
 MESSAGE DOT
; GOSUB @NPCINTERESTED
; set up effect table
 IF SPELLDURATION=0 THEN CASTEFFECTRET
 SPELLTIME(SPELL)=SPELLDURATION ; time the spell lasts for
 X1=SPTARGETBASE
 ADD X1,SPELL
 SPELLTIME(X1)=TARGET
 RETURN
;---
.SPELLFAIL
; this spell isn't going to work with things as they are.
 IF TARGET=NULLOBJECT THEN SPELLFAILNULL
 X1=TARGET
; gives RESULT=CONJNOTHING, CONJA, CONJAN, CONJSOME
; as appropriate for X1
 GOSUB @DESCTHEOBJX1
 X1=TARGET
 GOSUB @CONJUGATEX1
 M1=3010 ; are unchanged etc.
 IF ANIMATE=TRUE THEN SPELLFAIL1
 M1=3000 ; is unchanged etc.
.SPELLFAIL1
 GOSUB @VARYMESSAGE
 MESSAGE DOT
 RETURN
.SPELLFAILNULL
 MESSAGE 3554 ; spell expends itself in mid-air
 RETURN
;---
.DIVIDEX1BY2
 X2=0
.DIVBY2
 SUB X1,C2
 IF X1>NEGATIVE THEN DIVBY2END
 ADD X2,C1
 GOTO DIVBY2
.DIVBY2END
 X1=X2
 RETURN
;---
.MULTX1BY10
 ADD X1,X1
 X2=X1
 ADD X2,X2
 ADD X2,X2
 ADD X1,X2
 RETURN
;---
.ISOBJECTMAGIC
; return RESULT=TRUE if OBJECT is magical
 IF OBJECT=CHEST THEN @ISCHESTMAGIK
 RESULT=TRUE
 IF OBJECT=PLAYER THEN IOMRET
 IF OBJECT<19 THEN IOMRET ; spell focus
 IF OBJECT=TALISMAN THEN IOMRET
 IF OBJECT=CLAYMORE THEN IOMRET
 IF OBJECT<MANDRAKE THEN IOM1
 IF OBJECT<SHOVEL THEN IOMRET ; mandrake,eyebright,wolfsbane,black ball
.IOM1
 IF OBJECT=REDMOON THEN IOMRET
 IF OBJECT=MYGLAR THEN IOMRET
 IF OBJECT=MOONBEAST THEN IOMRET
; IF OBJECT=VAMPIRE THEN IOMRET
 IF OBJECT=ANTS THEN IOMRET
 IF OBJECT=BAT THEN IOMRET
; maybe should have tests for animated objects ?
 RESULT=FALSE
.IOMRET
 RETURN
;---
;.NPCINTERESTED
;; if OBJECT is an NPC, it has done something interesting
; IF OBJECT<NPCBASE THEN NPCINTRET
; IF OBJECT>NPCMAX THEN NPCINTRET
; GOSUB @GETNPCATTRIBUTES
; X1=BOREDOFFSET
; ADD X4,X1
; NPCCURRENT(X4)=C0
;.NPCINTRET
; RETURN
;---
.GHOSTAPPEARS
; make ghost appear, if not here alrea`y
 X1=CURRENTPOS(GHOST)
 IF X1=ROOM THEN GHOSTAPRET
 IF X1=MURDERED THEN GHOSTAPRET
 LASTWORDPRINTED=GHOST
 MESSAGE 3575 ; ghost appears
 CURRENTPOS(GHOST)=ROOM
 HICURRENTPOS(GHOST)=C0
.GHOSTAPRET
 RETURN
;---
.AREBONESBURIED
; return RESULT=TRUE f bones all buried
; or FALSE and OBJECT=object not buried
 RESULT=FALSE
 OBJECT=BONES
 X1=CURRENTPOS(BONES)
 IF X1<>BURIED THEN ABBNO
 OBJECT=KNUCKLEBONE
 X1=CURRENTPOS(KNUCKLEBONE)
 IF X1<>BURIED THEN ABBNO
 OBJECT=SKULL
 X1=CURRENTPOS(SKULL)
 IF X1<>BURIED THEN ABBNO
 RESULT=TRUE
.ABBNO
 RETURN
;---
.ATTACKANTS
 X1=ATTACKER
 GOSUB @DESCTHEOBJX1
 X1=ATTACKER
 M1=3523 ; kill a couple of ants
 GOSUB CONJUGATEM1X1
 MESSAGE M1
 BLOWSTRENGTH=2
 GOTO @DODAMAGE
;---
.CONJUGATEM1X1
; the NPC (or player) X1 is doing M1='TAKE' etc, M1+1='TAKES' etc.
 IF X1=USER THEN CONJRET
 IF X1=ANTS THEN CONJRET
 ADD M1,C1
.CONJRET
 RETURN
;---
.CHECKIFANTSPREVENT
; RESULT=TRUE if ants prevent you 
; and print message 
 RESULT=FALSE
 X1=CURRENTPOS(ANTS)
 IF X1<>ROOM THEN CONJRET
; M1=2652 ; ants cower back
 X1=SPELLTIME(IBMSPELLTARGET)
 IF X1=ANTS THEN CONJRET
; near ants - you floating ?
 M1=2651 ; you float over the ants
 X1=SPELLTIME(FLYSPELLTARGET)
 IF X1=PLAYER THEN CIAPPRINT
 RESULT=TRUE ; prevent action
 M1=2650 ; ants block you
.CIAPPRINT
 GOTO @PRINTM1
;---
.RAMSAVE
; single position only at present
 GOSUB @GETNEXTWORD ; see if there is a second word giving load/save
 SEARCHTYPE=VERBTYPE
 GOSUB @CHECKTYPE
 IF VALUE=IRESTORE THEN RAMLOAD

 X2=0 ; first position
 GOSUB RAMWRITE
.RAMSAVEEND
 IF RESULT<>0 THEN RAMERROR
 GOTO @DONE
;---
.RAMLOAD
; single position only at present
 X2=0 ; first position
 GOSUB RAMREAD
 IF RESULT<>0 THEN RAMERROR
 GOTO @AFTERRESTORE
;---
.INITOOPS
; count number of oops positions available
 X2=0
 GOSUB RAMWRITE
 IF RESULT<>0 THEN INITOOPSRET
 MESSAGE 2309 ; ram and oops available
; now go through and write 'certified garbage' to all positions
 C2=65000 ; show that positions are invalid
 X2=0
.INITOOPS1
 GOSUB RAMWRITE
 ADD X2,C1
 IF RESULT=0 THEN INITOOPS1
 C2=2 ; restore C2
.INITOOPSRET
 RETURN
;---
;.INITOOPS1
; GOSUB RAMWRITE
; IF RESULT<>0 THEN INITOOPS3
; ADD X2,C1
; GOTO INITOOPS1
;
;;.INITOOPS3
;; X2 oops positios availble
; IF X2<>0 THEN INITOOPS4
; MESSAGE 2308 ; oops and ramsave not available
; RETURN

;.INITOOPS4
; PRINT X2 ; number of positions;
; MESSAGE 2309 ; available for use
; RETURN
;---
.NOPOSITION
 MESSAGE 2308 ; no oops position found
 RETURN
;---
.OOPS
; OOPSPOS points to next free position in ram save area
; go back some positions if they are available
 IF OOPSPOS<>1 THEN OOPS2
; have gone to position 0, so need to wrap round to end of ram area
 IF OOPSPOSEND=0 THEN NOPOSITION ; something wrong here
 OOPSPOS=OOPSPOSEND ; first invalid position

.OOPS2
 SUB OOPSPOS,C1
 X2=OOPSPOS
 GOSUB RAMREAD
 LASTPICTURE=0
 GOSUB @DESCRIBEROOM
 GOTO @WHATNOW ; ?? surely this is not the right way to do it ?

.RAMERROR
 MESSAGE 2300 ; ram error
 RETURN
;---
.SAVEOOPS
; OOPSPOS points to the next free position in ram save area
; this routine called on EVERY movement
; try writing to current position

 X2=OOPSPOS
 GOSUB RAMWRITE
 IF RESULT=0 THEN SAVEOOPSOK ; fine, inc pointer and finish
; something went wrong.
; if we are in the first position, tell the user
; that it is not available here
 IF OOPSPOS<2 THEN SAVEOOPSRET ; keep quiet, ignore the error
; ok, so have some saved positions. Rotate round cyclically
 OOPSPOSEND=OOPSPOS ; save first invalid position
 SUB OOPSPOS,C1
 OOPSPOS=1
 GOTO SAVEOOPS

.SAVEOOPSOK
 ADD OOPSPOS,C1
.SAVEOOPSRET
 RETURN
;---
.RAMREAD
 X1=23 ; code for ramload
 GOSUB RAMOPERATION
 IF C2<>2 THEN FATALERROR
 RETURN
;---
.RAMWRITE
 X1=22 ; code for ramsave
; drop through to RAMOPERATION

.RAMOPERATION
; write to position X2, return RESULT=0 if OK
 LINPUT(0)=X1
 LINPUT(1)=X2 ; position number
 LINPUT(2)=C0
 DRIVER
 RESULT=LINPUT(0)
 RETURN
;
.FATALERROR
 MESSAGE 2310 ; fatal error
 GOTO @QUITPLAYAGAIN ; ask player to press space, then restart
;---
;--------------------------------------------------------
;---------------------------------------------------------
;-------------------
